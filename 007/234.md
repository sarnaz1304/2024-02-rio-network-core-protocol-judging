Overt Rouge Porcupine

medium

# Assets for which `thirdPartyTransfersForbidden` is enabled in Eigenlayer's StrategyManager will become trapped within the protocol

## Summary
When the `RioLRTOperatorDelegator` initiates the withdrawal process in Eigenlayer by calling `delegationManager.queueWithdrawals()`, it assigns an address other than its own to the `withdrawer` field of `QueuedWithdrawalParams`. Consequently, any asset for which Eigenlayer's `StrategyManager.thirdPartyTransfersForbidden` is set to true cannot be withdrawn from the protocol, resulting in funds becoming permanently inaccessible.

## Vulnerability Detail
When the protocol needs to deallocate funds staked in Eigenlayer (e.g during rebalance), the operators' `RioLRTOperatorDelegator` contracts will call Eigenlayer's `delegationManager.queueWithdrawalForUserSettlement` (or if `queueWithdrawalForOperatorExit`  for operators exits) to queue withdrawal of funds. See the `RioLRTOperatorDelegator._queueWithdrawal` code below, it creates the `QueuedWithdrawalParams` struct and then calls `delegationManager.queueWithdrawals`.

```solidity
function queueWithdrawalForUserSettlement(address strategy, uint256 shares) external onlyCoordinator returns (bytes32 root) {
    if (strategy == BEACON_CHAIN_STRATEGY) {
        _increaseETHQueuedForUserSettlement(shares);
    }
    root = _queueWithdrawal(strategy, shares, address(withdrawalQueue()));
}

function _queueWithdrawalForOperatorExitOrScrape(address strategy, uint256 shares) internal returns (bytes32 root) {
    if (strategy == BEACON_CHAIN_STRATEGY) {
        _increaseETHQueuedForOperatorExitOrScrape(shares);
    }
    root = _queueWithdrawal(strategy, shares, address(depositPool()));
}

function _queueWithdrawal(address strategy, uint256 shares, address withdrawer) internal returns (bytes32 root) {
    IDelegationManager.QueuedWithdrawalParams[] memory withdrawalParams = new IDelegationManager.QueuedWithdrawalParams[](1);
    withdrawalParams[0] = IDelegationManager.QueuedWithdrawalParams({
        strategies: strategy.toArray(),
        shares: shares.toArray(),
        withdrawer: withdrawer
    });
    root = delegationManager.queueWithdrawals(withdrawalParams)[0];
}
```

As we can see from the code above,  the `withdrawer` field is set to `withdrawalQueue` address (or `depositPool` address for operators exits). However as noted in Eigenlayer's M2 [documentation](https://github.com/Layr-Labs/eigenlayer-contracts/blob/dev/docs/core/DelegationManager.md#queuewithdrawals) "Note that the `QueuedWithdrawalParams` struct has a `withdrawer` field. Originally, this was used to specify an address that the withdrawal would be credited to once completed. However, `queueWithdrawals` now requires that `withdrawer == msg.sender`. Any other input is rejected.".  This can be problematic as the `msg.sender` for the `delegationManager.queueWithdrawals` call is the Operator's `OperatorDelegator` contract, but the `withdrawer` field is not set to its address. Therefore for assets which Eigenlayer's `StrategyManager.thirdPartyTransfersForbidden` is set to true, the call to `delegationManager.queueWithdrawals()` will revert because of this aforementioned condition (see Eigenlayer M2 [DelegateManager](https://github.com/Layr-Labs/eigenlayer-contracts/blob/6de01c6c16d6df44af15f0b06809dc160eac0ebf/src/contracts/core/DelegationManager.sol#L703-L705)).

As a result of this issue, if any asset used by the Rio Network has `thirdPartyTransfersForbidden` enabled (or will be enabled in the future), funds become trapped in the protocol. Withdrawal attempts from operators will consistently fail, rendering it impossible to retrieve funds from Eigenlayer.

This issue is not perceived during testing as in the test suite the contracts used does not match the ones from [v0.2.1-goerli-m2](https://github.com/Layr-Labs/eigenlayer-contracts/tree/v0.2.1-goerli-m2/src/contracts)

## Impact
For assets that `thirdPartyTransfersForbidden` is set to true, it will be impossible to withdraw funds from Eigenlayer. Making all funds deposited for this asset stuck in the protocol.

## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorDelegator.sol#L213-L273

## Tool used
Manual Review

## Recommendation
To address this issue, consider modifying the `RioLRTOperatorDelegator` contract to initially withdraw funds to itself, ensuring that `msg.sender` matches with the `withdrawer` field. Subsequently, the funds can be relayed to the intended target contract (`depositPool` or `withdrawalQueue`). This modification ensures compatibility with all tokens, including those with potential future activation of `thirdPartyTransfersForbidden`.