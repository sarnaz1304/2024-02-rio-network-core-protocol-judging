Rough Golden Dog

high

# If `RioLRTOperatorRegistry:setOperatorStrategyShareCaps` is used to set the cap to zero, fund may be locked

## Summary

The function `RioLRTOperatorRegistry:setOperatorStrategyShareCaps` calls on `s.setOperatorStrategyShareCaps(operatorId, newStrategyShareCaps)`, which is implemented in `OperatorRegistryV1Admin.sol`. The function then uses `setOperatorStrategyCap` in the same file.

The `OperatorRegistryV1Admin:setOperatorStrategyCap` function makes `utilizationHeap` by calling `s.getOperatorUtilizationHeapForStrategy()` (line 245 below).

The way `utilizationHeap` is made is either the index goes beyond `s.activeOperatorCount` or `operatorId` is zero (`getOperatorUtilizationHeapForStrategy` function).

Also note that the `store` (in the line 264 below) will not zero out the operatorId if the `utilizationHeap` should shrink.

If the `utilizationHeap` should expand of shrink the `s.activeOperatorCount` should properly updated. In the case of `addOperator` and `deactivateOperator`, it is properly done. However, for the `setOperatorStrategyCap` it is not done. This functions also does not check whether the cap was updated from non-zero to zero or vice versa.

Given it is `onlyOwner`, the owner should know that they should never use `setOperatorStrategyCap` to change the cap between zero and non-zero values (there is no documents about this though). However, the damage is severe and irreversable if this mistake is made, therefore access this bug as High severity.

```solidity
// utils/OperatorRegistryV1Admin.sol
245         OperatorUtilizationHeap.Data memory utilizationHeap = s.getOperatorUtilizationHeapForStrategy(newShareCap.strategy);
246
247         // If the current cap is greater than 0 and the new cap is 0, remove the operator from the strategy.
248         if (currentShareDetails.cap > 0 && newShareCap.cap == 0) {
249             // If the operator has allocations, queue them for exit.
250             if (currentShareDetails.allocation > 0) {
251                 operatorDetails.queueOperatorStrategyExit(operatorId, newShareCap.strategy);
252             }
253             // Remove the operator from the utilization heap.
254             utilizationHeap.removeByID(operatorId);
255         } else if (currentShareDetails.cap == 0 && newShareCap.cap > 0) {
256             // If the current cap is 0 and the new cap is greater than 0, insert the operator into the heap.
257             utilizationHeap.insert(OperatorUtilizationHeap.Operator(operatorId, 0));
258         } else {
259             // Otherwise, update the operator's utilization in the heap.
260             utilizationHeap.updateUtilizationByID(operatorId, currentShareDetails.allocation.divWad(newShareCap.cap));
261         }
262
263         // Persist the updated heap to the active operators tracking.
264         utilizationHeap.store(s.activeOperatorsByStrategyShareUtilization[newShareCap.strategy]);
```


## Vulnerability Detail

https://gist.github.com/lemonmon1984/1c8cbc18b97f7e49ef0dce2c877e070d
The gist above contains two test cases. Here are the relevant parts:


```solidity
    function test_allocateStrategySharesZeroCapBySetOperatorStrategyBug_poc() public {
        uint128 STRATEGY_CAP = 40e18;

        // Add 2 operator delegators
        IRioLRTOperatorRegistry.StrategyShareCap[] memory strategyShareCaps =
            new IRioLRTOperatorRegistry.StrategyShareCap[](1);
        strategyShareCaps[0] = IRioLRTOperatorRegistry.StrategyShareCap({strategy: CBETH_STRATEGY, cap: STRATEGY_CAP});

        addOperatorDelegators(reLST.operatorRegistry, address(reLST.rewardDistributor), 2, strategyShareCaps, 0);

        // second operator's cap was set to zero using setOperatorStrategyShareCaps
        uint8 operatorId = 2;
        IRioLRTOperatorRegistry.StrategyShareCap[] memory strategyShareCapsUpdate =
            new IRioLRTOperatorRegistry.StrategyShareCap[](1);
        strategyShareCapsUpdate[0] = IRioLRTOperatorRegistry.StrategyShareCap({strategy: CBETH_STRATEGY, cap: 0});
        reLST.operatorRegistry.setOperatorStrategyShareCaps(operatorId, strategyShareCapsUpdate);

        // Try to allocate But revert with DivWadFailed
        uint256 TOTAL_ALLOCATION = STRATEGY_CAP * 1;

        vm.expectRevert(FixedPointMathLib.DivWadFailed.selector);
        vm.prank(address(reLST.depositPool));
        (uint256 sharesAllocated, IRioLRTOperatorRegistry.OperatorStrategyAllocation[] memory allocations) =
            reLST.operatorRegistry.allocateStrategyShares(CBETH_STRATEGY, TOTAL_ALLOCATION);
    }
```

Case 1: No more allocation and deallocation can be done.
The bug's severity depends on the index of the operator, which the cap was set to zero.
The above case is where as soon as the operator's cap is set to zero no more allocation is possible.
Given the deallocation uses similar logic, it would not be possible to deallocate as well.
1. 2 operators delegators are added
1. the second operator's cap was set to zero using `setOperatorStrategyShareCaps`
1. an attempt of allocation will revert with DivWadFailed

The reason of revert is because the operator with zero cap is not excluded from the `utilizationHeap`.
So, the heap is trying to sort including the operator with zero cap, and fails with `DivWadFailed`.

```solidity
    function test_allocateStrategySharesSetZeroCapThenAddOperator_poc() public {
        uint128 STRATEGY_CAP = 40e18;

        // Add 2 operator delegators
        IRioLRTOperatorRegistry.StrategyShareCap[] memory strategyShareCaps =
            new IRioLRTOperatorRegistry.StrategyShareCap[](1);
        strategyShareCaps[0] = IRioLRTOperatorRegistry.StrategyShareCap({strategy: CBETH_STRATEGY, cap: STRATEGY_CAP});

        addOperatorDelegators(reLST.operatorRegistry, address(reLST.rewardDistributor), 2, strategyShareCaps, 0);

        // set the cap of operator 1 to 0 using setOperatorStrategyShareCaps
        uint8 operatorId = 1;
        IRioLRTOperatorRegistry.StrategyShareCap[] memory strategyShareCapsUpdate =
            new IRioLRTOperatorRegistry.StrategyShareCap[](1);
        strategyShareCapsUpdate[0] = IRioLRTOperatorRegistry.StrategyShareCap({strategy: CBETH_STRATEGY, cap: 0});

        reLST.operatorRegistry.setOperatorStrategyShareCaps(operatorId, strategyShareCapsUpdate);

        strategyShareCapsUpdate[0] = IRioLRTOperatorRegistry.StrategyShareCap({strategy: CBETH_STRATEGY, cap: 40e18});

        // set back the operator 1 back to the original cap
        reLST.operatorRegistry.setOperatorStrategyShareCaps(operatorId, strategyShareCapsUpdate);

        // or add a new Operator
        address operator = address(uint160(3));

          vm.prank(operator);
          delegationManager.registerAsOperator(
              IDelegationManager.OperatorDetails({
                  earningsReceiver: address(reLST.rewardDistributor),
                  delegationApprover: address(0),
                  stakerOptOutWindowBlocks: 0
              }),
              metadataURI
          );

          reLST.operatorRegistry.addOperator(
              IRioLRTOperatorRegistry.OperatorConfig({
                  operator: operator,
                  initialManager: address(this),
                  initialEarningsReceiver: address(this),
                  initialMetadataURI: metadataURI,
                  strategyShareCaps: strategyShareCapsUpdate,
                  validatorCap: 0
              })
          );

        // wants to allocate 80e18
        uint256 TOTAL_ALLOCATION = STRATEGY_CAP * 2;

        vm.prank(address(reLST.depositPool));
        (uint256 sharesAllocated, IRioLRTOperatorRegistry.OperatorStrategyAllocation[] memory allocations) =
            reLST.operatorRegistry.allocateStrategyShares(CBETH_STRATEGY, TOTAL_ALLOCATION);
        // Only half can be allocated (40e18)
        assertEq(sharesAllocated, STRATEGY_CAP);
        assertEq(allocations.length, 1);

        for (uint256 i = 0; i < allocations.length; i++) {
            assertEq(allocations[i].shares, STRATEGY_CAP);
            assertEq(allocations[i].tokens, STRATEGY_CAP);
        }
    }
```

Case 2: allocation is locked
In the case above, the allocation will not revert, but it behaves incorrectly.
It seems like it is not possible to recover from this state.

1. two operator delegators were added.
1. operator 1's cap was set to be zero
Since the `s.activeOperatorCount` is still 2, next time when the `utilizationHeap` is made, it will be [operator2, operator2].
The second operator2 was the original entry which was not zeroed out.
1. the cap of the operator 1 was back to non-zero (to demonstrate it will not recover the state)
1. a new operator was added (to demonstrate that it will not recover the state)
1. when it goes through the loop, the first operator2 will be allocated, then the second operator2 will be over the cap then it will break from the loop.

Even if the operator's cap is set back to non-zero, or a new operator is added it will not recover from the state.


## Impact

If `RioLRTOperatorRegistry:setOperatorStrategyShareCaps` is used to set the cap to zero, the allocation and deallocation logic will behave inproperly.
Depending on the index, it may freeze the fund, by make it impossible to call the deallocation function.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L156-L158

## Tool used

Manual Review

## Recommendation

Consider check for non-zero to zero transition of the cap in `RioLRTOperatorRegistry:setOperatorStrategyShareCaps`

