Fast Chili Trout

high

# The `allocation` of shares has to be deleted, if `old cap > 0 && new cap == 0` in `OperatorRegistryV1Admin.setOperatorStrategyCap()`.


## Summary
In `OperatorRegistryV1Admin.setOperatorStrategyCap()`, `queueOperatorStrategyExit()` is called and `allocation` is not deleted, when `currentShareDetails.cap > 0 && newShareCap.cap == 0`. It may lead to storing an incorrect value of utilization of the operator registry, if `currentShareDetails.cap == 0 && newShareCap.cap > 0` in the subsequent calling of `OperatorRegistryV1Admin.setOperatorStrategyCap()`.
This also happens when a deactivated operator is activated again.

## Vulnerability Detail

When `allocateStrategyShares()` allocates shares, it finds the minimum utilization operator and validates `allocation` which isn't reset while deleting the operator.

```javascript
    function allocateStrategyShares(address strategy, uint256 sharesToAllocate) external onlyDepositPool returns (uint256 sharesAllocated, OperatorStrategyAllocation[] memory allocations) {
        OperatorUtilizationHeap.Data memory heap = s.getOperatorUtilizationHeapForStrategy(strategy);
        [...]
        while (remainingShares > 0) {
@>353       uint8 operatorId = heap.getMin().id;

            OperatorDetails storage operator = s.operatorDetails[operatorId];
            OperatorShareDetails memory operatorShares = operator.shareDetails[strategy];

            // If the allocation of the operator with the lowest utilization rate is maxed out,
            // then exit early. We will not be able to allocate to any other operators.
@>360       if (operatorShares.allocation >= operatorShares.cap) break;

            [...]
    }
```

Here is a possible scenario.
- An operator has 100 cap and 50 allocations.
- The cap of this operator is set to 0 using `setOperatorStrategyCap()` and it's removed from the heap. But `operatorShares.allocation` is still 50.
- After that, the operator is added again with 50 cap and it will be added to the heap with 0 utilization(but 50 allocation).
- Then in `allocateStrategyShares()`, this operator might have a min id and the while loop will break unexpectedly as `operatorShares.allocation >= operatorShares.cap`.

## Impact

The allocation logic with the utilization heap will be broken as some operators might have old allocations.

As a result, 
- Users cannot deposit some underlying assets, even though there are some empty operators.
- Withdrawal of some underlying assets will be reverted even if there are left underlying assets.

## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L251

## Tool used
Manual Review


## Recommendation
`setOperatorStrategyCap()` should reset the deleted operator's allocation.

```diff
        
    function setOperatorStrategyCap(
        RioLRTOperatorRegistryStorageV1.StorageV1 storage s,
        uint8 operatorId,
        IRioLRTOperatorRegistry.StrategyShareCap memory newShareCap
    ) internal {
        IRioLRTOperatorRegistry.OperatorDetails storage operatorDetails = s.operatorDetails[operatorId];
        if (operatorDetails.delegator == address(0)) revert IRioLRTOperatorRegistry.INVALID_OPERATOR_DELEGATOR();

        IRioLRTOperatorRegistry.OperatorShareDetails memory currentShareDetails = operatorDetails.shareDetails[newShareCap.strategy];

        // If the new cap is the same as the current, no update is necessary.
        if (currentShareDetails.cap == newShareCap.cap) {
            return;
        }
        OperatorUtilizationHeap.Data memory utilizationHeap = s.getOperatorUtilizationHeapForStrategy(newShareCap.strategy);

        // If the current cap is greater than 0 and the new cap is 0, remove the operator from the strategy.
        if (currentShareDetails.cap > 0 && newShareCap.cap == 0) {
            // If the operator has allocations, queue them for exit.
            if (currentShareDetails.allocation > 0) {
                operatorDetails.queueOperatorStrategyExit(operatorId, newShareCap.strategy);
+               operatorDetails.shareDetails[newShareCap.strategy].allocation = 0;
            }
            // Remove the operator from the utilization heap.
            utilizationHeap.removeByID(operatorId);
        } else if (currentShareDetails.cap == 0 && newShareCap.cap > 0) {
            // If the current cap is 0 and the new cap is greater than 0, insert the operator into the heap.
            utilizationHeap.insert(OperatorUtilizationHeap.Operator(operatorId, 0));
        } else {
            // Otherwise, update the operator's utilization in the heap.
            utilizationHeap.updateUtilizationByID(operatorId, currentShareDetails.allocation.divWad(newShareCap.cap));
        }

        // Persist the updated heap to the active operators tracking.
        utilizationHeap.store(s.activeOperatorsByStrategyShareUtilization[newShareCap.strategy]);

        // Update the share cap in the operator details.
        operatorDetails.shareDetails[newShareCap.strategy].cap = newShareCap.cap;

        emit IRioLRTOperatorRegistry.OperatorStrategyShareCapSet(operatorId, newShareCap.strategy, newShareCap.cap);
    }
```