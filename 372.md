Wild Hemp Sealion

medium

# Reward distribution can be frontrun or sandwich attacked

## Summary
In some conditions attackers can receive the most part of rewards by the reward distribution frontrunning or sandwiching.  

## Vulnerability Detail
When a user deposits assets they are collected at the `RioLRTDepositPool` until the `RioLRTCoordinator.rebalance` is executed.
```solidity
    function deposit(address asset, uint256 amountIn) external checkDeposit(asset, amountIn) returns (uint256 amountOut) {
        // Convert deposited asset amount to restaking tokens.
        amountOut = convertFromAssetToRestakingTokens(asset, amountIn);


        // Pull tokens from the sender to the deposit pool.
        IERC20(asset).safeTransferFrom(msg.sender, address(depositPool()), amountIn);


        // Mint restaking tokens to the caller.
        token.mint(msg.sender, amountOut);


        emit Deposited(msg.sender, asset, amountIn, amountOut);
    }


    function _depositETH() internal checkDeposit(ETH_ADDRESS, msg.value) returns (uint256 amountOut) {
        // Convert deposited ETH to restaking tokens and mint to the caller.
        amountOut = convertFromUnitOfAccountToRestakingTokens(msg.value);


        // Forward ETH to the deposit pool.
        address(depositPool()).transferETH(msg.value);


        // Mint restaking tokens to the caller.
        token.mint(msg.sender, amountOut);


        emit Deposited(msg.sender, ETH_ADDRESS, msg.value, amountOut);
    }
```
Then user can instantly request withdrawal but actually asset receiving will be also delayed until the `RioLRTCoordinator.rebalance` be executed.
```solidity
    function requestWithdrawal(address asset, uint256 amountIn) external checkWithdrawal(asset, amountIn) returns (uint256 sharesOwed) {
        // Determine the amount of shares owed to the withdrawer using the current exchange rate.
        sharesOwed = convertToSharesFromRestakingTokens(asset, amountIn);


        // If requesting ETH, reduce the precision of the shares owed to the nearest Gwei,
        // which is the smallest unit of account supported by EigenLayer.
        if (asset == ETH_ADDRESS) sharesOwed = sharesOwed.reducePrecisionToGwei();


        // Pull restaking tokens from the sender to the withdrawal queue.
        token.safeTransferFrom(msg.sender, address(withdrawalQueue()), amountIn);


        // Ensure there are enough shares to cover the withdrawal request, and queue the withdrawal.
        uint256 availableShares = assetRegistry().convertToSharesFromAsset(asset, assetRegistry().getTotalBalanceForAsset(asset));
        if (sharesOwed > availableShares - withdrawalQueue().getSharesOwedInCurrentEpoch(asset)) {
            revert INSUFFICIENT_SHARES_FOR_WITHDRAWAL();
        }
        withdrawalQueue().queueWithdrawal(msg.sender, asset, sharesOwed, amountIn);
    }
```
When the `RioLRTCoordinator.rebalance` is executed an available asset balance of the `RioLRTDepositPool` contract is used to cover the current epoch withdrawals. In case the balance is sufficient the withdrawal settlement goes by the short way.
```solidity
    function rebalance(address asset) external checkRebalanceDelayMet(asset) {
        if (!assetRegistry().isSupportedAsset(asset)) revert ASSET_NOT_SUPPORTED(asset);
        if (msg.sender != tx.origin) revert CALLER_MUST_BE_EOA();


        // Process any outstanding withdrawals using funds from the deposit pool and EigenLayer.
        uint256 sharesOwed = withdrawalQueue().getSharesOwedInCurrentEpoch(asset);
        if (sharesOwed > 0) {
            _processUserWithdrawalsForCurrentEpoch(asset, sharesOwed);
        }
      
    function _processUserWithdrawalsForCurrentEpoch(address asset, uint256 sharesOwed) internal {
        IRioLRTWithdrawalQueue withdrawalQueue_ = withdrawalQueue();
        (uint256 assetsSent, uint256 sharesSent) = depositPool().transferMaxAssetsForShares(
            asset,
            sharesOwed,
            address(withdrawalQueue_)
        );
        uint256 sharesRemaining = sharesOwed - sharesSent;


        // Exit early if all pending withdrawals were paid from the deposit pool.
        if (sharesRemaining == 0) {
            withdrawalQueue_.settleCurrentEpoch(asset, assetsSent, sharesSent);
            return;
        }
```
Rebalancing can be delayed until a new deposit or a new withdrawal.
```solidity
        (uint256 sharesReceived, bool isDepositCapped) = depositPool().depositBalanceIntoEigenLayer(asset);
        if (sharesOwed == 0 && sharesReceived == 0) {
            revert NO_REBALANCE_NEEDED();
        }
```
In these conditions the attacker can sandwich attack the rewards distribution with a large amount of assets and instantly withdraw assets back by calling `RioLRTCoordinator.rebalance`.


##  Impact
Users losses due to rewards stealing.


## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L254-L258
## Tool used

Manual Review

## Recommendation
Consider using the `RioLRTDepositPool` tracking exchange rate of the liquid restaking token and using assets balance of the `RioLRTDepositPool` contract for withdrawals in order of withdrawal queue without short ways.