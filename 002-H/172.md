Energetic Turquoise Quail

high

# Epoch can be settled after queueing withdrawals from EigenLayer

## Summary

When requested withdrawals exceed the balance in the DepositPool contract, rebalancing is not immediately possible, so deposits are withdrawn from EigenLayer. This takes up to 7 days. However, after 1 day, RioLRTCoordinator::rebalance() can be called again. If the balance in the DepositPool increased during this period and is greater than the requested withdrawals, the epoch is settled and it's not possible to settle the previously requested withdrawals from EigenLayer, resulting in the loss of these assets.

## Vulnerability Detail

This vulnerability arises from the [rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) function not correctly handling new calls after a previous rebalance triggered EigenLayer's withdrawals:

```solidity
    function _checkRebalanceDelayMet(address asset) internal view {
        if (block.timestamp < assetNextRebalanceAfter[asset]) revert REBALANCE_DELAY_NOT_MET();
    }

    function rebalance(address asset) external checkRebalanceDelayMet(asset) {
        if (!assetRegistry().isSupportedAsset(asset)) revert ASSET_NOT_SUPPORTED(asset);
        if (msg.sender != tx.origin) revert CALLER_MUST_BE_EOA();

        // Process any outstanding withdrawals using funds from the deposit pool and EigenLayer.
        uint256 sharesOwed = withdrawalQueue().getSharesOwedInCurrentEpoch(asset);
        if (sharesOwed > 0) {
            _processUserWithdrawalsForCurrentEpoch(asset, sharesOwed);
        }

        // Deposit remaining assets into EigenLayer.
        (uint256 sharesReceived, bool isDepositCapped) = depositPool().depositBalanceIntoEigenLayer(asset);
        if (sharesOwed == 0 && sharesReceived == 0) {
            revert NO_REBALANCE_NEEDED();
        }
        if (sharesReceived > 0) {
            if (asset == ETH_ADDRESS) {
                assetRegistry().increaseUnverifiedValidatorETHBalance(sharesReceived);
            } else {
                assetRegistry().increaseSharesHeldForAsset(asset, sharesReceived);
            }
        }

        // When the deposit is not capped, the rebalance is considered complete, and the asset rebalance
        // timestamp is increased by the specified delay. If capped, the asset may be rebalanced again
        // immediately as there are more assets to deposit.
        if (!isDepositCapped) {
            assetNextRebalanceAfter[asset] = uint40(block.timestamp) + rebalanceDelay;
        }
        emit Rebalanced(asset);
    }
```

Note that rebalance() can be called again as soon as `block.timestamp >= assetNextRebalanceAfter[asset]`, which is sooner than EigenLayer's withdrawal process delay, and `withdrawalQueue().getSharesOwedInCurrentEpoch(asset)` will still return the same value or greater.

If the DepositPool balance still can't cover the shares owed, rebalance() will revert trying to account for a repeated EigenLayer withdrawal settlement, as intended. This is possible by checking whether the [aggregateRoot](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L188) has been set for the current epoch.

However, there's the possibility that new deposits are made while the contract is waiting for the EigenLayer's withdrawals to complete. If the DepositPool balance becomes greater than the sharesOwed's valuation, the Coordinator contract [attempts](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L247-L258) to settle the epoch again through settleCurrentEpoch():

```solidity
    (uint256 assetsSent, uint256 sharesSent) = depositPool().transferMaxAssetsForShares(
        asset,
        sharesOwed,
        address(withdrawalQueue_)
    );
    uint256 sharesRemaining = sharesOwed - sharesSent;

    // Exit early if all pending withdrawals were paid from the deposit pool.
    if (sharesRemaining == 0) {
        withdrawalQueue_.settleCurrentEpoch(asset, assetsSent, sharesSent);
        return;
    }
```

So, the balance held in the DepositPool contract is sent once more to the WithdrawalQueue contract, `settleCurrentEpoch()` doesn't revert because `aggregateRoot` is not checked, the current epoch is settled and tokens are burnt for the second time for the same accounted `sharesOwed`. In addition, `assetsReceived` and `shareValueOfAssetsReceived` get overwritten. 

```solidity
    function settleCurrentEpoch(address asset, uint256 assetsReceived, uint256 shareValueOfAssetsReceived)
        external
        onlyCoordinator
    {
        uint256 currentEpoch = getCurrentEpoch(asset);

        EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
        if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
        if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();

        epochWithdrawals.settled = true;
        epochWithdrawals.assetsReceived = SafeCast.toUint120(assetsReceived);
        epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(shareValueOfAssetsReceived);

        token.burn(epochWithdrawals.amountToBurnAtSettlement);
        currentEpochsByAsset[asset] += 1;

        emit EpochSettledFromDepositPool(currentEpoch, asset, assetsReceived);
    }
```

So what happened to the pending EigenLayer's withdrawals? The WithdrawalQueue contract is the only one capable of completing these withdrawals through [settleEpochFromEigenLayer](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216-L271). Unfortunately, the epoch was already marked as settled and therefore the call will [revert](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L224).

## Impact

EigenLayer's withdrawals could remain stuck forever, losing the withdrawing assets of an entire rebalancing epoch.

## Code Snippet

## Tool used

Manual Review

## Recommendation

Either:
1. block epoch settlements if withdrawals have been requested from EigenLayer. This could be done by simple checking `epochWithdrawals.aggregateRoot` in RioLRTWithdrawalQueue::settleCurrentEpoch.
2. allow new rebalancing epochs to be handled in parallel to epochs being settled with EigenLayer's withdrawals.
