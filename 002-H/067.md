Rural Tweed Lemur

high

# Withdrawals requested while the current epoch is queued for settlement will dilute and cause a loss to all the withdrawals of the epoch

## Summary
[RioLRTWithdrawalQueue::settleEpochFromEigenLayer()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216) doesn't take into account withdrawals requested while the epoch is being settled but still burns all of the LRT tokens deposited during withdrawal requests.

## Vulnerability Detail
When [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) is called and the [deposit pool](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol) doesn't have enough funds to satisfy the currently requested withdrawals, the current epoch will be [queued for settlement](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L266) because funds have to be withdrawn from Eigenlayer. 

When an epoch is currently queued for settlement it's still possible for users to request a withdrawal for the queued epoch via [RioLRTCoordinator::requestWithdrawal()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L266), but if this happens funds will be lost.

In a normal scenario in which [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) is called after requesting withdrawals and the [deposit pool](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol) holds enough assets the correct amount of funds will be transferred to the withdrawal queue and the variable `epochWithdrawals.assetsReceived`, which is used to determine the total amount of funds that have been withdrawn in the current epoch, correctly updated.

However, when an epoch gets settled via [RioLRTWithdrawalQueue::settleEpochFromEigenLayer()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216) the variable `epochWithdrawals.assetsReceived` is only increased by the amount of assets requested from Eigenlayer at the time [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) was called without taking into account the withdrawals requested during the timeframe in which the epoch was queued for settlement. This results in less assets being distributed to withdrawers while still burning all of the LRT tokens sent during the withdrawal requests.

### POC
To copy-paste in `RioLRTCoordinator.t.sol`:

```solidity
function test_requestWithdrawalssettleEpochFromEigenLayerAreLost() public {
    //-> Setup
    uint256 initialAmountInDepositPool = address(reETH.depositPool).balance;
    uint256 initialDepositAmount = 42 ether;
    address alice = makeAddr("alice");
    deal(alice, initialDepositAmount);
    vm.prank(alice);
    reETH.token.approve(address(reETH.coordinator), type(uint256).max);
    uint8 operatorId = addOperatorDelegators(reETH.operatorRegistry, address(reETH.rewardDistributor), 1)[0];
    address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

    //-> Alice deposits 42ETH in the deposit pool
    vm.prank(alice);
    reETH.coordinator.depositETH{value: initialDepositAmount}();

    //-> Rebalance is called, 32ETH are deposited in Eigenlayer and 10ETH + dust stays in the deposit pool
    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);
    assertEq(address(reETH.depositPool).balance, 10 ether + initialAmountInDepositPool);

    //-> Verify withdrawal crdentials of the validator in which ETH have been deposited via `rebalance()`
    uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);
    skip(reETH.coordinator.rebalanceDelay());

    //-> Alice requests a withdrawal of 11ETH, which will require queueing a withdrawal from Eigenlayer since the deposit pool only holds ~10ETH
    uint256 reETHbalanceBefore = reETH.token.balanceOf(alice);
    vm.prank(alice);
    reETH.coordinator.requestWithdrawal(ETH_ADDRESS, 11 ether);
    uint256 reETHbalanceAfter = reETH.token.balanceOf(alice);
    uint256 reETHBurnedFromAlice = (reETHbalanceBefore - reETHbalanceAfter);

    //-> Rebalance is called and the current epoch is queued for settlement because funds have to be withdrawn from Eigenlayer
    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);
    IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary1 = reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, 0);
    assertNotEq(epochSummary1.aggregateRoot, bytes32(0));

    //-> Process withdrawal of ETH from the validators
    verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

    //-> Alice requests a new withdrawal of 5ETH, which will burn her reETH immediately
    reETHbalanceBefore = reETH.token.balanceOf(alice);
    vm.prank(alice);
    reETH.coordinator.requestWithdrawal(ETH_ADDRESS, 5 ether);
    reETHbalanceAfter = reETH.token.balanceOf(alice);
    reETHBurnedFromAlice += (reETHbalanceBefore - reETHbalanceAfter);

    //-> Epoch `0` gets settled via `settleEpochFromEigenLayer`
    IDelegationManager.Withdrawal[] memory withdrawals = new IDelegationManager.Withdrawal[](1);
    withdrawals[0] = IDelegationManager.Withdrawal({
        staker: operatorDelegator,
        delegatedTo: address(1),
        withdrawer: address(reETH.withdrawalQueue),
        nonce: 0,
        startBlock: 1,
        strategies: BEACON_CHAIN_STRATEGY.toArray(),
        shares: uint256(990000000000000000).toArray()
    });
    reETH.withdrawalQueue.settleEpochFromEigenLayer(ETH_ADDRESS, 0, withdrawals, new uint256[](1));

    //-> Alices claims her withdrawals from epoch `0`, which should be 11 ETH + 5 ETH but only gets 11ETH instead
    uint256 aliceETHBalanceBefore = alice.balance;
    vm.prank(alice);
    reETH.withdrawalQueue.claimWithdrawalsForEpoch(IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: 0}));
    uint256 aliceETHBalanceAfter = alice.balance;
    uint256 aliceETHReceived = aliceETHBalanceAfter - aliceETHBalanceBefore;

    assertEq(aliceETHReceived, 11 ether);
    assertEq(reETHBurnedFromAlice, 16 ether);
}
```

Add the following imports to make it work:
```solidity
import {IDelegationManager} from 'contracts/interfaces/eigenlayer/IDelegationManager.sol';
import {Array} from 'contracts/utils/Array.sol';
import {
    ETH_ADDRESS,
    ETH_DEPOSIT_SIZE,
    ETH_DEPOSIT_SOFT_CAP,
    ETH_DEPOSIT_BUFFER_LIMIT,
    MAX_REBALANCE_DELAY,
    BEACON_CHAIN_STRATEGY
} from 'contracts/utils/Constants.sol';
```

## Impact
When completing the withdrawal for the epoch users will withdraw less funds than they are entitled to. 

## Code Snippet

## Tool used

Manual Review

## Recommendation
Adjust [RioLRTWithdrawalQueue::settleEpochFromEigenLayer()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216) to take into account the new requested withdrawals or prevent [RioLRTCoordinator::requestWithdrawal()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L266) from being called while an epoch is being settled.
