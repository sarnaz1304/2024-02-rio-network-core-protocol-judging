Energetic Turquoise Quail

high

# Withdrawals can be requested for the same epoch while assets from EigenLayer are being withdrawn

## Summary

While RioLRTWithdrawalQueue is awaiting withdrawals from EigenLayer to complete, withdrawal requests coming through RioLRTCoordinator remain enable for the current epoch. These requests add to the user's `sharesOwed` and to the epoch's total `sharesOwed` and `amountToBurnAtSettlement`, but the withdrawal of assets corresponding to these extra amounts is not executed (this would normally happen during rebalancing). This means that the assets withdrawn will be claimable among a bigger pool of shares than it should be, diluting the value of the shares burnt and increasing the value of shares remaining in RIO protocol.

## Vulnerability Detail

When withdrawals are queued from EigenLayer for a given epoch, neither the epoch count `currentEpochsByAsset[asset]` for the corresponding asset is updated while the settlement is awaited (see [queueCurrentEpochSettlement](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L177-L209)):

```solidity
    function queueCurrentEpochSettlement(
        address asset,
        uint256 assetsReceived,
        uint256 shareValueOfAssetsReceived,
        bytes32 aggregateRoot
    ) external onlyCoordinator {
        if (aggregateRoot == bytes32(0)) revert INVALID_AGGREGATE_WITHDRAWAL_ROOT();

        uint256 currentEpoch = getCurrentEpoch(asset);

        EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
        if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();
        if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
        if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();

        uint256 restakingTokensToBurn;
        if (assetsReceived > 0) {
            epochWithdrawals.assetsReceived = SafeCast.toUint120(assetsReceived);
            epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(shareValueOfAssetsReceived);

            restakingTokensToBurn = epochWithdrawals.amountToBurnAtSettlement.mulWad(
                shareValueOfAssetsReceived.divWad(epochWithdrawals.sharesOwed)
            );
            token.burn(restakingTokensToBurn);

            epochWithdrawals.amountToBurnAtSettlement -= restakingTokensToBurn;
        }
        epochWithdrawals.aggregateRoot = aggregateRoot;

        emit EpochQueuedForSettlementFromEigenLayer(
            currentEpoch, asset, assetsReceived, shareValueOfAssetsReceived, restakingTokensToBurn, aggregateRoot
        );
    }
```

nor new [withdrawal requests](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L99-L116) are deactivated:

```solidity
    function requestWithdrawal(address asset, uint256 amountIn) external checkWithdrawal(asset, amountIn) returns (uint256 sharesOwed) {
        // Determine the amount of shares owed to the withdrawer using the current exchange rate.
        sharesOwed = convertToSharesFromRestakingTokens(asset, amountIn);

        // If requesting ETH, reduce the precision of the shares owed to the nearest Gwei,
        // which is the smallest unit of account supported by EigenLayer.
        if (asset == ETH_ADDRESS) sharesOwed = sharesOwed.reducePrecisionToGwei();

        // Pull restaking tokens from the sender to the withdrawal queue.
        token.safeTransferFrom(msg.sender, address(withdrawalQueue()), amountIn);

        // Ensure there are enough shares to cover the withdrawal request, and queue the withdrawal.
        uint256 availableShares = assetRegistry().convertToSharesFromAsset(asset, assetRegistry().getTotalBalanceForAsset(asset));
        if (sharesOwed > availableShares - withdrawalQueue().getSharesOwedInCurrentEpoch(asset)) {
            revert INSUFFICIENT_SHARES_FOR_WITHDRAWAL();
        }
        withdrawalQueue().queueWithdrawal(msg.sender, asset, sharesOwed, amountIn);
    }
```

```solidity
    function queueWithdrawal(address withdrawer, address asset, uint256 sharesOwed, uint256 amountIn)
        external
        onlyCoordinator
    {
        if (sharesOwed == 0) revert NO_SHARES_OWED();
        uint256 currentEpoch = getCurrentEpoch(asset);

        EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
        epochWithdrawals.sharesOwed += SafeCast.toUint120(sharesOwed);
        epochWithdrawals.amountToBurnAtSettlement += amountIn;

        UserWithdrawalSummary storage userSummary = epochWithdrawals.users[withdrawer];
        userSummary.sharesOwed += SafeCast.toUint120(sharesOwed);

        emit WithdrawalQueued(currentEpoch, asset, withdrawer, sharesOwed, amountIn);
    }
```

As can be seen in [queueWithdrawal](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L135-L142), new requests amounts during this period add to the user's `sharesOwed` and to the epoch's total `sharesOwed` and `amountToBurnAtSettlement`. 

When the epoch is finally [settled](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L135-L142):
1. All the shares accounted for the current epoch are burnt
```solidity
        token.burn(epochWithdrawals.amountToBurnAtSettlement);
```
2. An inflated amount of shares (due to the extra withdrawal requests) is assigned the requested amount of assets from EigenLayer
```solidity
        epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(epochWithdrawals.sharesOwed);
```
3. The amount of assets received during settlement is correctly accounted for
```solidity
        uint256 assetsReceived = asset.getSelfBalance() - balanceBefore;
        epochWithdrawals.assetsReceived += SafeCast.toUint120(assetsReceived);
```

This results in the same amount of underlying assets being assigned to more shares than it should. Every withdrawer of the epoch will be able to [claim](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L92-L108) their share, but it will be diluted.
```solidity
        amountOut = userSummary.sharesOwed.mulDiv(epochWithdrawals.assetsReceived, epochWithdrawals.sharesOwed);
        request.asset.transferTo(withdrawer, amountOut);
```

On the other hand, the rest of the protocol shares will increase their values.

## Impact

The value of shares can be wrongly diluted while waiting for EigenLayer's withdrawals, while the remaining shares in the protocol would increaser their value.

## Code Snippet

## Tool used

Manual Review

## Recommendation

While withdrawals from EigenLayer are waiting to be settled, record new withdrawal requests in a new epoch.