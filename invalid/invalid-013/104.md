Cool Aquamarine Unicorn

medium

# Depositors may receive zero restaking tokens due to small deposit

## Summary
Users deposits in `RioLTRCoordinator` through `RioLTRCoordinator::deposit` function that lacks of a zero check on the amount calculated for minting restaking tokens.
## Vulnerability Detail
`RioLTRCoordinator::deposit` function calculates `amountOut` as the amount of restaking tokens that will be minted based on the `amountIn` parameter: `amountOut = convertFromAssetToRestakingTokens(asset, amountIn);`

Inside [`convertFromAssetToRestakingTokens`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L185-L191) the flow is as follows:
```markdown
convertFromAssetToRestakingToken
	RIOLTRAssertRegistry::convertToUnitOfAccountFromAsset
	convertFromUnitOfAccountToRestakingTokens
```

Where [`RIOLTRAssetRegistry::convertToUnitOfAccountFromAsset`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L184-L196) calculates the return value with the following formula: `_normalizeDecimals(price * amount / priceScale, assetInfo[asset].decimals, priceFeedDecimals)`. The return value will only be zero if the price of the given asset is less than 1e`priceFeedDecimals` and the amount is a small number. 

Additionally, [`RIOLTRCoordinator::convertFromUnitOfAccountToRestakingToken`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L159-L170) receives the result of `RIOLTRAssetRegistry::convertToUnitOfAccountFromAsset` as parameter and returns the following:
`return value * supply / tvl`. 

[`RIOLTRAssetRegistry::getTVL`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L69-L75), [`RIOLTRAssetRegistry::getTVLForAsset`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L77-L85), and [`RIOLTRAssetRegistry::getTotalBalanceForAsset`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L87-L102)
```solidity
    /// @notice Returns the total value of all underlying assets in the unit of account.
    function getTVL() public view returns (uint256 value) {
        address[] memory assets = getSupportedAssets();
        for (uint256 i = 0; i < assets.length; ++i) {
            value += getTVLForAsset(assets[i]);
        }
    }
    
	/// @notice Returns the total value of the underlying asset in the unit of account.
    /// @param asset The address of the asset.
    function getTVLForAsset(address asset) public view returns (uint256) {
        uint256 balance = getTotalBalanceForAsset(asset);
        if (asset == ETH_ADDRESS) {
            return balance;
        }
        return convertToUnitOfAccountFromAsset(asset, balance);
    }
    
    /// @notice Returns the total balance of the asset, including the deposit pool and EigenLayer.
    /// @param asset The address of the asset.
    function getTotalBalanceForAsset(address asset) public view returns (uint256) {
        if (!isSupportedAsset(asset)) revert ASSET_NOT_SUPPORTED(asset);
        
        address depositPool_ = address(depositPool());
        if (asset == ETH_ADDRESS) {
            return depositPool_.balance + getETHBalanceInEigenLayer();
        }
        
        uint256 sharesHeld = getAssetSharesHeld(asset);
        uint256 tokensInRio = IERC20(asset).balanceOf(depositPool_);
        uint256 tokensInEigenLayer = convertFromSharesToAsset(getAssetStrategy(asset), sharesHeld);
        
        return tokensInRio + tokensInEigenLayer;
    }
```

TVL calculation is manipulatable to donations on any supported asset. However, due to the initial sacrificial deposit made by the issuer this risk is reduced.
## Impact
Under special circumstances, small deposits could result in minting zero restaking tokens to depositors.

## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L79
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L185-L191

Add the following test to `RioLRTCoordinator.t.sol`:

```solidity
import {MockPriceFeed} from './utils/MockPriceFeed.sol';
...
function test_smallDepositMintsZeroRestakingTokens() public {
        address depositor = makeAddr('depositor');

        // Changing price of an asset to be less than 1e18
        reLST.assetRegistry.setAssetPriceFeed(CBETH_ADDRESS, address(new MockPriceFeed(0.5 ether)));
  
        cbETH.mint(depositor, 1);

        vm.startPrank(depositor);

        cbETH.approve(address(reLST.coordinator), type(uint256).max);
        reLST.coordinator.deposit(CBETH_ADDRESS, 1);

        vm.stopPrank();

        assertEq(reLST.token.balanceOf(depositor), 0);
    }
```

## Tool used
Manual Review
## Recommendation
Check if `amountOut` calculated on deposits is zero:

```diff
    // forgefmt: disable-next-item
    /// @notice Deposits ERC20 tokens and mints restaking token(s) to the caller.
    /// @param asset The asset being deposited.
    /// @param amountIn The amount of the asset being deposited.
    function deposit(address asset, uint256 amountIn) external checkDeposit(asset, amountIn) returns (uint256 amountOut) {
        // Convert deposited asset amount to restaking tokens.
        amountOut = convertFromAssetToRestakingTokens(asset, amountIn);
+		if(amountOut == 0) revert AMOUNT_MUST_BE_GREATER_THAN_ZERO();
			
        // Pull tokens from the sender to the deposit pool.
        IERC20(asset).safeTransferFrom(msg.sender, address(depositPool()), amountIn);
        
        // Mint restaking tokens to the caller.
        token.mint(msg.sender, amountOut);
        
        emit Deposited(msg.sender, asset, amountIn, amountOut);
    }    
    ...
	/// @notice Deposits ETH and mints restaking token(s) to the caller.
    /// @dev This function assumes that the quote asset is ETH.
    function _depositETH() internal checkDeposit(ETH_ADDRESS, msg.value) returns (uint256 amountOut) {
        // Convert deposited ETH to restaking tokens and mint to the caller.
        amountOut = convertFromUnitOfAccountToRestakingTokens(msg.value);
+		if(amountOut == 0) revert AMOUNT_MUST_BE_GREATER_THAN_ZERO();
        
        // Forward ETH to the deposit pool.
        address(depositPool()).transferETH(msg.value);
        
        // Mint restaking tokens to the caller.
        token.mint(msg.sender, amountOut);
        
        emit Deposited(msg.sender, ETH_ADDRESS, msg.value, amountOut);
    }
```
