Exotic Parchment Tadpole

medium

# EL Strategy setting third party transfers to forbidden to break Rio system

## Summary

EL strategy whitelisters can forbid third-party transfers of withdrawals by setting the Strategy.thirdPartyTransfersForbidden to true. If this value is set to true, then any attempt to set the queue withdraw request to any address other than OperatorDelegator will lead to any queue withdrawal request to revert.


## Vulnerability Detail

Eigenlayer supports a setting (`thirdPartyTransfersForbidden`) for all strategies that can be enabled or disabled by strategy whitelisters. When this value is set to true, the operator delegator will not be able to withdraw on behalf of someone else. In Rio's case, the OperatorDelegator withdraws on behalf of the WithdrawalQueue or DepositPool contracts. Let's focus on Rio withdrawing on behalf of the DepositPool contract.

We can see this guard check in action in Eigenlayer's DelegationManager contract below:

```solidity
/**
 * Allows a staker to withdraw some shares. Withdrawn shares/strategies are immediately removed
 * from the staker. If the staker is delegated, withdrawn shares/strategies are also removed from
 * their operator.
 *
 * All withdrawn shares/strategies are placed in a queue and can be fully withdrawn after a delay.
 */
function queueWithdrawals(
    QueuedWithdrawalParams[] calldata queuedWithdrawalParams
) external onlyWhenNotPaused(PAUSED_ENTER_WITHDRAWAL_QUEUE) returns (bytes32[] memory) {
    bytes32[] memory withdrawalRoots = new bytes32[](queuedWithdrawalParams.length);
    address operator = delegatedTo[msg.sender];

    for (uint256 i = 0; i < queuedWithdrawalParams.length; i++) {
        require(queuedWithdrawalParams[i].strategies.length == queuedWithdrawalParams[i].shares.length, "DelegationManager.queueWithdrawal: input length mismatch");
        require(queuedWithdrawalParams[i].withdrawer != address(0), "DelegationManager.queueWithdrawal: must provide valid withdrawal address");

        // Remove shares from staker's strategies and place strategies/shares in queue.
        // If the staker is delegated to an operator, the operator's delegated shares are also reduced
        // NOTE: This will fail if the staker doesn't have the shares implied by the input parameters
        withdrawalRoots[i] = _removeSharesAndQueueWithdrawal({
            staker: msg.sender,
            operator: operator,
            withdrawer: queuedWithdrawalParams[i].withdrawer,
            strategies: queuedWithdrawalParams[i].strategies,
            shares: queuedWithdrawalParams[i].shares
        });
    }
    return withdrawalRoots;
}

function _removeSharesAndQueueWithdrawal(
    address staker, 
    address operator,
    address withdrawer,
    IStrategy[] memory strategies, 
    uint256[] memory shares
) internal returns (bytes32) {
    require(staker != address(0), "DelegationManager._removeSharesAndQueueWithdrawal: staker cannot be zero address");
    require(strategies.length != 0, "DelegationManager._removeSharesAndQueueWithdrawal: strategies cannot be empty");

    // Remove shares from staker and operator
    // Each of these operations fail if we attempt to remove more shares than exist
    for (uint256 i = 0; i < strategies.length;) {
        // Similar to `isDelegated` logic
        if (operator != address(0)) {
            _decreaseOperatorShares({
                operator: operator,
                staker: staker,
                strategy: strategies[i],
                shares: shares[i]
            });
        }

        // Remove active shares from EigenPodManager/StrategyManager
        if (strategies[i] == beaconChainETHStrategy) {
            /**
             * This call will revert if it would reduce the Staker's virtual beacon chain ETH shares below zero.
             * This behavior prevents a Staker from queuing a withdrawal which improperly removes excessive
             * shares from the operator to whom the staker is delegated.
             * It will also revert if the share amount being withdrawn is not a whole Gwei amount.
             */
            eigenPodManager.removeShares(staker, shares[i]);
        } else {
            // AUDIT: as we can see here if 
            require(
                staker == withdrawer || !strategyManager.thirdPartyTransfersForbidden(strategies[i]),
                "DelegationManager._removeSharesAndQueueWithdrawal: withdrawer must be same address as staker if thirdPartyTransfersForbidden are set"
            );
            ...
```

Based on the Eigenlayer code above, if the withdrawer value that is passed to EL's DelegationManager.queueWithdrawals is not the msg.sender (aka staker) and thirdPartyTransfersForbidden is set to true, the function will revert.

We can see below how Rio calls DelegationManager.queueWithdrawals on behalf of the DepositPool contract:

```solidity
// forgefmt: disable-next-item
/// @dev Queues a withdrawal of the specified amount of `shares` from the given `strategy` to the deposit pool,
/// specifically for facilitating operator exits or excess full withdrawal scrapes.
/// @param strategy The strategy from which to withdraw.
/// @param shares The amount of shares to withdraw.
function _queueWithdrawalForOperatorExitOrScrape(address strategy, uint256 shares) internal returns (bytes32 root) {
    if (strategy == BEACON_CHAIN_STRATEGY) {
        _increaseETHQueuedForOperatorExitOrScrape(shares);
    }
    root = _queueWithdrawal(strategy, shares, address(depositPool()));
}

// forgefmt: disable-next-item
/// @dev Queue a withdrawal of the given amount of `shares` to the `withdrawer` from the provided `strategy`.
/// @param strategy The strategy to withdraw from.
/// @param shares The amount of shares to withdraw.
/// @param withdrawer The address who has permission to complete the withdrawal.
function _queueWithdrawal(address strategy, uint256 shares, address withdrawer) internal returns (bytes32 root) {
    IDelegationManager.QueuedWithdrawalParams[] memory withdrawalParams = new IDelegationManager.QueuedWithdrawalParams[](1);
    withdrawalParams[0] = IDelegationManager.QueuedWithdrawalParams({
        strategies: strategy.toArray(),
        shares: shares.toArray(),
        withdrawer: withdrawer
    });
    // AUDIT DONE: Strategy can prevent a third-party withdrawer. Rio has no way to mitigate this if this switch is made. doesn't matter whether EL can be trusted. the issue is that the Rio system has no checks in place to handle a strategy flipping this flag on. See EL StrategyManager.setThirdPartyTransfersForbidden()
    root = delegationManager.queueWithdrawals(withdrawalParams)[0];
}
```

If a strategy's setting changes thirdPartyTransfersForbidden to true, then any attempt to queue a withdrawal will result in a revert, halting Rio's withdrawal system for the given asset. Considering this setting can be enabled/disabled at any time, Rio is at risk of Eigenlayer unintentionally breaking Rio's withdrawal system. Please note that the Eigenlayer strategy is still operational when the thirdPartyTransfersForbidden is set to true. This is not an extreme edge-case setting like pausing the strategy or intentional rug-pulling.   

## Impact

If the strategy whitelist ever changes, Rio will have no way to effectively migrate or handle the strategy.

## Code Snippet

https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/core/StrategyManager.sol?plain=1?plain=1#L211-L223

https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/core/StrategyManager.sol?plain=1?plain=1#L417-L420

https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/core/DelegationManager.sol?plain=1?plain=1#L267-L289

https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/core/DelegationManager.sol?plain=1?plain=1#L670-L735

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorDelegator.sol?plain=1#L253-L273

## Tool used

Manual Review

## Recommendation

Rio should consider assigning the withdrawer contract to the DelegatorOperator and then have any funds delivered to the operator to be forwarded to the Withdrawal Queue contract. Alternatively, an additional contract can be created that acts both as Withdrawer and Staker in Eigenlayer's eyes. This contract can be a pass-through contract such that it only stakes assets to Eigenlayer and any funds it receives will be redirected to the WithdrawalQueue contract.