Strong Denim Hyena

medium

# Deactivating an operator with a validator cap will always revert

## Summary

Deactivating an operator that has beacon chain strategy shares allocated and a validator cap set will always revert. 

## Vulnerability Detail
When a given operator has a validator cap set and beacon chain strategy shares allocated, deactivating that operator will always revert. This is because both `setOperatorStrategyCap()` and `setOperatorValidatorCap()` will queue a withdrawal to exit the same operator in Eigenlayer.

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L112-L137
```solidity
function deactivateOperator(
    RioLRTOperatorRegistryStorageV1.StorageV1 storage s,
    IRioLRTAssetRegistry assetRegistry,
    uint8 operatorId
) external {
    IRioLRTOperatorRegistry.OperatorDetails storage operator = s.operatorDetails[operatorId];

    if (operator.delegator == address(0)) revert IRioLRTOperatorRegistry.INVALID_OPERATOR_DELEGATOR();
    if (!operator.active) revert IRioLRTOperatorRegistry.OPERATOR_ALREADY_INACTIVE();

    // Queue exits for all strategies with non-zero allocations.
    address[] memory strategies = assetRegistry.getAssetStrategies();

    // @audit triggers a withdrawal of all Eigenpod shares for the beacon chain strategy
    for (uint256 i = 0; i < strategies.length; ++i) {
        s.setOperatorStrategyCap(
            operatorId, IRioLRTOperatorRegistry.StrategyShareCap({strategy: strategies[i], cap: 0})
        );
    }

    if (operator.validatorDetails.cap > 0) {
        // @audit triggers a withdrawal of all Eigenpod shares again
        s.setOperatorValidatorCap(operatorId, 0);
    }

    operator.active = false;
    s.activeOperatorCount -= 1;

    emit IRioLRTOperatorRegistry.OperatorDeactivated(operatorId);
}
```

Deactivating an operator sets the operator's strategy cap to 0, which exits the operator by queueing full withdrawal of its ETH. 

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L250-L252
```solidity
function setOperatorStrategyCap(
    RioLRTOperatorRegistryStorageV1.StorageV1 storage s,
    uint8 operatorId,
    IRioLRTOperatorRegistry.StrategyShareCap memory newShareCap
) internal {
    IRioLRTOperatorRegistry.OperatorDetails storage operatorDetails = s.operatorDetails[operatorId];
    // ... snip ...

    // If the current cap is greater than 0 and the new cap is 0, remove the operator from the strategy.
    if (currentShareDetails.cap > 0 && newShareCap.cap == 0) {
        // This branch queues the withdrawal in Eigenlayer and removes the Operator from the Utilization Heap.
        // If the operator has allocations, queue them for exit.
        if (currentShareDetails.allocation > 0) {
            operatorDetails.queueOperatorStrategyExit(operatorId, newShareCap.strategy);
        }
        // Remove the operator from the utilization heap.
        utilizationHeap.removeByID(operatorId);
    } else if (currentShareDetails.cap == 0 && newShareCap.cap > 0) {
    // ... snip ...
}
```

If the operator has a validator cap set, its validator cap will be set to 0 during deactivation. This also exits the operator and queues a full ETH withdrawal once again.

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L298-L301
```solidity
function setOperatorValidatorCap(
    RioLRTOperatorRegistryStorageV1.StorageV1 storage s,
    uint8 operatorId,
    uint40 newValidatorCap
) internal {
    IRioLRTOperatorRegistry.OperatorDetails storage operatorDetails = s.operatorDetails[operatorId];
    if (operatorDetails.delegator == address(0)) revert IRioLRTOperatorRegistry.INVALID_OPERATOR_DELEGATOR();

    IRioLRTOperatorRegistry.OperatorValidatorDetails memory validatorDetails = operatorDetails.validatorDetails;
    // ... snip ...
    uint40 activeDeposits = validatorDetails.deposited - validatorDetails.exited;
    OperatorUtilizationHeap.Data memory utilizationHeap = s.getOperatorUtilizationHeapForETH();

    // If the current cap is greater than 0 and the new cap is 0, remove the operator from ETH deposit tracking.
    if (validatorDetails.cap > 0 && newValidatorCap == 0) {
        // If there are active deposits, queue the operator for strategy exit.
        if (activeDeposits > 0) {
            operatorDetails.queueOperatorStrategyExit(operatorId, BEACON_CHAIN_STRATEGY);
            s.operatorDetails[operatorId].validatorDetails.exited += activeDeposits;
        }
        // Remove the operator from the utilization heap.
        utilizationHeap.removeByID(operatorId);
    } 
    // ... snip ...
}
```

The operator will have active deposits since it has not yet been fully exited. Attempting to exit the operator a second time will revert because there are no more shares to exit since the earlier withdrawal has already withdrawn all shares. 

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L160
```solidity
function queueOperatorStrategyExit(IRioLRTOperatorRegistry.OperatorDetails storage operator, uint8 operatorId, address strategy) internal {
    IRioLRTOperatorDelegator delegator = IRioLRTOperatorDelegator(operator.delegator);

    uint256 sharesToExit;
    if (strategy == BEACON_CHAIN_STRATEGY) {
        int256 eigenPodShares = delegator.getEigenPodShares();
        if (eigenPodShares > 0) {
            sharesToExit = uint256(eigenPodShares).reducePrecisionToGwei();
        }
    } else {
    // ... snip ...
    // @audit-issue this fails because shares to exit is 0 once all of the eigenpod shares have already been queued for withdrawal in the earlier call
    if (sharesToExit == 0) revert IRioLRTOperatorRegistry.CANNOT_EXIT_ZERO_SHARES();

    // Queues a withdrawal to the deposit pool.
    bytes32 withdrawalRoot = delegator.queueWithdrawalForOperatorExit(strategy, sharesToExit);
    emit IRioLRTOperatorRegistry.OperatorStrategyExitQueued(operatorId, strategy, sharesToExit, withdrawalRoot);
}
```

## Impact
Deactivating an operator with a validator cap and ETH allocations will always unexpectedly revert.

## Code Snippet
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L112-L137
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L250-L252
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L298-L301
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L160

## Tool used

Manual Review

## Recommendation

Consider setting the validator's exited amount to equal its deposited amount when the operator has already exited the BEACON_CHAIN_STRATEGY when setting its strategy share cap to 0. Below are the possible code changes for the fix.

```diff
diff --git a/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol b/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol
index a0714ff..80df70b 100644
--- a/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol
+++ b/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol
@@ -249,6 +249,10 @@ library OperatorRegistryV1Admin {
             // If the operator has allocations, queue them for exit.
             if (currentShareDetails.allocation > 0) {
                 operatorDetails.queueOperatorStrategyExit(operatorId, newShareCap.strategy);
+
+                if (newShareCap.strategy == BEACON_CHAIN_STRATEGY) {
+                    operatorDetails.validatorDetails.exited = operatorDetails.validatorDetails.deposited;
+                }
             }
             // Remove the operator from the utilization heap.
             utilizationHeap.removeByID(operatorId);
```