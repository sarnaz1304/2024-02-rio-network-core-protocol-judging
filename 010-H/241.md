Powerful Pastel Albatross

high

# The value of `operatorDetails.shareDetails[newShareCap.strategy].allocation` has to be deleted when `currentShareDetails.cap > 0 && newShareCap.cap == 0` at the function `OperatorRegistryV1Admin.setOperatorStrategyCap()`.

kennedy1030

high

## Summary

In the function `OperatorRegistryV1Admin.setOperatorStrategyCap()`, the function `queueOperatorStrategyExit()` is called and the value of `operatorDetails.shareDetails[newShareCap.strategy].allocation` is not deleted, in the case of `currentShareDetails.cap > 0 && newShareCap.cap == 0` at [[L297](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L297)].

When a deactivated operator is activated again, the values of the allocations of this operator are kept and this may lead to an uncorrect `minMaxHeap` representation of utilization, so some unreasonable things could occur.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L249
```javascript
    function setOperatorStrategyCap(
        RioLRTOperatorRegistryStorageV1.StorageV1 storage s,
        uint8 operatorId,
        IRioLRTOperatorRegistry.StrategyShareCap memory newShareCap
    ) internal {
        IRioLRTOperatorRegistry.OperatorDetails storage operatorDetails = s.operatorDetails[operatorId];
        if (operatorDetails.delegator == address(0)) revert IRioLRTOperatorRegistry.INVALID_OPERATOR_DELEGATOR();

        IRioLRTOperatorRegistry.OperatorShareDetails memory currentShareDetails = operatorDetails.shareDetails[newShareCap.strategy];

        // If the new cap is the same as the current, no update is necessary.
        if (currentShareDetails.cap == newShareCap.cap) {
            return;
        }
        OperatorUtilizationHeap.Data memory utilizationHeap = s.getOperatorUtilizationHeapForStrategy(newShareCap.strategy);

        // If the current cap is greater than 0 and the new cap is 0, remove the operator from the strategy.
249     if (currentShareDetails.cap > 0 && newShareCap.cap == 0) {
            // If the operator has allocations, queue them for exit.
            if (currentShareDetails.allocation > 0) {
                operatorDetails.queueOperatorStrategyExit(operatorId, newShareCap.strategy);
            }
            // Remove the operator from the utilization heap.
            utilizationHeap.removeByID(operatorId);
        } else if (currentShareDetails.cap == 0 && newShareCap.cap > 0) {
            // If the current cap is 0 and the new cap is greater than 0, insert the operator into the heap.
            utilizationHeap.insert(OperatorUtilizationHeap.Operator(operatorId, 0));
        } else {
            // Otherwise, update the operator's utilization in the heap.
            utilizationHeap.updateUtilizationByID(operatorId, currentShareDetails.allocation.divWad(newShareCap.cap));
        }

        // Persist the updated heap to the active operators tracking.
        utilizationHeap.store(s.activeOperatorsByStrategyShareUtilization[newShareCap.strategy]);

        // Update the share cap in the operator details.
        operatorDetails.shareDetails[newShareCap.strategy].cap = newShareCap.cap;

        emit IRioLRTOperatorRegistry.OperatorStrategyShareCapSet(operatorId, newShareCap.strategy, newShareCap.cap);
    }
```

When the owner deactivates an operator, all caps of strategies of this operator are set to 0, but all allocations are not changed.

If the owner activates this operator again, actual allocations are kept still, but the operator hasn't got any shares.

In this case, when `RioLRTCoordinator.rebalance()` is called, the utilization of this operator is 0 in the `heap`, but it's `allocation` is not 0 and even it can be greater than `cap` so the assets could never be deposited.

As a result, corresponding asset could never be deposited, even a little.

## Impact

Deposit of some assets could be failed, even though all operators are not full.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L249

## Tool used

Manual Review

## Recommendation

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L252
```diff
        
    function setOperatorStrategyCap(
        RioLRTOperatorRegistryStorageV1.StorageV1 storage s,
        uint8 operatorId,
        IRioLRTOperatorRegistry.StrategyShareCap memory newShareCap
    ) internal {
        IRioLRTOperatorRegistry.OperatorDetails storage operatorDetails = s.operatorDetails[operatorId];
        if (operatorDetails.delegator == address(0)) revert IRioLRTOperatorRegistry.INVALID_OPERATOR_DELEGATOR();

        IRioLRTOperatorRegistry.OperatorShareDetails memory currentShareDetails = operatorDetails.shareDetails[newShareCap.strategy];

        // If the new cap is the same as the current, no update is necessary.
        if (currentShareDetails.cap == newShareCap.cap) {
            return;
        }
        OperatorUtilizationHeap.Data memory utilizationHeap = s.getOperatorUtilizationHeapForStrategy(newShareCap.strategy);

        // If the current cap is greater than 0 and the new cap is 0, remove the operator from the strategy.
        if (currentShareDetails.cap > 0 && newShareCap.cap == 0) {
            // If the operator has allocations, queue them for exit.
            if (currentShareDetails.allocation > 0) {
                operatorDetails.queueOperatorStrategyExit(operatorId, newShareCap.strategy);
+               operatorDetails.shareDetails[newShareCap.strategy].allocation = 0;
            }
            // Remove the operator from the utilization heap.
            utilizationHeap.removeByID(operatorId);
        } else if (currentShareDetails.cap == 0 && newShareCap.cap > 0) {
            // If the current cap is 0 and the new cap is greater than 0, insert the operator into the heap.
            utilizationHeap.insert(OperatorUtilizationHeap.Operator(operatorId, 0));
        } else {
            // Otherwise, update the operator's utilization in the heap.
            utilizationHeap.updateUtilizationByID(operatorId, currentShareDetails.allocation.divWad(newShareCap.cap));
        }

        // Persist the updated heap to the active operators tracking.
        utilizationHeap.store(s.activeOperatorsByStrategyShareUtilization[newShareCap.strategy]);

        // Update the share cap in the operator details.
        operatorDetails.shareDetails[newShareCap.strategy].cap = newShareCap.cap;

        emit IRioLRTOperatorRegistry.OperatorStrategyShareCapSet(operatorId, newShareCap.strategy, newShareCap.cap);
    }
```