Active Azure Elephant

high

# OperatorOperations.depositTokenToOperators may unexpectedly revert due to rounding

## Summary

OperatorOperations.depositTokenToOperators enforces the invariant that `sharesReceived == sharesAllocated`. However, since we convert from shares to assets and back throughout this process, sharesReceived may be rounded down, causing execution to unexpectedly revert.

## Vulnerability Detail

In OperatorOperations.depositTokenToOperators, we call allocateStrategyShares, which returns sharesAllocated as well as allocations:

```solidity
(uint256 sharesAllocated, IRioLRTOperatorRegistry.OperatorStrategyAllocation[] memory  allocations) = operatorRegistry.allocateStrategyShares(
    strategy, sharesToAllocate
);
```

Part of each element in the allocations array, `allocation.tokens`, is the amount of the underlying token which will be passed to stakeERC20 for the given allocation:

```solidity
IERC20(token).safeTransfer(allocation.delegator, allocation.tokens);
sharesReceived += IRioLRTOperatorDelegator(allocation.delegator).stakeERC20(strategy, token, allocation.tokens);
```

We compute allocation.tokens in allocateStrategyShares by converting the share allocation amount to an amount of underlying using sharesToUnderlyingView:

```solidity
uint256 newTokenAllocation = IStrategy(strategy).sharesToUnderlyingView(newShareAllocation);
allocations[allocationIndex] = OperatorStrategyAllocation(
    operator.delegator,
    newShareAllocation,
    newTokenAllocation
);
```

sharesToUnderlyingView effectively just retrieves the equivalent amount underlying tokens for the relative amount of the total shares:

```solidity
function sharesToUnderlyingView(uint256 amountShares) public view virtual override returns (uint256) {
    // account for virtual shares and balance
    uint256 virtualTotalShares = totalShares + SHARES_OFFSET;
    uint256 virtualTokenBalance = _tokenBalance() + BALANCE_OFFSET;
    // calculate ratio based on virtual shares and balance, being careful to multiply before dividing
    return (virtualTokenBalance * amountShares) / virtualTotalShares;
}
```

The amount of underlying tokens passed to stakeERC20 is then deposited into the strategy via StrategyManager.depositIntoStrategy:

```solidity
function stakeERC20(address strategy, address token_, uint256 amount) external onlyDepositPool returns (uint256 shares) {
    if (IERC20(token_).allowance(address(this), address(strategyManager)) < amount) {
        IERC20(token_).forceApprove(address(strategyManager), type(uint256).max);
    }
    shares = strategyManager.depositIntoStrategy(strategy, token_, amount);
}
```

In depositIntoStrategy, we call strategy.deposit, where we compute the number of shares received by essentially doing the reverse of sharesToUnderlyingView:

```solidity
/**
 * @notice calculation of newShares *mirrors* `underlyingToShares(amount)`, but is different since the balance of `underlyingToken`
 * has already been increased due to the `strategyManager` transferring tokens to this strategy prior to calling this function
 */
// account for virtual shares and balance
uint256 virtualShareAmount = priorTotalShares + SHARES_OFFSET;
uint256 virtualTokenBalance = _tokenBalance() + BALANCE_OFFSET;
// calculate the prior virtual balance to account for the tokens that were already transferred to this contract
uint256 virtualPriorTokenBalance = virtualTokenBalance - amount;
newShares = (amount * virtualShareAmount) / virtualPriorTokenBalance;
```

After making our deposits, we validate the invariant that sharesReceived == sharesAllocated, to ensure that our accounting logic is correct:

```solidity
if (sharesReceived != sharesAllocated) revert INCORRECT_NUMBER_OF_SHARES_RECEIVED();
```

Given the flow above, we can test whether sharesReceived will always equal sharesAllocated by doing the conversion from shares to underlying and back and see if our input matches our output. I've made a simple proof of concept contract which simplifies the conversion logic and tests this:

```solidity
pragma solidity 0.8.20;

contract ConversionPrecisionLoss {
    function sharesToUnderlyingView(uint256 amountShares, uint256 totalShares, uint256 tokenBalance) public pure returns (uint256) {
        return (tokenBalance * amountShares) / totalShares;
    }

    function underlyingToSharesDeposit(uint256 amountUnderlying, uint256 totalShares, uint256 tokenBalance) public pure returns (uint256) {
        return (amountUnderlying * totalShares) / tokenBalance;
    }
}
```

We can test this by running sharesToUnderlyingView with some example parameters:

amountShares: 69
totalShares: 420
tokenBalance: 1000

Which gives us the output: 164

We can now call underlyingToSharesDeposit with the output as amountUnderlying, so:

amountUnderlying: 164
totalShares: 420
tokenBalance: 1000

And the output is 68, which doesn't match our initial input of 69, thus execution is reverted. The reason this occurs is because when the result of either conversion is a decimal value, solidity rounds down by default.

Since we may deposit many allocations at a time, if any of these allocations rounds down, execution will revert, making this case much more likely.

## Impact

This not only regularly blocks deposits into eigenlayer, but since depositTokensToOperators is initiated by rebalance, withdrawal processing is also regularly blocked.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L363

## Tool used

Manual Review

## Recommendation

Rather than computing the amount of underlying to deposit using sharesToUnderlyingView, a custom function should be made to perform the same computation but rounding the result upwards.