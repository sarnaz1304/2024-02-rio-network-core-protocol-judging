Strong Denim Hyena

medium

# Rebalancing can break on Strategy deposits due to max deposit checks

## Summary
The LRT does not check for the max total deposits allowed by a strategy when it deposits it balance into Eigenlayer. This can break rebalancing once the max deposit cap for a strategy has been reached.

## Vulnerability Detail
Eigenlayer M2 supports three LSTs with each having its own instance of StrategyBaseTVLLimits that has a cap per deposit and for total deposits into the strategy.

ref: https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/strategies/StrategyBaseTVLLimits.sol#L79-L82
```solidity
function _beforeDeposit(IERC20 /*token*/, uint256 amount) internal virtual override {
    require(amount <= maxPerDeposit, "StrategyBaseTVLLimits: max per deposit exceeded");
    require(_tokenBalance() <= maxTotalDeposits, "StrategyBaseTVLLimits: max deposits exceeded");
}
```

When rebalancing, the LRT will deposit any remaining balance in its Deposit Pool into Eigenlayer. When it deposits a non-ETH asset into Eigenlayer (cbETH, rETH, stETH), the total deposited amount is not capped.

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol#L47-L67
```solidity
function depositBalanceIntoEigenLayer(address asset) external onlyCoordinator returns (uint256, bool) {
    uint256 amountToDeposit = asset.getSelfBalance();
    if (amountToDeposit == 0) return (0, false);

    bool isDepositCapped;
    // deposit is only capped for ETH deposits
    if (asset == ETH_ADDRESS) {
        if (amountToDeposit > ETH_DEPOSIT_SOFT_CAP) {
            if (amountToDeposit - ETH_DEPOSIT_SOFT_CAP > ETH_DEPOSIT_BUFFER_LIMIT) {
                (amountToDeposit, isDepositCapped) = (ETH_DEPOSIT_SOFT_CAP, true);
            }
        }
        return (OperatorOperations.depositETHToOperators(operatorRegistry(), amountToDeposit), isDepositCapped);
    }

    address strategy = assetRegistry().getAssetStrategy(asset);
    uint256 sharesToAllocate = assetRegistry().convertToSharesFromAsset(asset, amountToDeposit);
    return (OperatorOperations.depositTokenToOperators(operatorRegistry(), asset, strategy, sharesToAllocate), isDepositCapped);
}
```

There are caps applied on a per-operator basis which can address the per-deposit cap. However, there are no checks in the LRT for the maxTotalDeposits in the strategy which can lead to rebalancing failing. 

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L342-L392
```solidity
function allocateStrategyShares(address strategy, uint256 sharesToAllocate) external onlyDepositPool returns (uint256 sharesAllocated, OperatorStrategyAllocation[] memory allocations) {
    // ... snip ...
    while (remainingShares > 0) {
        // ... snip ...
        uint256 newShareAllocation = FixedPointMathLib.min(operatorShares.cap - operatorShares.allocation, remainingShares);
        // (token balance * new share allocation) / total shares
        uint256 newTokenAllocation = IStrategy(strategy).sharesToUnderlyingView(newShareAllocation);
        allocations[allocationIndex] = OperatorStrategyAllocation(
            operator.delegator,
            newShareAllocation,
            newTokenAllocation
        );
        remainingShares -= newShareAllocation;
        // ... snip ...
    }
    sharesAllocated = sharesToAllocate - remainingShares;
    // ... snip ...
}
```

## Impact
This issue breaks rebalancing, which stops future withdrawals from the LRT without stopping deposits into the LRT. Once a strategy has hit its max total deposits, all of its corresponding assets in the LRT will be locked indefinitely. This issue can occur naturally and it can be forced by an attacker that makes a donation to the Deposit Pool or the Strategy vault to increase the balance enough to reach the total deposits cap.

## Code Snippet
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L342-L392
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol#L47-L67
- https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/strategies/StrategyBaseTVLLimits.sol#L79-L82
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L132
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorOperations.sol#L51-L68
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorDelegator.sol#L174-L179

## Tool used

Manual Review

## Recommendation
Consider applying the same deposit cap logic for ETH assets when depositing non-ETH assets into Eigenlayer. The maxTotalDeposits of the strategy to be deposited into will be used as the cap.