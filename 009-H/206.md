Strong Denim Hyena

high

# Undelegating Operator can break withdrawals and lead to insolvency

## Summary
When an Operator undelegates, all the delegated shares to the Operator are queued for withdrawal. Since these withdrawals were initiated outside of the LRT, they are not recorded and cause the LRT's TVL to drop creating a window of opportunity to mint discounted LRTs. The effect is broken withdrawals and if that issue is fixed, an LRT that is insolvent due to the discounted LRT tokens.

_NOTE: I reported this finding since it shows a well-intentioned Operator that is not stealing assets can still cause harm to the LRT_

## Vulnerability Detail
For context, [here](https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/docs/core/DelegationManager.md#undelegate) is the documentation for undelegate.

Undelegating does the ff:
- queues withdrawals on behalf of the Staker for all their delegated shares
- decreases the Operator's delegated shares according to the amounts and strategies being withdrawn

ref: https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/core/DelegationManager.sol#L211-L258
```solidity
function undelegate(address staker) external onlyWhenNotPaused(PAUSED_ENTER_WITHDRAWAL_QUEUE) returns (bytes32[] memory withdrawalRoots) {
    // ... snip ...

    // Gather strategies and shares to remove from staker/operator during undelegation
    // Undelegation removes ALL currently-active strategies and shares
    (IStrategy[] memory strategies, uint256[] memory shares) = getDelegatableShares(staker);
    // ... snip ..
    } else {
        withdrawalRoots = new bytes32[](strategies.length);
        for (uint256 i = 0; i < strategies.length; i++) {
            IStrategy[] memory singleStrategy = new IStrategy[](1);
            uint256[] memory singleShare = new uint256[](1);
            singleStrategy[0] = strategies[i];
            singleShare[0] = shares[i];

            withdrawalRoots[i] = _removeSharesAndQueueWithdrawal({
                staker: staker,
                operator: operator,
                withdrawer: staker,
                strategies: singleStrategy,
                shares: singleShare
            });
        }
    }

    return withdrawalRoots;
}
```

When an Operator undelegates, its corresponding Operator Delegator (Staker) will have all its shares queued for withdrawal without it being aware of it. All shares of the Operator Delegator are delegated to the Operator so its `podOwnerShares` will be down to 0. The combination of the Delegator's owned shares dropping to 0 and no recording of the shares queued for withdrawal will cause a significant decrease to the TVL of the LRT. The worst-case of this would be an LRT with all its assets deposited into Eigenlayer and with only one Operator. If that one Operator undelegates, the TVL of the LRT will drop to 0 until the withdrawal has been completed.

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorDelegator.sol#L101-L126
```solidity
function getEigenPodShares() public view returns (int256) {
    return eigenPodManager.podOwnerShares(address(this));
}
// ... snip ...
function getETHUnderManagement() external view returns (uint256) {
    int256 aum = getEigenPodShares() + int256(getETHQueuedForWithdrawal());
    if (aum < 0) return 0;

    return uint256(aum);
}
```

Any deposits done while the TVL has significantly dropped will mint LRTs at a huge discount at the expense of the earlier depositors. This is because the value of the LRT tokens is based on the LRT's TVL.

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L161-L170
```solidity
function convertFromUnitOfAccountToRestakingTokens(uint256 value) public view returns (uint256) {
    uint256 tvl = getTVL();
    uint256 supply = token.totalSupply();

    if (supply == 0) {
        return value;
    }
    return value * supply / tvl;
}
```

When the undelegate action queues withdrawals, it sets the staker (the Operator Delegator) as the withdrawer. However, the Operator Delegator has no function for completing queued withdrawals in Eigenlayer's Delegation Manager. The withdrawals can not be completed because the Operator Delegator will need to do the `completeQueuedWithdrawal()` call to the Delegation Manager.

ref: https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/core/DelegationManager.sol#L545-L650
```solidity
function _completeQueuedWithdrawal(
    Withdrawal calldata withdrawal,
    IERC20[] calldata tokens,
    uint256 /*middlewareTimesIndex*/,
    bool receiveAsTokens
) internal {
    // ... snip ...
    require(
        msg.sender == withdrawal.withdrawer, 
        "DelegationManager._completeQueuedWithdrawal: only withdrawer can complete action"
    );
    // ... snip ...
}
```

## Impact
Withdrawals are blocked since the Operator Delegator does not have functionality to complete queued withdrawals. Any upgrades to the Delegator to complete the withdrawals will unblock rebalancing and withdrawals. However, the LRT will still be insolvent due to the huge discount for minting LRT tokens during the big drop in TVL. If all the LRT token holders were to withdraw their assets once the queued withdrawals have been completed, not all token holders will be able retrieve their assets and the earlier depositors are guaranteed to lose a portion of their principal because of the diluted value of their LRT tokens.

## Code Snippet
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorDelegator.sol#L101-L126
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L161-L170
- https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/core/DelegationManager.sol#L211-L258
- https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/core/DelegationManager.sol#L548-L650
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L70-L114

## Tool used

Manual Review

## Recommendation
Consider doing the following to handle the case of an Operator undelegating:
1. Deposits and Withdrawals in the Coordinator should first check that all active operators are still delegated to by their corresponding Operator Delegators. This is a check to ensure that none of them have undelegated. Possibly revert if any are undelegated.
2. Add functionality to the Operator Delegator to complete queued withdrawals, transfer all the withdrawn assets to the Deposit Pool, and remove and/or deactivate the undelegated Operator.