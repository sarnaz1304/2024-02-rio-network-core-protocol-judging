Raspy Gunmetal Urchin

high

# Partial withdrawals to operator delegator are bricked due to low-level call gas limit

## Summary
Partial rewards or ETH sent accidentally to the Eigen Pod cannot be transferred to the Operator Delegator due to a low gas limit in the protocol's custom `transferETH()`.

## Vulnerability Detail
Whenever partial withdrawals, or ETH that has accidentally been sent to the Eigen Pod, are to be sent back to the Operator Delegator contract, the [receive function](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTOperatorDelegator.sol#L242-L246) calls the custom logic for [transferring ETH](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/Asset.sol#L37-L46) in order to forward the `msg.value` to be distributed through the Rewards Distributor.

The use of the function is fine when used for depositing native eth in the [coordinator](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L222) to transfer it to the deposit pool, the issue is that in the `receive()` [function of the Rewards Distributor](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTRewardDistributor.sol#L81-L94) there is logic that consumes a lot of gas:

```solidity
    receive() external payable {
        uint256 value = msg.value;

        uint256 treasuryShare = value * treasuryETHValidatorRewardShareBPS / MAX_BPS;
        uint256 operatorShare = value * operatorETHValidatorRewardShareBPS / MAX_BPS;
        uint256 poolShare = value - treasuryShare - operatorShare;

        if (treasuryShare > 0) treasury.transferETH(treasuryShare);
        if (operatorShare > 0) operatorRewardPool.transferETH(operatorShare);
        if (poolShare > 0) address(depositPool()).transferETH(poolShare);

        emit ETHValidatorRewardsDistributed(treasuryShare, operatorShare, poolShare);
    }
```

Meanwhile the `transferETH()` function limits the gas for the call to only `10_000`:

```solidity
    function transferETH(address recipient, uint256 amount) internal {
        (bool success,) = recipient.call{value: amount, gas: 10_000}('');
        if (!success) {
            revert ETH_TRANSFER_FAILED();
        }
    }
```

The gas limit is not enough to execute the whole flow of the distributor's receive and any attempts to forward native eth from partial withdrawals/funds sent to the eigen pod on accident will fail.

## Impact
DOS to core protocol functionality to be able to receive partial withdrawals. Running 10 foundry tests, the gas consumed was 20-30K every time which is 2x-3x the gas limit of the function. I believe high impact since this will pretty much always revert and it affects user funds/yield.

## Code Snippet
Proof of concept:

1. Add `import {Test, console2} from "forge-std/Test.sol";` to RioLRTOperatorDelegator.t.sol
2. Change the contract to: `contract RioLRTOperatorDelegatorTest is Test, RioDeployer {`
3. Add the test below and run with `forge test --mt testOutOfGas -vvvv`

<details closed>
<summary>PoC</summary>
<br>

```solidity
    function testOutOfGas() public {
        // Setting up an operator
        uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
        address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;
        RioLRTOperatorDelegator delegatorContract = RioLRTOperatorDelegator(payable(operatorDelegator));
        
        // Simulating Eigen Layer address and dealing it eth
        address eigenLayer = makeAddr("eigenLayer");
        vm.deal(eigenLayer, 1 ether);

        // Logging gas before transfer and after transfer when receiving partial withdrawal to operator delegator
        vm.prank(eigenLayer);
        vm.expectRevert();
        uint256 initialGas = gasleft();
        (bool success,) = address(delegatorContract).call{value: 1 ether}('');
        uint256 afterGas = gasleft();
        uint256 gasUsed = initialGas - afterGas;

        console2.log("gas used:", gasUsed);
    }
```

</details>

## Tool used
Manual Review/Foundry

## Recommendation
Use a regular low-level call with no gas limit for the receive function of the operator delegator contract.