Active Azure Elephant

high

# Forwarding ETH to the rewardDistributor using transferETH will run out of gas

## Summary

ETH received by RioLRTOperatorDelegator is forwarded to the RioLRTRewardDistributor contract where it is distributed to multiple parties. transferETH, which is used to execute the transfer limits the gas limit to 10000 which is insufficient to complete the call, resulting in all ETH transfers to RioLRTOperatorDelegator being reverted.

## Vulnerability Detail

In RioLRTOperatorDelegator.receive, we forward any received ETH to the rewardDistributor using transferETH where it splits the received funds throughout a set of recipients:

```solidity
receive() external payable {
    address(rewardDistributor()).transferETH(msg.value);
}
```

```solidity
receive() external payable {
    uint256 value = msg.value;

    uint256 treasuryShare = value * treasuryETHValidatorRewardShareBPS / MAX_BPS;
    uint256 operatorShare = value * operatorETHValidatorRewardShareBPS / MAX_BPS;
    uint256 poolShare = value - treasuryShare - operatorShare;

    if (treasuryShare > 0) treasury.transferETH(treasuryShare);
    if (operatorShare > 0) operatorRewardPool.transferETH(operatorShare);
    if (poolShare > 0) address(depositPool()).transferETH(poolShare);

    emit ETHValidatorRewardsDistributed(treasuryShare, operatorShare, poolShare);
}
```

The problem is that transferETH only provides 10000 gas along with the call:

```solidity
function transferETH(address recipient, uint256 amount) internal {
    (bool success,) = recipient.call{value: amount, gas: 10_000}('');
    if (!success) {
        revert ETH_TRANSFER_FAILED();
    }
}
```

The reward distribution makes up to 3 calls to cold addresses with a non-zero value. Using evm.codes as a reference, we can compute the gas cost of these calls. Each of these cold calls with non-zero values cost 9300. As a result, we obviously exceed the 10000 gas limit provided with the initial transferETH call. 

## Impact

ETH transfers to RioLRTOperatorDelegator will always revert, contrary to the intended effect of distributing the ETH via the RioLRTRewardDistributor.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/Asset.sol#L42

## Tool used

Manual Review

## Recommendation

There is no need or benefit to limiting the gas limit for transferETH.