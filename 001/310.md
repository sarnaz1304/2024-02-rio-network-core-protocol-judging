Straight Neon Blackbird

medium

# depositTokenToOperators: When allocating ERC20 to multiple operators, it always fails because the predicted share and the actual share received are not the same

## Summary

There may be a discrepancy between the predicted value of the share and the value of the share that actually be received by staking. The transaction always reverts when allocating ERC20 to multiple operators. Also, because the `rebalance` fails, so the token withdrawal cannot be settled. 

## Vulnerability Detail

When allocating ERC20 in EigenLayer's Strategy, first, calculate how much token to deposit in which operator and how much share can be received in `operatorRegistry.allocateStrategyShares`. Then, call `OperatorDelegator.stakeERC20` to stake ERC20, and sum the shares actually received. Finally, check if the shares calculated in `operatorRegistry.allocateStrategyShares` and the shares actually received match. If they don't match, the transaction is reverted.

```solidity
function depositTokenToOperators(
    IRioLRTOperatorRegistry operatorRegistry,
    address token,
    address strategy,
    uint256 sharesToAllocate
) internal returns (uint256 sharesReceived) {
@>  (uint256 sharesAllocated, IRioLRTOperatorRegistry.OperatorStrategyAllocation[] memory  allocations) = operatorRegistry.allocateStrategyShares(
        strategy, sharesToAllocate
    );

    for (uint256 i = 0; i < allocations.length; ++i) {
        IRioLRTOperatorRegistry.OperatorStrategyAllocation memory allocation = allocations[i];

        IERC20(token).safeTransfer(allocation.delegator, allocation.tokens);
@>      sharesReceived += IRioLRTOperatorDelegator(allocation.delegator).stakeERC20(strategy, token, allocation.tokens);
    }
@>  if (sharesReceived != sharesAllocated) revert INCORRECT_NUMBER_OF_SHARES_RECEIVED();
}
```

`operatorRegistry.allocateStrategyShares` calls `Strategy.sharesToUnderlyingView` to calculate the tokens needed to receive shares. [`Strategy.sharesToUnderlyingView`](https://github.com/Layr-Labs/eigenlayer-contracts/blob/1d926f25f3bd401c4d6552f43aa561782982cb00/src/contracts/strategies/StrategyBase.sol#L200) calculates based on the tokens and shares in the current Strategy. When allocating to multiple operators, it calculates without reflecting the tokens and shares that the previous operator has deposited. Therefore, there is a discrepancy between the predicted amount and the sum of the shares received when actually deposited, because in real situation, the tokens and shares of the former operator staked affect the number of shares the next operator will receive.

```solidity
// operatorRegistry.allocateStrategyShares
function allocateStrategyShares(address strategy, uint256 sharesToAllocate) external onlyDepositPool returns (uint256 sharesAllocated, OperatorStrategyAllocation[] memory allocations) {
    OperatorUtilizationHeap.Data memory heap = s.getOperatorUtilizationHeapForStrategy(strategy);
    if (heap.isEmpty()) {
        return (sharesAllocated, allocations);
    }

    uint256 allocationIndex;
    uint256 remainingShares = sharesToAllocate;

    allocations = new OperatorStrategyAllocation[](s.activeOperatorCount);
    while (remainingShares > 0) {
@>      uint8 operatorId = heap.getMin().id;

        ...

@>      uint256 newShareAllocation = FixedPointMathLib.min(operatorShares.cap - operatorShares.allocation, remainingShares);
@>      uint256 newTokenAllocation = IStrategy(strategy).sharesToUnderlyingView(newShareAllocation);
        allocations[allocationIndex] = OperatorStrategyAllocation(
            operator.delegator,
            newShareAllocation,
            newTokenAllocation
        );
@>      remainingShares -= newShareAllocation;

        ...
    }
@>  sharesAllocated = sharesToAllocate - remainingShares;

    ...
}

// StrategyBase.sharesToUnderlyingView (from EigenLayer)
function sharesToUnderlyingView(uint256 amountShares) public view virtual override returns (uint256) {
    // account for virtual shares and balance
    uint256 virtualTotalShares = totalShares + SHARES_OFFSET;
    uint256 virtualTokenBalance = _tokenBalance() + BALANCE_OFFSET;
    // calculate ratio based on virtual shares and balance, being careful to multiply before dividing
@>  return (virtualTokenBalance * amountShares) / virtualTotalShares;
}

```

Therefore, `if (sharesReceived != sharesAllocated) revert INCORRECT_NUMBER_OF_SHARES_RECEIVED();` always reverts if you allocate to multiple operators.

## Impact

Cannot stake ERC20 in EigenLayer Strategy. Also, because the `rebalance` fails, so the token withdrawal cannot be settled. 

## Code Snippet

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/OperatorOperations.sol#L67](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/OperatorOperations.sol#L67)

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L363](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L363)

## Tool used

Manual Review

## Recommendation

Check the error range instead of checking the exact value with `if (sharesReceived != sharesAllocated) revert INCORRECT_NUMBER_OF_SHARES_RECEIVED();`.