Early Currant Stork

high

# RioLRTWithdrawalQueue.sol - If a withdrawal request is made after queueCurrentEpochSettlement is called, accounting will break if the epoch is settled from EigenLayer

## Summary
 If a withdrawal request is made after queueCurrentEpochSettlement is called, accounting will break if the epoch is settled from EigenLayer
 
## Vulnerability Detail
When there isnâ€™t enough asset shares in the deposit pool to pay out a withdrawal request, the remaining shares are queued from EigenLayer.

At this point another user can also request a withdrawal, which will call `queueWithdrawal` and add `sharesOwed` and `amountToBurnAtSettlement` . After this `settleEpochFromEigenLayer` is called and the epoch is settled.

The problem occurs when the two users claim their withdrawals. Notice that `sharesOwed` and `amountToBurnAtSettlement` got increased correctly, but `assetsReceived` only account for the assets that were received on the first rebalance and only account for the assets that the first user should receive.

```solidity
amountOut = userSummary.sharesOwed.mulDiv(epochWithdrawals.assetsReceived, epochWithdrawals.sharesOwed);
```

Now the two users will claim both their shares, but `assetsReceived` will only account for the assets that one of the users should receive.

Because of this the two users will claim much less assets then they should, leading to a loss of funds.

## Proof of Concept
Paste the following inside `RioLRTWithdrawalQueue.t.sol` and first run `forge test --mt test_claimWithdrawalsForEpochSomeEtherPaidFromEigenLayerGood -vv` then `forge test --mt test_claimWithdrawalsForEpochSomeEtherPaidFromEigenLayerBad -vv`

```solidity
function test_claimWithdrawalsForEpochSomeEtherPaidFromEigenLayerBad() public {
        uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
        address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

        // Deposit ETH, rebalance, verify the validator withdrawal credentials, and deposit again.
        uint256 depositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
        reETH.coordinator.depositETH{value: depositAmount}();

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
        uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);
        reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE}();

        // Request a withdrawal and rebalance.
        uint256 withdrawalAmount = ETH_DEPOSIT_SIZE + 1 ether;
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);
        skip(reETH.coordinator.rebalanceDelay());

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Alice also requests a withdrawal after the rebalance
        address alice = address(1);
        vm.deal(alice, ETH_DEPOSIT_SIZE * 2);
        uint256 withdrawalAmountAlice = ETH_DEPOSIT_SIZE + 1 ether;
        vm.prank(alice);
        reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE + 2 ether}();
        vm.prank(alice);
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmountAlice);

        // Validate reETH total supply and process withdrawals.
        verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

        // Settle the withdrawal epoch.
        uint256 withdrawalEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);
        IDelegationManager.Withdrawal[] memory withdrawals = new IDelegationManager.Withdrawal[](1);
        withdrawals[0] = IDelegationManager.Withdrawal({
            staker: operatorDelegator,
            delegatedTo: address(1),
            withdrawer: address(reETH.withdrawalQueue),
            nonce: 0,
            startBlock: 1,
            strategies: BEACON_CHAIN_STRATEGY.toArray(),
            shares: uint256(1 ether).toArray()
        });
        reETH.withdrawalQueue.settleEpochFromEigenLayer(ETH_ADDRESS, withdrawalEpoch, withdrawals, new uint256[](1));

        // Assert epoch summary details.
        IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary =
            reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
        console.log("[BAD] Epoch assets received: ", epochSummary.assetsReceived);
        console.log("[BAD] Epoch share value of assets received: ", epochSummary.shareValueOfAssetsReceived);
        console.log("[BAD] Restaking tokens burned at settlement", epochSummary.amountToBurnAtSettlement);

        // Claim and assert withdrawal.
        uint256 balanceBefore = address(this).balance;
        uint256 amountOutThis = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
            IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
        );

        vm.prank(alice);
        uint256 amountOutAlice = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
            IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
        );
        IRioLRTWithdrawalQueue.UserWithdrawalSummary memory userSummary =
            reETH.withdrawalQueue.getUserWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch, address(this));

        console.log("[BAD] Amount out after claim of Alice: ", amountOutAlice);
        console.log("[BAD] Amount out after claim of address(this): ", amountOutThis);
    }
```

```solidity
function test_claimWithdrawalsForEpochSomeEtherPaidFromEigenLayerGood() public {
        uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
        address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

        // Deposit ETH, rebalance, verify the validator withdrawal credentials, and deposit again.
        uint256 depositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
        reETH.coordinator.depositETH{value: depositAmount}();

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
        uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);
        reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE}();

        // Request a withdrawal and rebalance.
        uint256 withdrawalAmount = ETH_DEPOSIT_SIZE + 1 ether;
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);
        skip(reETH.coordinator.rebalanceDelay());

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Validate reETH total supply and process withdrawals.
        verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

        // Settle the withdrawal epoch.
        uint256 withdrawalEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);
        IDelegationManager.Withdrawal[] memory withdrawals = new IDelegationManager.Withdrawal[](1);
        withdrawals[0] = IDelegationManager.Withdrawal({
            staker: operatorDelegator,
            delegatedTo: address(1),
            withdrawer: address(reETH.withdrawalQueue),
            nonce: 0,
            startBlock: 1,
            strategies: BEACON_CHAIN_STRATEGY.toArray(),
            shares: uint256(1 ether).toArray()
        });
        reETH.withdrawalQueue.settleEpochFromEigenLayer(ETH_ADDRESS, withdrawalEpoch, withdrawals, new uint256[](1));

        // Assert epoch summary details.
        IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary =
            reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
        console.log("[GOOD] Epoch assets received: ", epochSummary.assetsReceived);
        console.log("[GOOD] Epoch share value of assets received: ", epochSummary.shareValueOfAssetsReceived);
        console.log("[GOOD] Restaking tokens burned at settlement", epochSummary.amountToBurnAtSettlement);

        // Claim and assert withdrawal.
        uint256 balanceBefore = address(this).balance;
        uint256 amountOut = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
            IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
        );
        IRioLRTWithdrawalQueue.UserWithdrawalSummary memory userSummary =
            reETH.withdrawalQueue.getUserWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch, address(this));

        console.log("[GOOD] Amount out after claim of address(this): ", amountOut);
    }
```
Logs:

Without requesting another withdrawal

[GOOD] Epoch assets received:  33000000000000000000
[GOOD] Epoch share value of assets received:  33000000000000000000
[GOOD] Restaking tokens burned at settlement 1000000000000000032
[GOOD] Amount out after claim of address(this):  33000000000000000000

With requesting another withdrawal

[BAD] Epoch assets received:  33000000000000000000
[BAD] Epoch share value of assets received:  65999999999000000000
[BAD] Restaking tokens burned at settlement 34000000000000000032
[BAD] Amount out after claim of Alice:  16499999999749999999
[BAD] Amount out after claim of address(this):  16500000000250000000

## Impact
Loss of funds for the users

## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L130-L145

## Tool used
Manual Review
Foundry

## Recommendation
Not sure what the best fix would be here, disabling `queueWithdrawal` seems like the most straight forward fix, but it seems drastic as it would stop other users from queuing their withdrawals.
