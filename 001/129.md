Square Marmalade Sloth

medium

# Lack of slippage control on `RioLRTCoordinator.deposit()`

## Summary

Lack of slippage control on `RioLRTCoordinator.deposit()`, so users can't predict how many `reETH` they will get back at the moment of minting, as the price could be updated while the request is in the mempool.

## Vulnerability Detail

The `deposit` function of the `RioLRTCoordinator` contract, enables users to deposit assets into the protocol, getting `reETH` tokens in return. The function doesn't have any type of slippage control; this is relevant in the context of the `deposit` function since the amount of tokens received by the user is determined by an interaction with an oracle, meaning that the amount received in return may vary indefinitely while the request is waiting to be executed.

_Also, the users will have no defense against price manipulation attacks if they were to be found after the protocol's deployment._

As can be observed by looking at its parameters and implementation, the `deposit` function of the `RioLRTCoordinator` contract, doesn't have any type of slippage protection:

[RioLRTCoordinator.sol#L73-L88](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L73-L88)
```solidity
    /// @notice Deposits ERC20 tokens and mints restaking token(s) to the caller.
    /// @param asset The asset being deposited.
    /// @param amountIn The amount of the asset being deposited.
    function deposit(address asset, uint256 amountIn) external checkDeposit(asset, amountIn) returns (uint256 amountOut) {
        // Convert deposited asset amount to restaking tokens.
        amountOut = convertFromAssetToRestakingTokens(asset, amountIn);

        // Pull tokens from the sender to the deposit pool.
        IERC20(asset).safeTransferFrom(msg.sender, address(depositPool()), amountIn);

        // Mint restaking tokens to the caller.
        token.mint(msg.sender, amountOut);

        emit Deposited(msg.sender, asset, amountIn, amountOut);
    }
```

The amount of tokens to be minted, for the deposited amount, is determined by the `convertFromAssetToRestakingTokens` function of the same contract:

[RioLRTCoordinator.sol#L185-L191](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L185-L191)
```solidity
    /// @notice Converts an asset amount to its equivalent value in restaking tokens.
    /// @param asset The address of the asset to convert.
    /// @param amount The amount of the asset to convert.
    function convertFromAssetToRestakingTokens(address asset, uint256 amount) public view returns (uint256) {
        uint256 value = assetRegistry().convertToUnitOfAccountFromAsset(asset, amount);
        return convertFromUnitOfAccountToRestakingTokens(value);
    }
```

As can be observed, this function is calling `RioLRTAssetRegistry.convertToUnitOfAccountFromAsset()`:

[RioLRTAssetRegistry.sol#L184-L196](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L184-L196)
```solidity
    /// @notice Converts an asset amount to its equivalent value in the unit of account. The unit of
    /// account is the price feed's quote asset.
    /// @param asset The address of the asset to convert.
    /// @param amount The amount of the asset to convert.
    function convertToUnitOfAccountFromAsset(address asset, uint256 amount) public view returns (uint256) {
        if (asset == ETH_ADDRESS) {
            return amount;
        }
        address priceFeed = assetInfo[asset].priceFeed;
        uint256 price = getPrice(priceFeed);


        return _normalizeDecimals(price * amount / priceScale, assetInfo[asset].decimals, priceFeedDecimals);
    }
```

which in turn calls `getPrice` from the `IPriceFeed` interface, which is an external oracle used to determine how many tokens to mint:

[RioLRTAssetRegistry.sol#L233-L240](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L233-L240)
```solidity
    /// @dev Get the current price from the provided price feed.
    /// @param priceFeed The price feed contract address.
    function getPrice(address priceFeed) public view returns (uint256) {
        if (priceFeed == address(0)) {
            revert INVALID_PRICE_FEED();
        }
        return IPriceFeed(priceFeed).getPrice();
    }
```

## Impact

The users have no way to predict how many `reETH` they will get back at the moment of minting, as the price could be updated while the request is in the mempool.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L74-L88

## Tool used

Manual Review

## Recommendation

Add a slippage protection parameter to the `deposit` function, to let users decide the minimum amount of tokens to be received, with a relative check after minting.