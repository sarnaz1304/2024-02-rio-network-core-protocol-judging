Rough Golden Dog

high

# `RioLRTWithdrawalQueue:queueCurrentEpochSettlement` does not update the epoch, resulting in disabled `rebalance`

## Summary

When `RioLRTCoordinator:rebalance` is called, depending the shares owend and assets in the deposit pool, either `RioLRTWithdrawalQueue:settleCurrentEpoch` or `RioLRTWithdrawalQueue:queueCurrentEpochSettlement` is called.

In the case the `RioLRTWithdrawalQueue:queueCurrentEpochSettlement` is called, it will not properly update the epoch for the asset to rebalance. As the result:
1. the users might not get the expected asset as withdrawal
3. no more rebalance is possible.


## Vulnerability Detail

https://gist.github.com/lemonmon1984/4c9941a9da45e05a058757b68e16ecf5

The above gist contains the full proof of concept demonstrate the issue. Below is the relevant part of code:

```solidity
    function test_claimWithdrawalsForEpochNotUpdated_poc() public {
        uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
        address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

        // user 'this'
        // Deposit ETH, rebalance, and verify the validator withdrawal credentials.
        uint256 depositAmount = (ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance)/2;
        reETH.coordinator.depositETH{value: depositAmount}();

        // another user alice also deposits
        address alice = address(0xa11ce);
        vm.deal(alice, depositAmount);
        vm.prank(alice);
        reETH.coordinator.depositETH{value: depositAmount}();


        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
        uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);

        // Request a withdrawal from 'this'
        uint256 withdrawalAmount = 10 ** 18 - 1;
        uint256 expectedAmountOut = reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);
        skip(reETH.coordinator.rebalanceDelay());

        // After rebalance, the current epoch should be closed
        // but epoch is not updated and the withdrawal for the current epoch is still open
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Unknowingly the alice is withdrawing
        vm.prank(alice);
        uint expectedAmountOut2 = reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);

        // Ensure no reETH has been burned yet and process withdrawals.
        assertEq(reETH.token.totalSupply(), ETH_DEPOSIT_SIZE);
        verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

        // Settle the withdrawal epoch.
        uint256 withdrawalEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);
        IDelegationManager.Withdrawal[] memory withdrawals = new IDelegationManager.Withdrawal[](1);
        withdrawals[0] = IDelegationManager.Withdrawal({
            staker: operatorDelegator,
            delegatedTo: address(1),
            withdrawer: address(reETH.withdrawalQueue),
            nonce: 0,
            startBlock: 1,
            strategies: BEACON_CHAIN_STRATEGY.toArray(),
            shares: expectedAmountOut.toArray()
        });
        reETH.withdrawalQueue.settleEpochFromEigenLayer(ETH_ADDRESS, withdrawalEpoch, withdrawals, new uint256[](1));

        // Assert epoch summary details.
        IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary =
            reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
        assertTrue(epochSummary.settled);
        // only the originally requested withdrawal is received (withdrawal from 'this')
        assertEq(epochSummary.assetsReceived, expectedAmountOut);
        // however it thinks the share for the received asset is from both 'this' and alice
        assertEq(epochSummary.shareValueOfAssetsReceived, expectedAmountOut + expectedAmountOut2);

        // Claim and assert withdrawal.
        uint256 balanceBefore = address(this).balance;
        uint256 amountOut = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
            IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
        );
        IRioLRTWithdrawalQueue.UserWithdrawalSummary memory userSummary =
            reETH.withdrawalQueue.getUserWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch, address(this));

        // as the result 'this' will only receive the half of what is deserved
        assertTrue(userSummary.claimed);
        assertEq(amountOut, expectedAmountOut / 2);
        assertEq(address(this).balance - balanceBefore, expectedAmountOut / 2);

        balanceBefore = alice.balance;
        // alice also claims
        vm.prank(alice);
        amountOut = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
            IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
        );
        assertEq(amountOut, expectedAmountOut2 / 2);
        assertEq(alice.balance - balanceBefore, expectedAmountOut2 / 2);

        ///// It should be now next epoch but it is not
        expectedAmountOut2 = reETH.coordinator.requestWithdrawal(ETH_ADDRESS, 1 ether);
        skip(reETH.coordinator.rebalanceDelay());

        // no more rebalance possible due to this
        vm.expectRevert(IRioLRTWithdrawalQueue.WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH.selector);
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
    }
```

The step by step taken in the above function is following:
1. operator delegator is added
1. `this` (which is the test contract) deposits
1. alice deposits
1. EOA does rebalance and verify credentials for the validator
1. `this` will request a withdraw
1. EOA calls rebalance again, this will call the `queueCurrentEpochSettlement`. Even though the current epoch is not yet settled, the epoch should be closed for the further withdrawal by increasing the epoch. But the epoch is NOT increased and cause the following issues
1. Alice request withdraw
1. the epoch is settled via `settleEpochFromEigenLayer`
1. `this` and alice claim their withdrawals but will get only the half of the asset expected
1. if a new withdrawal is requested and rebalance is called, it will revert



## Impact

1. users will get less assets than expected. The shares which could not be converted back to asset will be locked
1. no more rebalance is possible, thus no more withdrawing or depositing

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L177-L271

## Tool used

Manual Review

## Recommendation

update the epoch in `RioLRTWithdrawalQueue:queueCurrentEpochSettlement`.

```solidity
// RioLRTWithdrawalQueue:queueCurrentEpochSettlement
+        currentEpochsByAsset[asset] += 1;
```

