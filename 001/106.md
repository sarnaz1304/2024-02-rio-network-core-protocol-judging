Exotic Parchment Tadpole

medium

# RioLRTCoordinator.requestWithdrawal() allows for slippage

## Summary

When a user requests to withdraw their deposited underlying tokens via requestWithdrawal(), the user can not provide a minimum amount of shares they would like to receive. Because the shares that they are owed are dependent on oracle prices, some slippage can occur. This can result in the user receiving less tokens than expected.

## Vulnerability Detail

As you can see in the requestWithdrawal() function below there are no slippage checks to ensure that the sharesOwed does not meet a minimum amount:

```solidity
function requestWithdrawal(address asset, uint256 amountIn) external checkWithdrawal(asset, amountIn) returns (uint256 sharesOwed) {
    // Determine the amount of shares owed to the withdrawer using the current exchange rate.
    sharesOwed = convertToSharesFromRestakingTokens(asset, amountIn);

    // If requesting ETH, reduce the precision of the shares owed to the nearest Gwei,
    // which is the smallest unit of account supported by EigenLayer.
    if (asset == ETH_ADDRESS) sharesOwed = sharesOwed.reducePrecisionToGwei();

    // Pull restaking tokens from the sender to the withdrawal queue.
    token.safeTransferFrom(msg.sender, address(withdrawalQueue()), amountIn);

    // Ensure there are enough shares to cover the withdrawal request, and queue the withdrawal.
    uint256 availableShares = assetRegistry().convertToSharesFromAsset(asset, assetRegistry().getTotalBalanceForAsset(asset));
    if (sharesOwed > availableShares - withdrawalQueue().getSharesOwedInCurrentEpoch(asset)) {
        revert INSUFFICIENT_SHARES_FOR_WITHDRAWAL();
    }
    withdrawalQueue().queueWithdrawal(msg.sender, asset, sharesOwed, amountIn);
}
```

The forge test below shows what can happen if the transaction is not immediately processed and the oracle price changes between the time the transaction is submitted and processed. Toggling the `simulateSlippage` will show how the amount of shares owed can change: 

```solidity
function test_requestERC20WithdrawalSlippageCheck() public {
    uint256 amount = 50e18;

    cbETH.approve(address(reLST.coordinator), amount);
    uint256 amountOut = reLST.coordinator.deposit(CBETH_ADDRESS, amount);

    bool simulateSlippage = true;
    if (simulateSlippage) {
        (,address priceFeed,,,) = reLST.assetRegistry.assetInfo(CBETH_ADDRESS);
        vm.mockCall(
            priceFeed,
            abi.encodeWithSelector(IPriceFeed.getPrice.selector),
            // AUDIT: price is increased to simulate slippage
            abi.encode(1155000000000000000)
        );
    } else {
        (,address priceFeed,,,) = reLST.assetRegistry.assetInfo(CBETH_ADDRESS);
        vm.mockCall(
            priceFeed,
            abi.encodeWithSelector(IPriceFeed.getPrice.selector),
            abi.encode(1055500000000000000)
        );
    }

    reLST.coordinator.requestWithdrawal(CBETH_ADDRESS, amountOut);

    uint256 currentEpoch = reLST.withdrawalQueue.getCurrentEpoch(CBETH_ADDRESS);
    IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary =
        reLST.withdrawalQueue.getEpochWithdrawalSummary(CBETH_ADDRESS, currentEpoch);
    IRioLRTWithdrawalQueue.UserWithdrawalSummary memory userSummary =
        reLST.withdrawalQueue.getUserWithdrawalSummary(CBETH_ADDRESS, currentEpoch, address(this));

    console.log("user shares owed: ", userSummary.sharesOwed);

    // no slippage - 50000000000000000000
    // slippage    - 49999105577231643743
}

```

## Impact

Users may receive less shares owed than expected.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol?plain=1#L99-L116

## Tool used

Manual Review

## Recommendation

Add a slippage check argument for users when calling requestWithdrawal() to provide some users the ability to control how many tokens they will receive.
