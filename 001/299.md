Early Currant Stork

high

# RioLRTWithdrawalQueue.sol#settleEpochFromEigenLayer() -  currentEpochsByAsset[asset] isn’t incremented, bricking the entire withdrawal logic for the asset

## Summary
currentEpochsByAsset[asset] isn’t incremented, bricking the entire withdrawal logic for the asset 

## Vulnerability Detail
If a user requests a withdrawal for more tokens than the deposit pool currently has, the protocol will queue withdrawals from operators, this will in turn queue q withdrawal from EigenLayer.

Now the user has to manually call `settleEpochFromEigenLayer` which goes off without any problems and sets the epoch as settled, but `settleEpochFromEigenLayer` doesn’t increment `currentEpochsByAsset[asset]`  which will brick the entire withdrawal and rebalance logic for that asset, because when `rebalance` is called `sharesOwed` will be the shares that are owed for the current epoch, and because `settleEpochFromEigenLayer` doesn’t increment `currentEpochsByAsset` and doesn’t set `sharesOwed = 0` (as it should, otherwise it will break the claiming logic), the protocol will attempt to `_processUserWithdrawalsForCurrentEpoch` .

The function will always revert.
If the deposit pool can pay for the shares, the code goes through `settleCurrentEpoch` which will revert as it has the following check

```solidity
 function settleCurrentEpoch(address asset, uint256 assetsReceived, uint256 shareValueOfAssetsReceived) //ok
        external //ok
        onlyCoordinator //ok
    {
        uint256 currentEpoch = getCurrentEpoch(asset); //ok

        EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch); //ok
        //@audit-info can we force this in some way, to brick the withdrawals
        if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH(); //ok
->      if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED(); //ok
```

If the deposit pool can’t pay for the shares, it will queue them from EigenLayer and will then hit `queueCurrentEpochSettlement` which will revert because of the check for the `aggregateRoot`

```solidity
function queueCurrentEpochSettlement(
        address asset,
        uint256 assetsReceived,
        uint256 shareValueOfAssetsReceived,
        bytes32 aggregateRoot
    ) external onlyCoordinator {
        //@audit-info can we force this?
        if (aggregateRoot == bytes32(0)) revert INVALID_AGGREGATE_WITHDRAWAL_ROOT();

        uint256 currentEpoch = getCurrentEpoch(asset);

        EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
        //@audit-info can this be used to brick rebalancing?
->      if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();
        if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
        if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();
```

At this point, it’s impossible to rebalance the asset, as every time `rebalance` is called, the code will attempt to `_processUserWithdrawalsForCurrentEpoch` which in any case, will revert, completely bricking the function.

## Proof of Concept
Paste the following inside `RioLRTWithdrawalQueue.t.sol` and run `forge test --mt test_settleEpochFromEigenLayerDoesntIncrementCurrentEpoch -vvvv` .

```solidity
function test_settleEpochFromEigenLayerDoesntIncrementCurrentEpoch() public {
        uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
        address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

        // Deposit ETH, rebalance, verify the validator withdrawal credentials, and deposit again.
        uint256 depositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
        reETH.coordinator.depositETH{value: depositAmount}();

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
        uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);
        reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE}();

        // Request a withdrawal and rebalance.
        uint256 withdrawalAmount = ETH_DEPOSIT_SIZE + 1 ether;
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);
        skip(reETH.coordinator.rebalanceDelay());

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Validate reETH total supply and process withdrawals.
        assertApproxEqAbs(reETH.token.totalSupply(), ETH_DEPOSIT_SIZE, 100);
        verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

        // Settle the withdrawal epoch.
        uint256 withdrawalEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);
        IDelegationManager.Withdrawal[] memory withdrawals = new IDelegationManager.Withdrawal[](1);
        withdrawals[0] = IDelegationManager.Withdrawal({
            staker: operatorDelegator,
            delegatedTo: address(1),
            withdrawer: address(reETH.withdrawalQueue),
            nonce: 0,
            startBlock: 1,
            strategies: BEACON_CHAIN_STRATEGY.toArray(),
            shares: uint256(1 ether).toArray()
        });

        // Current epoch is 0
        assertEq(reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS), 0);

        reETH.withdrawalQueue.settleEpochFromEigenLayer(ETH_ADDRESS, withdrawalEpoch, withdrawals, new uint256[](1));

        // Claim and assert withdrawal.
        uint256 balanceBefore = address(this).balance;
        uint256 amountOut = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
            IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
        );

        // Current epoch isn't incremented
        assertEq(reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS), 0);

        vm.warp(block.timestamp + 10 days);

        // Impossible to queueCurrentEpochSettlement, because the agregateRoot is already set to != 0
        vm.expectRevert();
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Impossible to settleCurrentEpoch, because the epoch is already settled
        vm.deal(address(reETH.depositPool), 100e18);
        vm.expectRevert();
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
    }
```

## Impact
Rebalance will be completely bricked for the asset

## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216-L271

## Tool used
Manual Review
Foundry

## Recommendation
Increment `currentEpochsByAsset[asset]`
