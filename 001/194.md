Straight Neon Blackbird

medium

# After calling queueCurrentEpochSettlement, it is possible to call settleCurrentEpoch with the same epoch. The EigenLayer withdrawal amount and the tokens previously transfered from the DepositPool are locked.

## Summary

`WithdrawalQueue.queueCurrentEpochSettlement` does not update `currentEpochsByAsset`. Therefore, if `rebalance` is requested again, and if there are enough tokens in the DepositPool this time, it will call `WithdrawalQueue.settleCurrentEpoch`. 

The token that was requested for withdrawal to the EigenLayer is locked because `WithdrawalQueue.settleEpochFromEigenLayer` cannot be called. Also, the tokens at WithdrawlQueue that were previously received from the DepositPool are also locked.

## Vulnerability Detail

`WithdrawalQueue.queueCurrentEpochSettlement` does not update `currentEpochsByAsset`. Therefore, when `rebalance` is requested again, if there are enough tokens in the DepositPool, it will call `WithdrawalQueue.settleCurrentEpoch`.

Since it takes a [7-day delay](https://discord.com/channels/812037309376495636/1209514827442167839/1212791096875421776) for the EigenLayer withdrawal request to be completed, there is enough time for rebalance to be called again.

> If we can cover all withdrawals from the deposit pool then it will only be as long as the rebalance delay (24 hours), otherwise, we'll have to queue a withdrawal from EigenLayer, which currently takes 7 days
> 

```solidity
function _processUserWithdrawalsForCurrentEpoch(address asset, uint256 sharesOwed) internal {
    IRioLRTWithdrawalQueue withdrawalQueue_ = withdrawalQueue();
@>  (uint256 assetsSent, uint256 sharesSent) = depositPool().transferMaxAssetsForShares(
        asset,
        sharesOwed,
        address(withdrawalQueue_)
    );
    uint256 sharesRemaining = sharesOwed - sharesSent;

    // Exit early if all pending withdrawals were paid from the deposit pool.
@>  if (sharesRemaining == 0) {
@>      withdrawalQueue_.settleCurrentEpoch(asset, assetsSent, sharesSent);
        return;
    }

    address strategy = assetRegistry().getAssetStrategy(asset);
    bytes32 aggregateRoot = OperatorOperations.queueWithdrawalFromOperatorsForUserSettlement(
        operatorRegistry(),
        strategy,
        sharesRemaining
    );
    withdrawalQueue_.queueCurrentEpochSettlement(asset, assetsSent, sharesSent, aggregateRoot);
}
```

When `WithdrawalQueue.settleCurrentEpoch` is called, the current epoch is settled. So the previous withdrawal request cannot be settled anymore.

```solidity
function settleEpochFromEigenLayer(
    address asset,
    uint256 epoch,
    IDelegationManager.Withdrawal[] calldata queuedWithdrawals,
    uint256[] calldata middlewareTimesIndexes
) external {
    EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, epoch);
    if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
@>  if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();
    ...
}
```

EigenLayer also does not have the ability to cancel a withdrawal request. You can't cancel a withdrawal request to EigenLayer, but you also can't settle it and receive the token. The tokens are locked and cannot be retrieved.

Also, the tokens received from the `DepositPool` at the time of `WithdrawalQueue.queueCurrentEpochSettlement` are also locked in the WithdrawQueue.

This is PoC. Add this to the RioLRTWithdrawalQueue.t.sol file and run it. Add the import code at the top of the file.

```solidity
import {IStrategy} from 'contracts/interfaces/eigenlayer/IStrategy.sol';

function test_PoCCanResettle() public {
    uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
    address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

    // Deposit ETH, rebalance, verify the validator withdrawal credentials, and deposit again.
    uint256 depositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
    reETH.coordinator.depositETH{value: depositAmount}();

    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);

    uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);

    reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE}();

    // Request a withdrawal and rebalance.
    uint256 beforeEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);

    uint256 withdrawalAmount = ETH_DEPOSIT_SIZE + 1 ether;
    reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);
    skip(reETH.coordinator.rebalanceDelay());

    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);

    uint256 afterEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);
    uint256 withdrawalEpoch = afterEpoch;

    assertEq(beforeEpoch, afterEpoch, "epoch not changed"); // epoch not changed

    // Assert epoch summary details.
    IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary =
        reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
    assertFalse(epochSummary.settled);
    assertEq(epochSummary.assetsReceived, ETH_DEPOSIT_SIZE);
    assertEq(epochSummary.shareValueOfAssetsReceived, ETH_DEPOSIT_SIZE);
    assertEq(epochSummary.sharesOwed, withdrawalAmount); // ETH_DEPOSIT_SIZE + 1 ether

    // check balance of withdrawal queue
    assertEq(address(reETH.withdrawalQueue).balance, ETH_DEPOSIT_SIZE);

    // A sufficient amount of tokens have been deposited
    reETH.coordinator.depositETH{value: withdrawalAmount}();

    skip(reETH.coordinator.rebalanceDelay());

    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS); // settled

    // check balance of withdrawal queue
    assertEq(address(reETH.withdrawalQueue).balance, ETH_DEPOSIT_SIZE + withdrawalAmount); // old deposit pool token remains

    epochSummary =
        reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
    assertTrue(epochSummary.settled); // settled
    assertEq(epochSummary.assetsReceived, withdrawalAmount);
    assertEq(epochSummary.shareValueOfAssetsReceived, withdrawalAmount);
    assertEq(epochSummary.sharesOwed, withdrawalAmount); // ETH_DEPOSIT_SIZE + 1 ether

    verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

    // cannot settleEpochFromEigenLayer
    IDelegationManager.Withdrawal[] memory withdrawals = new IDelegationManager.Withdrawal[](1);
    withdrawals[0] = IDelegationManager.Withdrawal({
        staker: operatorDelegator,
        delegatedTo: address(1),
        withdrawer: address(reETH.withdrawalQueue),
        nonce: 0,
        startBlock: 1,
        strategies: BEACON_CHAIN_STRATEGY.toArray(),
        shares: uint256(1 ether).toArray()
    });

    vm.expectRevert(abi.encodeWithSelector(IRioLRTWithdrawalQueue.EPOCH_ALREADY_SETTLED.selector)); // cannot settle withdrawal of eigen layer
    reETH.withdrawalQueue.settleEpochFromEigenLayer(ETH_ADDRESS, withdrawalEpoch, withdrawals, new uint256[](1));

    assertEq(delegationManager.operatorShares(address(0x1), IStrategy(BEACON_CHAIN_STRATEGY)), ETH_DEPOSIT_SIZE - 1 ether); // operator's share at eigen layer
}
```

## Impact

Unable to settle the withdrawal request to EigenLayer, the tokens are locked. Also, the tokens previously received from the DepositPool are also locked.

## Code Snippet

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L177](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L177)

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L247-L258](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L247-L258)

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L151](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L151)

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L104](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L104)

## Tool used

Manual Review

## Recommendation

```diff
function settleCurrentEpoch(address asset, uint256 assetsReceived, uint256 shareValueOfAssetsReceived)
    external
    onlyCoordinator
{
    uint256 currentEpoch = getCurrentEpoch(asset);

    EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
    if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
    if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();
+   if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();

    epochWithdrawals.settled = true;
    epochWithdrawals.assetsReceived = SafeCast.toUint120(assetsReceived);
    epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(shareValueOfAssetsReceived);

    token.burn(epochWithdrawals.amountToBurnAtSettlement);
    currentEpochsByAsset[asset] += 1;

    emit EpochSettledFromDepositPool(currentEpoch, asset, assetsReceived);
}

function queueCurrentEpochSettlement(
    address asset,
    uint256 assetsReceived,
    uint256 shareValueOfAssetsReceived,
    bytes32 aggregateRoot
) external onlyCoordinator {
    if (aggregateRoot == bytes32(0)) revert INVALID_AGGREGATE_WITHDRAWAL_ROOT();

    uint256 currentEpoch = getCurrentEpoch(asset);

    EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
    if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();
    if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
    if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();

    uint256 restakingTokensToBurn;
    if (assetsReceived > 0) {
        epochWithdrawals.assetsReceived = SafeCast.toUint120(assetsReceived);
        epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(shareValueOfAssetsReceived);

        restakingTokensToBurn = epochWithdrawals.amountToBurnAtSettlement.mulWad(
            shareValueOfAssetsReceived.divWad(epochWithdrawals.sharesOwed)
        );
        token.burn(restakingTokensToBurn);

        epochWithdrawals.amountToBurnAtSettlement -= restakingTokensToBurn;
    }
    epochWithdrawals.aggregateRoot = aggregateRoot;
+   currentEpochsByAsset[asset] += 1;

    emit EpochQueuedForSettlementFromEigenLayer(
        currentEpoch, asset, assetsReceived, shareValueOfAssetsReceived, restakingTokensToBurn, aggregateRoot
    );
}
```