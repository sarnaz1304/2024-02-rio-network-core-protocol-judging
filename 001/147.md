Strong Denim Hyena

medium

# Increase in exchange rate between queueing and rebalancing can break withdrawals

## Summary
The Eigenlayer shares exchange rate is a monotonically increasing value. There can be an increase in the shares exchange rate in the time between queueing and processing the Eigenlayer withdrawals. This increase in the exchange rate can break withdrawal processing because some of the assets withdrawn are from the Deposit Pool which do not benefit from the Eigenlayer yield.

## Vulnerability Detail
Below is an example scenario given the following state:
- Deposit Pool has 100 DAI
- Eigenlayer Shares exchange rate for DAI is at 2 DAI per share
- LRT holds 0 DAI shares

Alice requests withdrawal of 100 DAI and gets 50 shares owed (100 DAI / 2 DAI per share.) 
ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L99-L116
```solidity
function requestWithdrawal(address asset, uint256 amountIn) external checkWithdrawal(asset, amountIn) returns (uint256 sharesOwed) {
    // Determine the amount of shares owed to the withdrawer using the current exchange rate.
    sharesOwed = convertToSharesFromRestakingTokens(asset, amountIn);
    // ... snip ...
    withdrawalQueue().queueWithdrawal(msg.sender, asset, sharesOwed, amountIn);
}
```

The shares owed is computed using the Eigenlayer Strategy's exchange rate for shares. 

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L204-L209
```solidity
// In Coordinator
function convertToSharesFromRestakingTokens(address asset, uint256 amount) public view returns (uint256 shares) {
    uint256 assetAmount = convertToAssetFromRestakingTokens(asset, amount);
    return assetRegistry().convertToSharesFromAsset(asset, assetAmount);
}
```

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L214-L221
```solidity
// In AssetRegistry
function convertToSharesFromAsset(address asset, uint256 amount) public view returns (uint256 shares) {
    address strategy = assetInfo[asset].strategy;
    if (strategy == BEACON_CHAIN_STRATEGY) {
        return amount;
    }
    shares = IStrategy(strategy).underlyingToSharesView(amount);
}
```

On rebalancing, the share rate of DAI has increased to 2.5 DAI per share. The 100 DAI in the Deposit Pool is no longer enough to pay the 50 shares owed to Alice at that new rate. Only 40 shares are covered and the remaining 10 shares will need to be withdrawn from Eigenlayer. 

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol#L75-L96
```solidity
function transferMaxAssetsForShares(address asset, uint256 sharesRequested, address recipient)
    external
    onlyCoordinator
    returns (uint256, uint256)
{
    uint256 poolBalance = asset.getSelfBalance();
    uint256 poolBalanceShareValue = assetRegistry().convertToSharesFromAsset(asset, poolBalance);
    // ... snip ...
    asset.transferTo(recipient, poolBalance);

    return (poolBalance, poolBalanceShareValue);
}
```

ref: https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L245-L267
```solidity
function _processUserWithdrawalsForCurrentEpoch(address asset, uint256 sharesOwed) internal {
    IRioLRTWithdrawalQueue withdrawalQueue_ = withdrawalQueue();
    (uint256 assetsSent, uint256 sharesSent) = depositPool().transferMaxAssetsForShares(
        asset,
        sharesOwed,
        address(withdrawalQueue_)
    );
    uint256 sharesRemaining = sharesOwed - sharesSent;

    // Exit early if all pending withdrawals were paid from the deposit pool.
    if (sharesRemaining == 0) {
        withdrawalQueue_.settleCurrentEpoch(asset, assetsSent, sharesSent);
        return;
    }

    address strategy = assetRegistry().getAssetStrategy(asset);
    bytes32 aggregateRoot = OperatorOperations.queueWithdrawalFromOperatorsForUserSettlement(
        operatorRegistry(),
        strategy,
        sharesRemaining
    );
    withdrawalQueue_.queueCurrentEpochSettlement(asset, assetsSent, sharesSent, aggregateRoot);
}
```

In this case, the rebalancing reverts since there are not enough shares in Eigenlayer because the LRT holds 0 DAI shares.

## Impact
Withdrawals will unexpectedly break every time the LRT is in a similar state when some of the assets are in the Deposit Pool while the Eigenlayer shares exhange rate increased between the time of withdrawal queueing and processing. Anyone can force this state by donating enough to increase the Eigenlayer shares exchange rate before rebalancing or it can occur naturally.

## Code Snippet
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L99-L116
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L204-L209
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L214-L221
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol#L75-L96
- https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L245-L267

## Tool used

Manual Review

## Recommendation
Consider recording the asset share's current exchange rate on every rebalancing to keep the exchange rate static between rebalances. Note that the initial exchange rate needs to be recorded and this can be done on the first deposit of the asset.