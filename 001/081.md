Straight Neon Blackbird

medium

# New withdrawal requests can be created in the same epoch after WithdrawalQueue.queueCurrentEpochSettlement, causing loss of tokens for the user

## Summary

`WithdrawalQueue.queueCurrentEpochSettlement` does not update `currentEpochsByAsset` , so new withdrawal requests can be created in the same epoch after that. However, in `WithdrawalQueue.settleEpochFromEigenLayer`, the epoch ends without getting asset tokens for new withdrawal requests. Therefore, all users who made withdrawal requests in this epoch will receive less tokens than they should have received.

## Vulnerability Detail

`WithdrawalQueue.queueCurrentEpochSettlement` does not update `currentEpochsByAsset`. So users can still request token withdrawals in the same epoch even after `WithdrawalQueue.queueCurrentEpochSettlement`.

When a withdrawal request is made to the EigenLayer, `EigenPod.verifyAndProcessWithdrawals` should be called, and after that, `WithdrawalQueue.settleEpochFromEigenLayer` should be called to get tokens from the EigenLayer and settle the epoch. Hence, there is a sufficient chance that other normal users may request withdrawals during this time.

Only the remaining share is requested for withdrawal from the EigenLayer in `OperatorOperations.queueWithdrawalFromOperatorsForUserSettlement`. This means that WithdrawalQueue can't get asset tokens for new withdrawal requests.

```solidity
function _processUserWithdrawalsForCurrentEpoch(address asset, uint256 sharesOwed) internal {
    ...
    uint256 sharesRemaining = sharesOwed - sharesSent;
    ...

    bytes32 aggregateRoot = OperatorOperations.queueWithdrawalFromOperatorsForUserSettlement(
        operatorRegistry(),
        strategy,
@>      sharesRemaining
    );
@>  withdrawalQueue_.queueCurrentEpochSettlement(asset, assetsSent, sharesSent, aggregateRoot);
}
```

 `WithdrawalQueue.settleEpochFromEigenLayer` assumes that the asset tokens received from the EigenLayer can fully cover sharesOwed and settles the epoch. However, due to the new withdrawal requests, sharesOwed is higher than before.

```solidity
function settleEpochFromEigenLayer(
    address asset,
    uint256 epoch,
    IDelegationManager.Withdrawal[] calldata queuedWithdrawals,
    uint256[] calldata middlewareTimesIndexes
) external {
    ...

    uint256 balanceBefore = asset.getSelfBalance();

    address[] memory assets = asset.toArray();
    bytes32[] memory roots = new bytes32[](queuedWithdrawalCount);

    IDelegationManager.Withdrawal memory queuedWithdrawal;
    for (uint256 i; i < queuedWithdrawalCount; ++i) {
        queuedWithdrawal = queuedWithdrawals[i];

        roots[i] = _computeWithdrawalRoot(queuedWithdrawal);
@>      delegationManager.completeQueuedWithdrawal(queuedWithdrawal, assets, middlewareTimesIndexes[i], true);

        // Decrease the amount of ETH queued for withdrawal. We do not need to validate the staker as
        // the aggregate root will be validated below.
        if (asset == ETH_ADDRESS) {
            IRioLRTOperatorDelegator(queuedWithdrawal.staker).decreaseETHQueuedForUserSettlement(
                queuedWithdrawal.shares[0]
            );
        }
    }
    if (epochWithdrawals.aggregateRoot != keccak256(abi.encode(roots))) {
        revert INVALID_AGGREGATE_WITHDRAWAL_ROOT();
    }
    epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(epochWithdrawals.sharesOwed);

@>  uint256 assetsReceived = asset.getSelfBalance() - balanceBefore;
@>  epochWithdrawals.assetsReceived += SafeCast.toUint120(assetsReceived);

    emit EpochSettledFromEigenLayer(epoch, asset, assetsReceived);
}
```

Although `sharesOwed` increases, `assetsReceived` is based on the past `sharesOwed`, so the user receives fewer tokens when they claim. All users who requested withdrawals in this epoch receive fewer tokens than they should receive. In other words, users lose tokens.

```solidity
function claimWithdrawalsForEpoch(ClaimRequest calldata request) public returns (uint256 amountOut) {
    ...
@>  amountOut = userSummary.sharesOwed.mulDiv(epochWithdrawals.assetsReceived, epochWithdrawals.sharesOwed);
    request.asset.transferTo(withdrawer, amountOut);

    emit WithdrawalsClaimedForEpoch(request.epoch, request.asset, withdrawer, amountOut);
}
```

This is PoC. Add this to the RioLRTWithdrawalQueue.t.sol file and run it.

```solidity

function test_PoCCurrentEpochsByAssetNotUpdated() public {
    address user1 = address(0xa);
    address user2 = address(0xb);
    vm.deal(user1, 100 ether);
    vm.deal(user2, 100 ether);

    uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
    address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

    // Deposit ETH, rebalance, verify the validator withdrawal credentials, and deposit again.
    uint256 depositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
    reETH.coordinator.depositETH{value: depositAmount}();

    vm.prank(user1, user1);
    reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE}();

    vm.prank(user2, user2);
    reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE}();

    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);
    uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);
    reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE}();

    // Request a withdrawal and rebalance.
    uint256 beforeEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);

    uint256 withdrawalAmount = ETH_DEPOSIT_SIZE + 1 ether;
    reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);
    skip(reETH.coordinator.rebalanceDelay());

    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);

    uint256 afterEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);
    uint256 withdrawalEpoch = afterEpoch;

    // Validate reETH total supply and process withdrawals.
    assertApproxEqAbs(reETH.token.totalSupply(), 3 * ETH_DEPOSIT_SIZE, 100, "totalSupply");
    assertEq(beforeEpoch, afterEpoch, "epoch not changed"); // epoch not changed

    // Assert epoch summary details.
    IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary =
        reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
    assertFalse(epochSummary.settled);
    assertEq(epochSummary.assetsReceived, ETH_DEPOSIT_SIZE);
    assertEq(epochSummary.shareValueOfAssetsReceived, ETH_DEPOSIT_SIZE);
    assertEq(epochSummary.sharesOwed, withdrawalAmount); // ETH_DEPOSIT_SIZE + 1 ether

    verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

    // user2 request to withdraw at same epoch
    vm.prank(user2, user2);
    reETH.coordinator.requestWithdrawal(ETH_ADDRESS, ETH_DEPOSIT_SIZE - 1 ether);        

    // Assert epoch summary details.
    epochSummary = reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
    assertEq(epochSummary.assetsReceived, ETH_DEPOSIT_SIZE);
    assertEq(epochSummary.shareValueOfAssetsReceived, ETH_DEPOSIT_SIZE);
    assertApproxEqAbs(epochSummary.sharesOwed, withdrawalAmount + ETH_DEPOSIT_SIZE - 1 ether, 1 gwei); // user2 withdraw at same epoch after queueCurrentEpochSettlement

    // Settle the withdrawal epoch.
    IDelegationManager.Withdrawal[] memory withdrawals = new IDelegationManager.Withdrawal[](1);
    withdrawals[0] = IDelegationManager.Withdrawal({
        staker: operatorDelegator,
        delegatedTo: address(1),
        withdrawer: address(reETH.withdrawalQueue),
        nonce: 0,
        startBlock: 1,
        strategies: BEACON_CHAIN_STRATEGY.toArray(),
        shares: uint256(1 ether).toArray()
    });
    reETH.withdrawalQueue.settleEpochFromEigenLayer(ETH_ADDRESS, withdrawalEpoch, withdrawals, new uint256[](1));

    // Assert epoch summary details.
    epochSummary = reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
    assertTrue(epochSummary.settled);
    assertEq(epochSummary.assetsReceived, withdrawalAmount);
    assertApproxEqAbs(epochSummary.shareValueOfAssetsReceived, withdrawalAmount + ETH_DEPOSIT_SIZE - 1 ether, 1 gwei);
    assertApproxEqAbs(epochSummary.sharesOwed, withdrawalAmount + ETH_DEPOSIT_SIZE - 1 ether, 1 gwei); 

    // Claim and assert withdrawal.
    uint256 balanceBefore = address(this).balance;
    uint256 amountOut = reETH.withdrawalQueue.claimWithdrawalsForEpoch(
        IRioLRTWithdrawalQueue.ClaimRequest({asset: ETH_ADDRESS, epoch: withdrawalEpoch})
    );
    IRioLRTWithdrawalQueue.UserWithdrawalSummary memory userSummary =
        reETH.withdrawalQueue.getUserWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch, address(this));

    assertTrue(userSummary.claimed);
    assertApproxEqAbs(amountOut, withdrawalAmount * withdrawalAmount / (withdrawalAmount + ETH_DEPOSIT_SIZE - 1 ether), 1 gwei);
    assertApproxEqAbs(address(this).balance - balanceBefore, withdrawalAmount * withdrawalAmount / (withdrawalAmount + ETH_DEPOSIT_SIZE - 1 ether), 1 gwei);
}
```

## Impact

All users who made withdrawal requests in this epoch will receive less tokens than they should have received. Users lose tokens.

## Code Snippet

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L261-L266](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L261-L266)

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L177](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L177)

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L104](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L104)

## Tool used

Manual Review

## Recommendation

Update `currentEpochsByAsset` in `RioLRTWithdrawalQueue.queueCurrentEpochSettlement`

```diff
function queueCurrentEpochSettlement(
    address asset,
    uint256 assetsReceived,
    uint256 shareValueOfAssetsReceived,
    bytes32 aggregateRoot
) external onlyCoordinator {
    if (aggregateRoot == bytes32(0)) revert INVALID_AGGREGATE_WITHDRAWAL_ROOT();

    uint256 currentEpoch = getCurrentEpoch(asset);

    EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
    if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();
    if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
    if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();

    uint256 restakingTokensToBurn;
    if (assetsReceived > 0) {
        epochWithdrawals.assetsReceived = SafeCast.toUint120(assetsReceived);
        epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(shareValueOfAssetsReceived);

        restakingTokensToBurn = epochWithdrawals.amountToBurnAtSettlement.mulWad(
            shareValueOfAssetsReceived.divWad(epochWithdrawals.sharesOwed)
        );
        token.burn(restakingTokensToBurn);

        epochWithdrawals.amountToBurnAtSettlement -= restakingTokensToBurn;
    }
    epochWithdrawals.aggregateRoot = aggregateRoot;
+   currentEpochsByAsset[asset] += 1;

    emit EpochQueuedForSettlementFromEigenLayer(
        currentEpoch, asset, assetsReceived, shareValueOfAssetsReceived, restakingTokensToBurn, aggregateRoot
    );
}
```