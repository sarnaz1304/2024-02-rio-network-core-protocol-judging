Rural Tweed Lemur

high

# Epoch currently queued for settlement might get settled via `rebalance()` leading to stuck funds

## Summary
Funds can get stuck in Eigenlayer because an epoch can be settled via [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) when it's already queued for settlement.

## Vulnerability Detail
Withdrawals requested for the current epoch are processed when [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) is called, this can happen in two ways:

**A)** If the balance of the [RioLRTDepositPool](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol) contract is greater than the total amount of withdrawals requested the funds are transferred to the [RioLRTWithdrawalQueue](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol) and the [epoch settled](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L256).

**B)** If the balance of the [RioLRTDepositPool](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol) contract is lower than the total amount of withdrawals requested the funds are [queued for withdrawal from Eigenlayer](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L261) and [the epoch queued for settlement](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L266). After an enforced delay of up to 30 days anybody can call [RioLRTWithdrawalQueue::settleEpochFromEigenLayer()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216C14-L216C39) to complete the flow and settle the epoch.

There is an instance in which an epoch may get settled via flow `A` after it has been queued for settlement via flow `B`, this is the scenario:
1. [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) is called but the funds available in the [RioLRTDepositPool](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol) are not enough to cover the amount requested for withdrawal, which triggers flow `B`.
2. The delay enforced by Eigenlayer to retrieve queued withdrawals is bigger than the delay enforced by the rio protocol to call [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) again.
3. In the meantime users deposit an amount of assets that is greater than the amount of requested withdrawals for the current epoch.
4. [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) becomes callable again.
5. [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) is called and because the amount of assets currently in the [RioLRTDepositPool](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol) is greater than the amount requested for withdrawal flow `A` is triggered.

At this point the current epoch is settled, which renders [RioLRTWithdrawalQueue::settleEpochFromEigenLayer()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216C14-L216C39) uncallable, leaving the funds queued for withdrawal from Eigenlayer stuck.

### POC
Copy-paste in `RioLRTCoordinator.t.sol`:
```solidity
function test_epochWronglySettled() public {
    uint256 initialDepositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
    address alice = makeAddr("alice");
    address bob = makeAddr("bob");
    deal(alice, initialDepositAmount);
    deal(bob, ETH_DEPOSIT_SIZE + 1 ether);
    vm.prank(alice);
    reETH.token.approve(address(reETH.coordinator), type(uint256).max);

    addOperatorDelegators(reETH.operatorRegistry, address(reETH.rewardDistributor), 1);

    //-> Alice deposits ETH in the pool
    vm.prank(alice);
    reETH.coordinator.depositETH{value: initialDepositAmount}();

    //-> ETH in the pool are deposited into Eigenlayer
    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);

    verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);

    skip(reETH.coordinator.rebalanceDelay());

    //-> Alices requests a withdrawal of all the funds she deposited
    uint256 toRedeem = reETH.token.balanceOf(alice);
    vm.prank(alice);
    reETH.coordinator.requestWithdrawal(ETH_ADDRESS, toRedeem);

    //-> Because the deposit pool doesn't have enough assets funds have to be withdrawn from Eigenlayer and the epoch settlement is queued
    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);
    IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary1 = reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, 0);
    assertNotEq(epochSummary1.aggregateRoot, bytes32(0));

    //-> Rebalance can be called again
    skip(reETH.coordinator.rebalanceDelay());

    //-> Bob deposits enough funds to cover Alice withdrawal request directly from the deposit pool
    vm.prank(bob);
    reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE + 1 ether}();

    //-> Alice withdrawal is processed and the epoch is settled
    vm.prank(EOA, EOA);
    reETH.coordinator.rebalance(ETH_ADDRESS);
    IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary2 = reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, 0);
    assertTrue(epochSummary2.settled);
}
```

## Impact
Funds are stuck in Eigenlayer and some users will be unable to redeem their LRT tokens.

## Code Snippet

## Tool used

Manual Review

## Recommendation
Revert in [RioLRTWithdrawalQueue::settleCurrentEpoch()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L151) when an epoch is currently queued for settlement. Reverting if`epochWithdrawals.aggregateRoot` is not `0` should be enough.
