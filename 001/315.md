Gorgeous Basil Locust

high

# Assets for withdrawal can be locked

## Summary

On withdrawal processing, when the deposit pool does not have enough assets to cover the shares owed for the current epoch, the available assets are transferred to the withdrawal queue, and the remaining assets are queued for withdrawal from EigenLayer. However, the next time rebalance is called, this fact is not taken into account, so additional assets can be transferred to the withdrawal queue, locking the assets previously transferred.

## Vulnerability Detail

When `RioLRTCoordinator.rebalance` is called, it [is checked](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L126) if there are shares owed for the current epoch. If there are, withdrawals are processed in [`_processUserWithdrawalsForCurrentEpoch`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L127-L129).

`_processUserWithdrawalsForCurrentEpoch` [transfers all required assets](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L247-L250) available in the deposit pool to the withdrawal queue contract. If those assets are enough to cover the shares owed, [the current epoch is settled](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L256). Otherwise, the epoch settlement is queued and the rest of the assets required to cover the shares owed are requested from EigenLayer. 

The problem is that in the case of a partial amount of the required assets being transferred to the withdrawal queue and the rest of the assets being queued for withdrawal from EigenLayer, the number of shares owed for the current epoch is not updated. This means that when rebalance is called again, if there are new assets available in the deposit pool they will be also transferred to the withdrawal queue. Then we have two scenarios:

1. The new amount of available assets does not cover the total shares owed. In this case, `RioLRTWithdrawalQueue.queueCurrentEpochSettlement` is executed again and [reverts](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L188).

2. The new amount of available assets covers the total shares owed. In this case, `RioLRTWithdrawalQueue.settleCurrentEpoch` is executed, so the amount of assets received in the epoch [is overwritten](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L162C26-L162C40), meaning that the assets previously transferred to the withdrawal queue are lost. Also, the assets queued for withdrawal from EigenLayer cannot be processed, as calls to `settleEpochFromEigenLayer` will now [revert](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L223). This means that the withdrawal from EigenLayer will not be able to be completed, as [only the address set as withdrawer can call `completeQueuedWithdrawal`](https://github.com/Layr-Labs/eigenlayer-contracts/blob/6de01c6c16d6df44af15f0b06809dc160eac0ebf/src/contracts/core/DelegationManager.sol#L566-L569), which means that these assets will also be locked.

## Impact

Assets transferred to the withdrawal queue contract and assets queued for withdrawal from EigenLayer will be locked.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L245-L267


## Proof of concept

Add the following code in `test/RioLRTCoordinator.t.sol`.

<details>

<summary>Test</summary>

```solidity
import "forge-std/console2.sol";

(...)

    function test_lockedAssets() public {
        // Ensure there is an operator to allocate to.
        addOperatorDelegators(reLST.operatorRegistry, address(reLST.rewardDistributor), 1);

        uint256 amount = 100e18;

        cbETH.approve(address(reLST.coordinator), type(uint256).max);

        // Deposit and push the balance into EigenLayer.
        uint256 amountOut = reLST.coordinator.deposit(CBETH_ADDRESS, amount);

        vm.prank(EOA, EOA);
        reLST.coordinator.rebalance(CBETH_ADDRESS);

        // Deposit again and request a withdrawal. Following this deposit,
        // both EigenLayer and the deposit pool will have `amount` cbETH.
        reLST.coordinator.deposit(CBETH_ADDRESS, amount);
        reLST.coordinator.requestWithdrawal(CBETH_ADDRESS, amountOut + 1e18);

        skip(reLST.coordinator.rebalanceDelay());

        vm.prank(EOA, EOA);
        reLST.coordinator.rebalance(CBETH_ADDRESS);

        // Not enough cbETH in the deposit pool, so `queueCurrentEpochSettlement` is executed
        uint256 epoch = reLST.withdrawalQueue.getCurrentEpoch(CBETH_ADDRESS);
        IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary = reLST.withdrawalQueue.getEpochWithdrawalSummary(CBETH_ADDRESS, epoch);
        console2.log("Assets received:  %s", epochSummary.assetsReceived);
        console2.log("Contract balance: %s", cbETH.balanceOf(address(reLST.withdrawalQueue)));

        // More cbETH is deposited, and the rebalance is executed again
        skip(reLST.coordinator.rebalanceDelay());
        reLST.coordinator.deposit(CBETH_ADDRESS, amount + 1e18);
        vm.prank(EOA, EOA);
        reLST.coordinator.rebalance(CBETH_ADDRESS);

        // Enough cbETH in the deposit pool, so `settleCurrentEpoch` is executed.
        // The withdrawal queue holds the assets received in the previous rebalance plus the
        // assets received in the current rebalance.
        // Only `assetsReceived` can be pulled from the contract, so the rest is locked.
        epochSummary = reLST.withdrawalQueue.getEpochWithdrawalSummary(CBETH_ADDRESS, epoch);
        console2.log("Assets received:  %s", epochSummary.assetsReceived);
        console2.log("Contract balance: %s", cbETH.balanceOf(address(reLST.withdrawalQueue)));
        console2.log("Assets locked:    %s", cbETH.balanceOf(address(reLST.withdrawalQueue)) - epochSummary.assetsReceived);
    }
```

</details>

Console output:

```js
forge test --mt test_lockedAssets -vv

Ran 1 test for test/RioLRTCoordinator.t.sol:RioLRTCoordinatorTest
[PASS] test_lockedAssets() (gas: 7817193)
Logs:
  Assets received:  100000000000000000000
  Contract balance: 100000000000000000000
  Assets received:  100947418285172903837
  Contract balance: 200947418285172903837
  Assets locked:    100000000000000000000

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 28.60ms
```

## Tool used

Manual Review

## Recommendation

```diff
File: RioLRTWithdrawalQueue.sol

+    function isCurrentEpochQueuedForSettlement(address asset) external view returns (bool) {
+        return _getEpochWithdrawals(asset, getCurrentEpoch(asset)).aggregateRoot != bytes32(0);
+    }
```

```diff
File: RioLRTCoordinator.sol

        uint256 sharesOwed = withdrawalQueue().getSharesOwedInCurrentEpoch(asset);
-       if (sharesOwed > 0) {
+       if (sharesOwed > 0 && !withdrawalQueue().isCurrentEpochQueuedForSettlement(asset)) {
            _processUserWithdrawalsForCurrentEpoch(asset, sharesOwed);
        }
```