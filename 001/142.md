Curved Fossilized Scallop

high

# RioLRTWithdrawalQueue:settleCurrentEpoch overrides assetsReceived

## Summary
During rebalances, the coordinator contract prioritizes transferring assets from the deposit pool, and if it is able to process all pending withdrawals, it settles the current epoch.
If it isn't, it queues a settlement for the current epoch that should be settled via EigenLayer. 
There are no checks to ensure settlements from that point on can only happen through settleEpochFromEigenLayer calls. Settling epochs from the deposit pool through the settleCurrentEpoch function does not handle cases when a settlement has already been queued and will override the amount of assets received for that epoch.
Suppose rebalance is called again and the depositPool has enough newly deposited assets so that it can cover the amount of shares owed in its entirety. In that case, it will wrongly transfer the whole amount of assets (the amount that corresponds to shares owed) to the withdrawal queue not taking into account the already processed amount, when it should've transferred only the difference.

## Vulnerability Detail
The following PoC attempts to settle the current epoch through rebalance and the depositPool does not have enough assets to cover it initially.
It will then queue a current epoch settlement (that will start EigenLayer withdrawals), have new deposits happen at the deposit pool, pass some time, and then call rebalance. 
The final assetsReceived amount displayed by the epochSummary will be correct. Still, the amount transferred out of the depositPool will be much bigger as it will require the settleCurrentEpoch to pay the withdrawal amounts in its entirety as if the first rebalance call hadn't already sent 32 ether to the withdrawal queue.
Paste the following code snippet at the RioLRTWithdrawalQueue.t.sol file:
```solidity
function test_pocV1() public {
        uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
        address operatorDelegator = reETH.operatorRegistry.getOperatorDetails(operatorId).delegator;

        // Deposit ETH, rebalance, verify the validator withdrawal credentials, and deposit again.
        uint256 depositAmount = ETH_DEPOSIT_SIZE - address(reETH.depositPool).balance;
        reETH.coordinator.depositETH{value: depositAmount}();

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
        uint40[] memory validatorIndices = verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);
        reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE}();

        // Request a withdrawal and rebalance.
        uint256 withdrawalAmount = ETH_DEPOSIT_SIZE + 1 ether;
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, withdrawalAmount);
        skip(reETH.coordinator.rebalanceDelay());

        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        uint256 withdrawalEpoch = reETH.withdrawalQueue.getCurrentEpoch(ETH_ADDRESS);

        // assert the user has already been able to withdraw 32 ether
        IRioLRTWithdrawalQueue.EpochWithdrawalSummary memory epochSummary =
            reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
        assertFalse(epochSummary.settled);
        assertEq(epochSummary.assetsReceived, withdrawalAmount - 1 ether);
        assertEq(epochSummary.shareValueOfAssetsReceived, withdrawalAmount - 1 ether);

        // Validate reETH total supply and process withdrawals.
        assertApproxEqAbs(reETH.token.totalSupply(), ETH_DEPOSIT_SIZE, 100);
        verifyAndProcessWithdrawalsForValidatorIndexes(operatorDelegator, validatorIndices);

        vm.deal(address(0x01), withdrawalAmount);
        vm.prank(address(0x01));
        reETH.coordinator.depositETH{value: withdrawalAmount}();
        skip(reETH.coordinator.rebalanceDelay());

        assertEq(reETH.withdrawalQueue.getSharesOwedInCurrentEpoch(ETH_ADDRESS), withdrawalAmount);

        // assert the depositPool is paying an additional 33 ether even though it owes only 1 ether
        assertEq(address(reETH.depositPool).balance, 33 ether);
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);
        assertEq(address(reETH.depositPool).balance, 0);

        epochSummary =
            reETH.withdrawalQueue.getEpochWithdrawalSummary(ETH_ADDRESS, withdrawalEpoch);
        assertTrue(epochSummary.settled);
        assertEq(epochSummary.assetsReceived, withdrawalAmount);
        assertEq(epochSummary.shareValueOfAssetsReceived, withdrawalAmount);
    }
```

Run the test with the following command:
```shell
forge test --match-test test_pocV1 -vvv
```

## Impact
Settling epochs through settleCurrentEpoch overrides assetsReceived instead of summing it with the amount of the already received assets during the queueCurrentEpochSettlement call. 
This makes the DepositPool contract transfer more assets to the WithdrawalQueue than required to settle withdrawals. 
As the Queue contract has no means to process those funds back to the system, those funds will be stuck at the contract and not be claimable.
## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L162
```solidity
function settleCurrentEpoch(address asset, uint256 assetsReceived, uint256 shareValueOfAssetsReceived)
        external
        onlyCoordinator
    {
    ...
    epochWithdrawals.assetsReceived = SafeCast.toUint120(assetsReceived);
    ...
}
```

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L104
```solidity
function claimWithdrawalsForEpoch(ClaimRequest calldata request) public returns (uint256 amountOut) {
...
	amountOut = userSummary.sharesOwed.mulDiv(epochWithdrawals.assetsReceived, epochWithdrawals.sharesOwed);
... 
}
```

## Tool used

Manual Review

## Recommendation
Make sure to sum assetsReceived to epochWithdrawals.assetsReceived at the settleCurrentEpoch function:
```solidity
epochWithdrawals.assetsReceived += SafeCast.toUint120(assetsReceived);
```

This will make the contract avoid sending more Ether than required to process withdrawals during settleCurrentEpoch calls.