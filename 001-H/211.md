Crazy Ginger Bison

high

# Epoch not increasing may cause the protocol to get stuck

## Summary
[The withdrawal epoch of the protocol is not increased after the epoch is completed](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L177C1-L209C6), which will cause the protocol to be stuck in a withdrawal epoch, affecting the proper functioning of the protocol.

## Vulnerability Detail
Under normal circumstances, the protocol will execute the [rebalance function](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) to help users withdraw. [The protocol will give priority to withdrawing from the depositpool, and if it is not enough, it will withdraw from EL.](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L247C1-L266C100)
When enough is withdrawn from the depositpool, the settleCurrentEpoch function will be called, and [currentEpochsByAsset[asset] will be correctly increased by one](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L166).
But if the withdrawal from the depositpool is not enough, [the queueCurrentEpochSettlement function](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L177C14-L177C41) will be called, which forgets to increment currentEpochsByAsset[asset] by one.
This may result in the following situations:
1. In epoch1, rebalance was called for withdrawal and the queueCurrentEpochSettlement function was used.
2. Then [the settleEpochFromEigenLayer function](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216) was called to complete the withdrawal. [The current epoch was settled](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L230) and the epoch is still in epoch1.
3. Subsequently, other users request withdrawals.
4. Since the current epoch has been settled, the rebalance function cannot run normally because the same epoch cannot be settled twice.

## Impact
The withdrawal epoch will be stuck, the rebalance function cannot run, and users may not be able to deposit and withdraw normally.

## Code Snippet
```solidity
    function queueCurrentEpochSettlement(
        address asset,
        uint256 assetsReceived,
        uint256 shareValueOfAssetsReceived,
        bytes32 aggregateRoot
    ) external onlyCoordinator {
        if (aggregateRoot == bytes32(0)) revert INVALID_AGGREGATE_WITHDRAWAL_ROOT();

        uint256 currentEpoch = getCurrentEpoch(asset);

        EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
        if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();
        if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
        if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();

        uint256 restakingTokensToBurn;
        if (assetsReceived > 0) {
            epochWithdrawals.assetsReceived = SafeCast.toUint120(assetsReceived);
            epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(shareValueOfAssetsReceived);

            restakingTokensToBurn = epochWithdrawals.amountToBurnAtSettlement.mulWad(
                shareValueOfAssetsReceived.divWad(epochWithdrawals.sharesOwed)
            );
            token.burn(restakingTokensToBurn);

            epochWithdrawals.amountToBurnAtSettlement -= restakingTokensToBurn;
        }
        epochWithdrawals.aggregateRoot = aggregateRoot;

        emit EpochQueuedForSettlementFromEigenLayer(
            currentEpoch, asset, assetsReceived, shareValueOfAssetsReceived, restakingTokensToBurn, aggregateRoot
        );
    }
```

## Tool used

Manual Review

## Recommendation
It is recommended to increase currentEpochsByAsset[asset] by one in the queueCurrentEpochSettlement function.
```solidity
        epochWithdrawals.aggregateRoot = aggregateRoot;

        currentEpochsByAsset[asset] += 1;

        emit EpochQueuedForSettlementFromEigenLayer(
            currentEpoch, asset, assetsReceived, shareValueOfAssetsReceived, restakingTokensToBurn, aggregateRoot
        );
```
