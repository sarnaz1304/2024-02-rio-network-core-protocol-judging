Energetic Turquoise Quail

high

# Withdrawals freeze because epoch is not updated in RioLRTWithdrawalQueue::settleEpochFromEigenLayer

## Summary

When withdrawals are requested from EigenLayer, RioLRTWithdrawalQueue doesn't increment the epoch count. This results in the epoch getting stuck, which blocks any future rebalance that involves the withdrawal of tokens.

## Vulnerability Detail

In RioLRTWithdrawalQueue, [settleEpochFromEigenLayer()](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216-L271) settles the current epoch when withdrawals from EigenLayer are completed, but it doesn't increment `currentEpochsByAsset[asset]` like in [settleCurrentEpoch](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L166):

```solidity
function settleEpochFromEigenLayer(
    address asset,
    uint256 epoch,
    IDelegationManager.Withdrawal[] calldata queuedWithdrawals,
    uint256[] calldata middlewareTimesIndexes
) external {
    EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, epoch);
    if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
    if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();
    if (epochWithdrawals.aggregateRoot == bytes32(0)) revert WITHDRAWALS_NOT_QUEUED_FOR_EPOCH();

    uint256 queuedWithdrawalCount = queuedWithdrawals.length;
    if (queuedWithdrawalCount != middlewareTimesIndexes.length) revert INVALID_MIDDLEWARE_TIMES_INDEXES_LENGTH();

    epochWithdrawals.settled = true;

    // forgefmt: disable-next-item
    uint256 sharesReceivedDuringSettlement = epochWithdrawals.sharesOwed - epochWithdrawals.shareValueOfAssetsReceived;

    // If not ETH, decrease the shares held for the asset. The decrease in queued ETH is
    // handled on a per-operator basis below.
    if (asset != ETH_ADDRESS) {
        assetRegistry().decreaseSharesHeldForAsset(asset, sharesReceivedDuringSettlement);
    }
    token.burn(epochWithdrawals.amountToBurnAtSettlement);

    uint256 balanceBefore = asset.getSelfBalance();

    address[] memory assets = asset.toArray();
    bytes32[] memory roots = new bytes32[](queuedWithdrawalCount);

    IDelegationManager.Withdrawal memory queuedWithdrawal;
    for (uint256 i; i < queuedWithdrawalCount; ++i) {
        queuedWithdrawal = queuedWithdrawals[i];

        roots[i] = _computeWithdrawalRoot(queuedWithdrawal);
        delegationManager.completeQueuedWithdrawal(queuedWithdrawal, assets, middlewareTimesIndexes[i], true);

        // Decrease the amount of ETH queued for withdrawal. We do not need to validate the staker as
        // the aggregate root will be validated below.
        if (asset == ETH_ADDRESS) {
            IRioLRTOperatorDelegator(queuedWithdrawal.staker).decreaseETHQueuedForUserSettlement(
                queuedWithdrawal.shares[0]
            );
        }
    }
    if (epochWithdrawals.aggregateRoot != keccak256(abi.encode(roots))) {
        revert INVALID_AGGREGATE_WITHDRAWAL_ROOT();
    }
    epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(epochWithdrawals.sharesOwed);

    uint256 assetsReceived = asset.getSelfBalance() - balanceBefore;
    epochWithdrawals.assetsReceived += SafeCast.toUint120(assetsReceived);

    emit EpochSettledFromEigenLayer(epoch, asset, assetsReceived);
}
```

This means that the next time that an epoch is tried to be settled or queued, the already settled epoch will be used and the call will revert, blocking any possible withdrawal:

```solidity
function settleCurrentEpoch(address asset, uint256 assetsReceived, uint256 shareValueOfAssetsReceived)
    external
    onlyCoordinator
{
    uint256 currentEpoch = getCurrentEpoch(asset);

    EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
    if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
    if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();
```

```solidity
function queueCurrentEpochSettlement(
    address asset,
    uint256 assetsReceived,
    uint256 shareValueOfAssetsReceived,
    bytes32 aggregateRoot
) external onlyCoordinator {
    if (aggregateRoot == bytes32(0)) revert INVALID_AGGREGATE_WITHDRAWAL_ROOT();

    uint256 currentEpoch = getCurrentEpoch(asset);

    EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
    if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();
    if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
    if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();
```

## Impact

Withdrawals get stuck after an epoch is settled through EigenLayer.

## Code Snippet

## Tool used

Manual Review

## Recommendation

Increment `currentEpochsByAsset[asset]` when withdrawals are settled from EigenLayer.
