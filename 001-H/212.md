Sweet Navy Dragonfly

high

# Rebalance will be blocked by queued withdrawal from EigenLayer

## Summary
Rebalance will be blocked by queued withdrawal from EigenLayer because **currentEpochsByAsset** won't  be updated.

## Vulnerability Detail
When [rebalance(...)](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121) function is called, if there are any outstanding withdrawals, protocol first tries to process user withdrawals for the provided asset by transferring available assets from the deposit pool.
```solidity
        (uint256 assetsSent, uint256 sharesSent) = depositPool().transferMaxAssetsForShares(
            asset,
            sharesOwed,
            address(withdrawalQueue_)
        );
```
If all pending withdrawals were paid from the deposit pool, current asset epoch will be settled and [currentEpochsByAsset](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L26) for the asset will be updated.
```solidity
        currentEpochsByAsset[asset] += 1;
```
If there are still remaining amount for withdrawal, protocol queues withdrawal from EigenLayer.
```solidity
        address strategy = assetRegistry().getAssetStrategy(asset);
        bytes32 aggregateRoot = OperatorOperations.queueWithdrawalFromOperatorsForUserSettlement(
            operatorRegistry(),
            strategy,
            sharesRemaining
        );
        withdrawalQueue_.queueCurrentEpochSettlement(asset, assetsSent, sharesSent, aggregateRoot);
```
However **currentEpochsByAsset** for the asset is not updated after withdrawal queued from EigenLayer, this is problematic because when rebalance is called again, there are still outstanding withdrawals in current epoch, protocol will try to queue withdrawal from EigenLayer again and transaction will be reverted at [L188](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L188) in **RioLRTWithdrawalQueue.sol**.
```solidity
        if (epochWithdrawals.aggregateRoot != bytes32(0)) revert WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH();
```
Please see the test codes:
```solidity
    function test_audit_rebalance_fail() public {
        address alice = makeAddr("Alice");
        address bob = makeAddr("Bob");
        vm.deal(alice, ETH_DEPOSIT_SIZE);
        vm.deal(bob, ETH_DEPOSIT_SIZE);

        // Ensure there is an operator to allocate to.
        addOperatorDelegators(reETH.operatorRegistry, address(reETH.rewardDistributor), 1);

        // Alice and Bob deposit 32 ether in total
        vm.prank(alice);
        reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE / 2}();
        vm.prank(bob);
        reETH.coordinator.depositETH{value: ETH_DEPOSIT_SIZE / 2}();

        // Push the balance into EigenLayer.
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Verify validator withdrawal credentials.
        verifyCredentialsForValidators(reETH.operatorRegistry, 1, 1);

        // Alice requests to withdraw 2 ether
        vm.prank(alice);
        reETH.coordinator.requestWithdrawal(ETH_ADDRESS, 2 ether);

        // Rebalance to queue Alice's withdrawal
        skip(reETH.coordinator.rebalanceDelay());
        vm.prank(EOA, EOA);
        reETH.coordinator.rebalance(ETH_ADDRESS);

        // Bob deposits 1 ether
        vm.prank(bob);
        reETH.coordinator.depositETH{value: 1 ether}();

        // Rebalance to process Bob's deposit
        skip(reETH.coordinator.rebalanceDelay());
        vm.prank(EOA, EOA);
        // Rebalance revert due to queued withdrawl
        vm.expectRevert(abi.encodeWithSelector(IRioLRTWithdrawalQueue.WITHDRAWALS_ALREADY_QUEUED_FOR_EPOCH.selector));
        reETH.coordinator.rebalance(ETH_ADDRESS);
    }
```

## Impact
User won't be able to deposit or withdraw.

## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121

## Tool used
Manual Review

## Recommendation
Update currentEpochsByAsset for asset after queueing settlement from EigenLayer.
```diff
    function queueCurrentEpochSettlement(
       ...
        epochWithdrawals.aggregateRoot = aggregateRoot;

+       currentEpochsByAsset[asset] += 1;
        emit EpochQueuedForSettlementFromEigenLayer(
            currentEpoch, asset, assetsReceived, shareValueOfAssetsReceived, restakingTokensToBurn, aggregateRoot
        );
    }
```