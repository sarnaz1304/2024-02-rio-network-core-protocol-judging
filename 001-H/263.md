Pet Denim Sardine

high

# RioLRTWithdrawalQueue.settleEpochFromEigenLayer does not increase the current epoch, resulting in a loop while rebalancing.

## Summary

Since epoch is not reset whenever `settleEpochFromEigenLayer()` is called, rebalancing will not work anymore and all subsequent withdrawals cannot be fulfilled.

## Vulnerability Detail

`settleEpochFromEigenLayer()` is used when there is not enough assets in the deposit pool to complete withdrawal, and so the user has to wait for the cooldown of eigenlayer to complete his withdrawal.

When `settleEpochFromEigenLayer()` is called, 
- The remaining LRT is burned.
- epochWithdrawals.settled is set to true, meaning the user can claim their tokens.
- The shares held is decreased from the asset registry

```solidity
    function settleEpochFromEigenLayer(
        address asset,
        uint256 epoch,
        IDelegationManager.Withdrawal[] calldata queuedWithdrawals,
        uint256[] calldata middlewareTimesIndexes
    ) external {
        EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, epoch);
        if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
        if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();
        if (epochWithdrawals.aggregateRoot == bytes32(0)) revert WITHDRAWALS_NOT_QUEUED_FOR_EPOCH();


        uint256 queuedWithdrawalCount = queuedWithdrawals.length;
        if (queuedWithdrawalCount != middlewareTimesIndexes.length) revert INVALID_MIDDLEWARE_TIMES_INDEXES_LENGTH();


>       epochWithdrawals.settled = true;


        // forgefmt: disable-next-item
        uint256 sharesReceivedDuringSettlement = epochWithdrawals.sharesOwed - epochWithdrawals.shareValueOfAssetsReceived;


        // If not ETH, decrease the shares held for the asset. The decrease in queued ETH is
        // handled on a per-operator basis below.
        if (asset != ETH_ADDRESS) {
>           assetRegistry().decreaseSharesHeldForAsset(asset, sharesReceivedDuringSettlement);
        }
>       token.burn(epochWithdrawals.amountToBurnAtSettlement);
```

These three main changes happens, indicating that all the assets should be withdrawable.

Note that `epochWithdrawals.sharesOwed` will not become zero. Instead, what usually happens is that the epoch will increase by 1, indicating a new epoch (as seen in `settleCurrentEpoch()`).

```solidity
    function settleCurrentEpoch(address asset, uint256 assetsReceived, uint256 shareValueOfAssetsReceived)
        external
        onlyCoordinator
    {
        uint256 currentEpoch = getCurrentEpoch(asset);


        EpochWithdrawals storage epochWithdrawals = _getEpochWithdrawals(asset, currentEpoch);
        if (epochWithdrawals.sharesOwed == 0) revert NO_SHARES_OWED_IN_EPOCH();
        if (epochWithdrawals.settled) revert EPOCH_ALREADY_SETTLED();


        epochWithdrawals.settled = true;
        epochWithdrawals.assetsReceived = SafeCast.toUint120(assetsReceived);
        epochWithdrawals.shareValueOfAssetsReceived = SafeCast.toUint120(shareValueOfAssetsReceived);


        token.burn(epochWithdrawals.amountToBurnAtSettlement);
>       currentEpochsByAsset[asset] += 1;


        emit EpochSettledFromDepositPool(currentEpoch, asset, assetsReceived);
    }
```

There is no epoch change in the `settleEpochFromEigenLayer()` function. When rebalance is called, `epochWithdrawals.sharesOwed` is checked. Since the epoch stays the same, the shares owed stays the same, which means that the function will call for a withdrawal in eigen layer again. 

```solidity
    function rebalance(address asset) external checkRebalanceDelayMet(asset) {
        if (!assetRegistry().isSupportedAsset(asset)) revert ASSET_NOT_SUPPORTED(asset);
        if (msg.sender != tx.origin) revert CALLER_MUST_BE_EOA();


        // Process any outstanding withdrawals using funds from the deposit pool and EigenLayer.
>       uint256 sharesOwed = withdrawalQueue().getSharesOwedInCurrentEpoch(asset);
        if (sharesOwed > 0) {
>           _processUserWithdrawalsForCurrentEpoch(asset, sharesOwed);
        }
```

Since sharesOwed never decreases to zero, the amount will be used again and `queueCurrentEpochSettlement()` will be called.

```solidity
    function _processUserWithdrawalsForCurrentEpoch(address asset, uint256 sharesOwed) internal {
        IRioLRTWithdrawalQueue withdrawalQueue_ = withdrawalQueue();
        (uint256 assetsSent, uint256 sharesSent) = depositPool().transferMaxAssetsForShares(
            asset,
            sharesOwed,
            address(withdrawalQueue_)
        );
        uint256 sharesRemaining = sharesOwed - sharesSent;


        // Exit early if all pending withdrawals were paid from the deposit pool.
        if (sharesRemaining == 0) {
            withdrawalQueue_.settleCurrentEpoch(asset, assetsSent, sharesSent);
            return;
        }


        address strategy = assetRegistry().getAssetStrategy(asset);
        bytes32 aggregateRoot = OperatorOperations.queueWithdrawalFromOperatorsForUserSettlement(
            operatorRegistry(),
            strategy,
            sharesRemaining
        );
>       withdrawalQueue_.queueCurrentEpochSettlement(asset, assetsSent, sharesSent, aggregateRoot);
    }
```


## Impact

Since `sharesOwed` never changes and the epoch never resets, `sharesOwed` will only increase as more users call `requestWithdrawal()`. Users can skip the withdrawal waiting time and steal other people's withdrawal since `epochWithdrawals.settled()` has been set to true for that current epoch.

There will also be a point where there is not enough assets to be withdrawn in EigenLayer, resulting in a revert when rebalancing.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTWithdrawalQueue.sol#L216-L231

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L121-L130

## Tool used

Manual Review

## Recommendation

Recommend calling `currentEpochsByAsset[asset] += 1;` in `settleEpochFromEigenLayer()` to reset the epoch.