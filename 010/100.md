Exotic Parchment Tadpole

medium

# convertToUnitOfAccountFromAsset() divides before multiplication

## Summary

RioLRTAssetRegistry._normalizeDecimals() converts `assets -> unit of account` and `unit of account -> asset`. Unfortunately in both RioLRTAssetRegistry.convertToUnitOfAccountFromAsset() and RioLRTAssetRegistry.convertFromUnitOfAccountToAsset() uses division against the `amount` passed to _normalizeDecimals(). This can result in loss of precision when calculating between assets and unit of accounts causing accounting inaccuracies.



## Vulnerability Detail

Let's look at RioLRTAssetRegistry.convertToUnitOfAccountFromAsset() as an example. As you can see below the function first uses division (`price * amount / priceScale`) before passing the return value to _normalizeDecimals():

```solidity
function convertToUnitOfAccountFromAsset(address asset, uint256 amount) public view returns (uint256) {
    if (asset == ETH_ADDRESS) {
        return amount;
    }
    address priceFeed = assetInfo[asset].priceFeed;
    uint256 price = getPrice(priceFeed);

    return _normalizeDecimals(
        // AUDIT: division before multiplication
        price * amount / priceScale, 
        assetInfo[asset].decimals, 
        priceFeedDecimals
    );
}
```

We can see how this function influences functionality like deposits with this stack trace:

- RioLRTAssetRegistry.convertToUnitOfAccountFromAsset()
    - RioLRTAssetRegistry.getTVLForAsset()
        - RioLRTAssetRegistry.getTVL()
            - RioLRTCoordinator.getTVL()
                - RioLRTCoordinator.convertFromUnitOfAccountToRestakingTokens()
                    - RioLRTCoordinator.convertFromAssetToRestakingTokens()
                        - RioLRTCoordinator.deposit()

Below is a forge test showing how _normalizeDecimals() can truncate decimal precision with the following specifications:

priceScale = 6
fromDecimals = 8
toDecimals = 12

```solidity
pragma solidity 0.8.23;

import {RioDeployer} from 'test/utils/RioDeployer.sol';

import "forge-std/console.sol";

contract RioLRTNormalizeDecimals is RioDeployer {

  function test_NormalizeDecimals() public {
    uint priceScale = 6;
    uint amount = 1_000_000_000_123_456_789;
    uint8 fromDecimals = 8;
    uint8 toDecimals = 12;

    uint returnValue = _normalizeDecimals(
      // Simulating division before multiplication like found in convertToUnitOfAccountFromAsset() and convertFromUnitOfAccountToAsset()
      amount / (10 ** priceScale), 
      fromDecimals, 
      toDecimals
    );

    assertEq(returnValue, 10000000001230000); // AUDIT: This should be 10000000001234567
  }

  function _normalizeDecimals(uint256 amount, uint8 fromDecimals, uint8 toDecimals) internal pure returns (uint256) {
      // No adjustment needed if decimals are the same.
      if (fromDecimals == toDecimals) {
          return amount;
      }
      // Scale down to match the target decimal precision.
      if (fromDecimals > toDecimals) {
          return amount / 10 ** (fromDecimals - toDecimals);
      }
      // Scale up to match the target decimal precision.
      return amount * 10 ** (toDecimals - fromDecimals);
  }
}
```

## Impact

By dividing a value before calling _normalizeDecimals(), precision loss can occur for when calculating `asset <-> unit of accounts` values. This can impact users who are depositing and withdrawing assets into the protocol as these values are used in various accounting methods like when calculating total value locked.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol?plain=1#L367-L378

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTAssetRegistry.sol?plain=1#L188-L210

## Tool used

Manual Review

## Recommendation

When calling _normalizeDecimals(), divide after the value is normalized.
