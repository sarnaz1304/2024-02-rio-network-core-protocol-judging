Gorgeous Basil Locust

high

# The operator utilization heap is not updated correctly when a operator is deactivated

## Summary

The deactivation of operators causes the operator utilization heap to be updated incorrectly, leading to errors in all calculations that depend on the heap.

## Vulnerability Detail

When a [operator is deactivated](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L148-L150) the [`heapStore`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorUtilizationHeap.sol#L68-L75) is not properly updated. Let's see why this is the case:

```solidity
// We have just one operator, so
{
    count: 1,
    operators: [{ id: 0, utilization: 0 }, { id: 1, utilization: 100 }]
}

heapStore[0] = 1

// We deactivate the operator and now we have
{
    count: 0,
    operators: [{ id: 0, utilization: 0 }]
}

// As self.count is 0, the for loop does not clear heapStore[0], so the value is still 1
for (uint8 i = 0; i < self.count;) {
    unchecked {
        heapStore.set(i, self.operators[i + 1].id);
        ++i;
    }
}
```

This causes that when [`getOperatorUtilizationHeapForStrategy`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L327) and [`getOperatorUtilizationHeapForETH`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L357) are executed, the wrong values are extracted.

This has several implications, including:

- When the deactivated operator is the last one in the heap, a revert in [`getOperatorUtilizationHeapForStrategy`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L346) and [`getOperatorUtilizationHeapForETH`](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L381) will occur, due to division by zero. This means that new operators cannot be added, the strategy/validator cap of a deactivated operator cannot be increased, and the rebalance function will revert, preventing the deposit of funds to EigenLayer and the withdrawal of funds from Rio (see PoC "Deactivate max heap operator").

- When the deactivated operator is not the last one in the heap, the wrong values for [allocation](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L343) and [deallocation](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L490) will be used. This can potentially block the allocation and deallocation of funds to/from the EigenLayer (see PoC "Deactivate non-max heap operator").

Given that the operator utilization heap is a critical part of the system, used the most important actions of the protocol, this is not an exhaustive list of the implications of the bug, which could potentially lead to more unexpected behavior in the system.

## Impact

New operators cannot be added, the strategy/validator cap of a deactivated operator cannot be increased, and the rebalance function will revert, preventing the deposit of funds to EigenLayer and the withdrawal of funds from Rio.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L148-L150

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/main/rio-sherlock-audit/contracts/utils/OperatorUtilizationHeap.sol#L68-L75

## Proof of concept

<details>

<summary>Deactivate max heap operator</summary>

Add the following test to `RioLRTOperatorRegistry.t.sol` and run `forge test --mt test_deactivateMaxHeapOperator`.

```solidity
function test_deactivateMaxHeapOperator() public {
    uint40 validatorCap = 100;
    address operator1 = address(1);
    address operator2 = address(2);

    vm.prank(operator1);
    delegationManager.registerAsOperator(
        IDelegationManager.OperatorDetails({
            earningsReceiver: address(reLST.rewardDistributor),
            delegationApprover: address(0),
            stakerOptOutWindowBlocks: 0
        }),
        metadataURI
    );

    vm.prank(operator2);
    delegationManager.registerAsOperator(
        IDelegationManager.OperatorDetails({
            earningsReceiver: address(reLST.rewardDistributor),
            delegationApprover: address(0),
            stakerOptOutWindowBlocks: 0
        }),
        metadataURI
    );

    // Add operator
    (uint8 operator1Id, ) = reLST.operatorRegistry.addOperator(
        IRioLRTOperatorRegistry.OperatorConfig({
            operator: operator1,
            initialManager: address(this),
            initialEarningsReceiver: address(this),
            initialMetadataURI: metadataURI,
            strategyShareCaps: defaultStrategyShareCaps,
            validatorCap: validatorCap
        })
    );

    // Deactivate the operator (as is the only one is max in heap)
    reLST.operatorRegistry.deactivateOperator(operator1Id);

    // Adding a new operator reverts
    vm.expectRevert(abi.encodeWithSignature("DivWadFailed()"));
    reLST.operatorRegistry.addOperator(
        IRioLRTOperatorRegistry.OperatorConfig({
            operator: operator2,
            initialManager: address(this),
            initialEarningsReceiver: address(this),
            initialMetadataURI: metadataURI,
            strategyShareCaps: defaultStrategyShareCaps,
            validatorCap: validatorCap
        })
    );

    // Reactivate the first operator
    reLST.operatorRegistry.activateOperator(operator1Id);
    // Setting the strategy share caps reverts
    vm.expectRevert(abi.encodeWithSignature("DivWadFailed()"));
    reLST.operatorRegistry.setOperatorStrategyShareCaps(operator1Id, defaultStrategyShareCaps);

    // Deposit cbETH
    cbETH.approve(address(reLST.coordinator), type(uint256).max);
    reLST.coordinator.deposit(CBETH_ADDRESS, 1000e18);
    // Rebalance reverts
    vm.prank(EOA, EOA);
    vm.expectRevert(abi.encodeWithSignature("DivWadFailed()"));
    reLST.coordinator.rebalance(CBETH_ADDRESS);
}
```

</details>

<details>

<summary>Deactivate non-max heap operator</summary>

Add the following test to `RioLRTOperatorRegistry.t.sol` and run `forge test --mt test_deactivateNonMaxHeapOperator`.

```solidity
function test_deactivateNonMaxHeapOperator() public {
    uint40 validatorCap = 100;
    address operator1 = address(1);
    address operator2 = address(2);
    address operator3 = address(3);

    vm.prank(operator1);
    delegationManager.registerAsOperator(
        IDelegationManager.OperatorDetails({
            earningsReceiver: address(reLST.rewardDistributor),
            delegationApprover: address(0),
            stakerOptOutWindowBlocks: 0
        }),
        metadataURI
    );

    vm.prank(operator2);
    delegationManager.registerAsOperator(
        IDelegationManager.OperatorDetails({
            earningsReceiver: address(reLST.rewardDistributor),
            delegationApprover: address(0),
            stakerOptOutWindowBlocks: 0
        }),
        metadataURI
    );

    vm.prank(operator3);
    delegationManager.registerAsOperator(
        IDelegationManager.OperatorDetails({
            earningsReceiver: address(reLST.rewardDistributor),
            delegationApprover: address(0),
            stakerOptOutWindowBlocks: 0
        }),
        metadataURI
    );

    // Add the first operator
    (uint8 operator1Id, ) = reLST.operatorRegistry.addOperator(
        IRioLRTOperatorRegistry.OperatorConfig({
            operator: operator1,
            initialManager: address(this),
            initialEarningsReceiver: address(this),
            initialMetadataURI: metadataURI,
            strategyShareCaps: defaultStrategyShareCaps,
            validatorCap: validatorCap
        })
    );

    // Add the second operator
    reLST.operatorRegistry.addOperator(
        IRioLRTOperatorRegistry.OperatorConfig({
            operator: operator2,
            initialManager: address(this),
            initialEarningsReceiver: address(this),
            initialMetadataURI: metadataURI,
            strategyShareCaps: defaultStrategyShareCaps,
            validatorCap: validatorCap
        })
    );

    // Deactivate the first operator
    reLST.operatorRegistry.deactivateOperator(operator1Id);

    // Add the third operator
    (uint8 operator3Id, ) = reLST.operatorRegistry.addOperator(
        IRioLRTOperatorRegistry.OperatorConfig({
            operator: operator3,
            initialManager: address(this),
            initialEarningsReceiver: address(this),
            initialMetadataURI: metadataURI,
            strategyShareCaps: defaultStrategyShareCaps,
            validatorCap: validatorCap
        })
    );

    // Deposit cbETH and rebalance
    cbETH.approve(address(reLST.coordinator), type(uint256).max);
    reLST.coordinator.deposit(CBETH_ADDRESS, 2000e18);
    vm.prank(EOA, EOA);
    reLST.coordinator.rebalance(CBETH_ADDRESS);

    // Funds cannot be deposited into operator 3, as the heap contains operator 2 twice
    uint256 balanceInDepositPool = cbETH.balanceOf(address(reLST.depositPool));
    uint256 capOpertor3 = reLST.operatorRegistry.getOperatorShareDetails(operator3Id, CBETH_STRATEGY).cap;
    uint256 allocationOperator3 = reLST.operatorRegistry.getOperatorShareDetails(operator3Id, CBETH_STRATEGY).allocation;
    assertEq(balanceInDepositPool, 1000e18);
    assertEq(capOpertor3, 1000e18);
    assertEq(allocationOperator3, 0);
}
```

</details>


## Tool used

Manual Review

## Recommendation

```diff
    function store(Data memory self, LibMap.Uint8Map storage heapStore) internal {
        for (uint8 i = 0; i < self.count;) {
            unchecked {
                heapStore.set(i, self.operators[i + 1].id);
                ++i;
            }
        }
    }
+   // If an element has been removed, index `self.count` will still have the old value. 
+   // We need to set it to 0.
+   heapStore.set(self.count, 0);
```