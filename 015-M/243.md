Powerful Pastel Albatross

high

# The calculation of `availableShare` in `RioLRTCoordinator.requestWithdrawal()` is incorrect.

## Summary

Because there is a time delay from calling `requestWithdrawal()` to calling `rebalance()`, and the exchange rates of strategy for each time could be different. This may lead to some gap between the values of available shares calculated in `requestWithdrawal()` and `rebalance()`. Thus, `rebalance()` will be reverted.

## Vulnerability Detail
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L111
```javascript
    function requestWithdrawal(address asset, uint256 amountIn) external checkWithdrawal(asset, amountIn) returns (uint256 sharesOwed) {
        // Determine the amount of shares owed to the withdrawer using the current exchange rate.
        sharesOwed = convertToSharesFromRestakingTokens(asset, amountIn);

        // If requesting ETH, reduce the precision of the shares owed to the nearest Gwei,
        // which is the smallest unit of account supported by EigenLayer.
        if (asset == ETH_ADDRESS) sharesOwed = sharesOwed.reducePrecisionToGwei();

        // Pull restaking tokens from the sender to the withdrawal queue.
        token.safeTransferFrom(msg.sender, address(withdrawalQueue()), amountIn);

        // Ensure there are enough shares to cover the withdrawal request, and queue the withdrawal.
111     uint256 availableShares = assetRegistry().convertToSharesFromAsset(asset, assetRegistry().getTotalBalanceForAsset(asset));
        if (sharesOwed > availableShares - withdrawalQueue().getSharesOwedInCurrentEpoch(asset)) {
            revert INSUFFICIENT_SHARES_FOR_WITHDRAWAL();
        }
        withdrawalQueue().queueWithdrawal(msg.sender, asset, sharesOwed, amountIn);
    }
```

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L245
```javascript
    function _processUserWithdrawalsForCurrentEpoch(address asset, uint256 sharesOwed) internal {
        IRioLRTWithdrawalQueue withdrawalQueue_ = withdrawalQueue();
        (uint256 assetsSent, uint256 sharesSent) = depositPool().transferMaxAssetsForShares(
            asset,
            sharesOwed,
            address(withdrawalQueue_)
        );
        uint256 sharesRemaining = sharesOwed - sharesSent;

        // Exit early if all pending withdrawals were paid from the deposit pool.
        if (sharesRemaining == 0) {
            withdrawalQueue_.settleCurrentEpoch(asset, assetsSent, sharesSent);
            return;
        }

        address strategy = assetRegistry().getAssetStrategy(asset);
        bytes32 aggregateRoot = OperatorOperations.queueWithdrawalFromOperatorsForUserSettlement(
            operatorRegistry(),
            strategy,
            sharesRemaining
        );
        withdrawalQueue_.queueCurrentEpochSettlement(asset, assetsSent, sharesSent, aggregateRoot);
    }
```

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol#L82
```javascript
    function transferMaxAssetsForShares(address asset, uint256 sharesRequested, address recipient)
        external
        onlyCoordinator
        returns (uint256, uint256)
    {
        uint256 poolBalance = asset.getSelfBalance();
82      uint256 poolBalanceShareValue = assetRegistry().convertToSharesFromAsset(asset, poolBalance);

        // Return early if the deposit pool has no balance or value for the given asset.
        if (poolBalance == 0 || poolBalanceShareValue == 0) {
            return (0, 0);
        }

        // If the deposit pool balance can cover the requested shares, transfer the equivalent assets.
        if (poolBalanceShareValue >= sharesRequested) {
            address strategy = assetRegistry().getAssetStrategy(asset);
            uint256 assetsSent = assetRegistry().convertFromSharesToAsset(strategy, sharesRequested);
            asset.transferTo(recipient, assetsSent);

            return (assetsSent, sharesRequested);
        }

        // Transfer the maximum possible assets from the deposit pool if it
        // cannot cover the requested shares. If withdrawing ETH, we reduce the
        // precision of the shares owed to the nearest Gwei, which is the smallest
        // unit of account supported by EigenLayer.
        if (asset == ETH_ADDRESS) {
            poolBalance = poolBalance.reducePrecisionToGwei();
            poolBalanceShareValue = poolBalanceShareValue.reducePrecisionToGwei();
        }
        asset.transferTo(recipient, poolBalance);

        return (poolBalance, poolBalanceShareValue);
    }
```

In order to simplify our consideration, some following assumptions are made.
    Assumption 1: There was no corresponding shares left before calling `rebalance()`.
    Assumption 2: `deposit()` was called before calling `requestWithdrawal()`.
    Assumption 3: `requestWithdrawal()` and `deposit()` were called exactly once before calling `rebalance()`.
    Assumption 4: `sharesOwed == availableShares`

From assumption 2, `availableShares` were calculated some hours before `poolBalanceShareValue` is calculated. Both of them were calculated by using `underlyingToSharesView()`. If the exchange rate of the corresponding strategy changed during this period, `poolBalanceShareValue` could get smaller than `availableShares`.
So, `sharesOwed == availableShares && availableShares > poolBalanceShareValue` would hold from assumption 3 and 4.
However, `poolBalanceShareValue` must be greater than or equal to `sharesOwed` from assumption 1.
As a result, `rebalance()` would be reverted.

## Impact

`rebalance()` would be reverted unreasonably and users cannot take back their assets in time. The time is not predictable and users should wait until the assets are deposited as much as needed.

## Code Snippet

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L111

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L245

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTDepositPool.sol#L82

## Tool used

Manual Review

## Recommendation

https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTCoordinator.sol#L111
```diff
        
    function requestWithdrawal(address asset, uint256 amountIn) external checkWithdrawal(asset, amountIn) returns (uint256 sharesOwed) {
        // Determine the amount of shares owed to the withdrawer using the current exchange rate.
        sharesOwed = convertToSharesFromRestakingTokens(asset, amountIn);

+       uint256 availableShares
        // If requesting ETH, reduce the precision of the shares owed to the nearest Gwei,
        // which is the smallest unit of account supported by EigenLayer.
        if (asset == ETH_ADDRESS) {
            sharesOwed = sharesOwed.reducePrecisionToGwei();
+           availableShares = assetRegistry().convertToSharesFromAsset(asset, assetRegistry().getTotalBalanceForAsset(asset));
+       }
+       else {
+           availableShares = assetRegistry().getAssetSharesHeld(asset);
+       } 

        // Pull restaking tokens from the sender to the withdrawal queue.
        token.safeTransferFrom(msg.sender, address(withdrawalQueue()), amountIn);

        // Ensure there are enough shares to cover the withdrawal request, and queue the withdrawal.
-       uint256 availableShares = assetRegistry().convertToSharesFromAsset(asset, assetRegistry().getTotalBalanceForAsset(asset));
        if (sharesOwed > availableShares - withdrawalQueue().getSharesOwedInCurrentEpoch(asset)) {
            revert INSUFFICIENT_SHARES_FOR_WITHDRAWAL();
        }
        withdrawalQueue().queueWithdrawal(msg.sender, asset, sharesOwed, amountIn);
    }
```