Sunny Licorice Pheasant

medium

# Front-running with a reduced number of validators to deallocate from another operator

## Summary

Under certain conditions, the Operator may ensure that the funds instead of being deallocated from him, will be deallocated from another Operator whose actual utilization rate is lower by reporting a false (smaller) amount of exited validators.

## Vulnerability Detail

To understand this vulnerability following functions must be understood ```reportOutOfOrderValidatorExits```, ```deallocateETHDeposits``` .

```reportOutOfOrderValidatorExits``` - function to inform Rio that the validators have been exited (especially when an operator exited some validators without the protocol instructing them to do so). https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L310

```solidity
    /// @notice Reports validator exits that occur prior to instruction by the protocol.
    /// @param operatorId The operator's ID.
    /// @param fromIndex The index of the first validator to report.
    /// @param validatorCount The number of validators to report.
    function reportOutOfOrderValidatorExits(uint8 operatorId, uint256 fromIndex, uint256 validatorCount) external {
        OperatorDetails storage operator = s.operatorDetails[operatorId];
        OperatorValidatorDetails memory validators = operator.validatorDetails;

        if (fromIndex < validators.exited || fromIndex + validatorCount > validators.deposited) revert INVALID_INDEX();

        bytes memory exitedPubKeyBatch = ValidatorDetails.allocateMemoryForPubKeys(validatorCount);
        VALIDATOR_DETAILS_POSITION.loadValidatorDetails(
            operatorId, fromIndex, validatorCount, exitedPubKeyBatch, new bytes(0), 0
        );

        // Verify that all validators have exited.
        IEigenPod pod = IRioLRTOperatorDelegator(operator.delegator).eigenPod();
        bytes memory publicKey = Memory.unsafeAllocateBytes(BLS_PUBLIC_KEY_LENGTH);
        for (uint256 i = 0; i < validatorCount; ++i) {
            Memory.copyBytes(exitedPubKeyBatch, publicKey, i * BLS_PUBLIC_KEY_LENGTH, 0, BLS_PUBLIC_KEY_LENGTH);
            if (pod.validatorStatus(_hashValidatorBLSPubKey(publicKey)) != IEigenPod.VALIDATOR_STATUS.WITHDRAWN) {
                revert VALIDATOR_NOT_EXITED();
            }
        }
```

```deallocateETHDeposits``` - function that deallocates a specified amount of ETH deposits from the operators with the **highest utilization**. Can be called only by Coordinator. https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L541

```solidity
    // forgefmt: disable-next-item
    /// @notice Deallocates a specified amount of ETH deposits from the operators with the highest utilization.
    /// @param depositsToDeallocate The amount of deposits to deallocate (32 ETH each)
    function deallocateETHDeposits(uint256 depositsToDeallocate) external onlyCoordinator returns (uint256 depositsDeallocated, OperatorETHDeallocation[] memory deallocations) {
        deallocations = new OperatorETHDeallocation[](s.activeOperatorCount);

        OperatorUtilizationHeap.Data memory heap = s.getOperatorUtilizationHeapForETH();
        if (heap.isEmpty()) revert NO_AVAILABLE_OPERATORS_FOR_DEALLOCATION();

        uint256 deallocationIndex;
        uint256 remainingDeposits = depositsToDeallocate;

        bytes memory pubKeyBatch;
        while (remainingDeposits > 0) {
            uint8 operatorId = heap.getMax().id;

            OperatorDetails storage operator = s.operatorDetails[operatorId];
            OperatorValidatorDetails memory validators = operator.validatorDetails;
            uint256 activeDeposits = validators.deposited - validators.exited;

            // Exit early if the operator with the highest utilization rate has no active deposits,
            // as no further deallocations can be made.
            if (activeDeposits == 0) break;

            // Each deallocation will trigger the withdrawal of a 32 ETH deposit. The specific validators
            // to withdraw from are chosen by the software run by the operator.
            uint256 newDepositDeallocation = FixedPointMathLib.min(activeDeposits, remainingDeposits);
            pubKeyBatch = ValidatorDetails.allocateMemoryForPubKeys(newDepositDeallocation);
            VALIDATOR_DETAILS_POSITION.loadValidatorDetails(
                operatorId, validators.exited, newDepositDeallocation, pubKeyBatch, new bytes(0), 0
            );

            operator.validatorDetails.exited += uint40(newDepositDeallocation);

            deallocations[deallocationIndex] = OperatorETHDeallocation(operator.delegator, newDepositDeallocation);
            remainingDeposits -= newDepositDeallocation;

            uint256 updatedAllocation = activeDeposits - newDepositDeallocation;
            heap.updateUtilization(heap.getMaxIndex(), updatedAllocation.divWad(validators.cap));

            emit ETHDepositsDeallocated(operatorId, newDepositDeallocation, pubKeyBatch);

            unchecked {
                ++deallocationIndex;
            }
        }
        depositsDeallocated = depositsToDeallocate - remainingDeposits;

        heap.store(s.activeOperatorsByETHDepositUtilization);

        // Shrink the array length to the number of deallocations made.
        if (deallocationIndex < s.activeOperatorCount) {
            assembly {
                mstore(deallocations, deallocationIndex)
            }
        }
    }
```

Now, imagine two operators, A and B, both with a cap of 15 validators. Initially, both have 10 deposited validators, but their exited counts differ (we consider here exited validators without the protocol instructing them to do so):
* Operator A has 2 exited validators.
* Operator B has 3 exited validators.

```activeDeposits``` are calculated as follows(https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L378):
```solidity
    activeDeposits = validators.deposited - validators.exited;
```

```Utilization``` is calculated as follows (https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/OperatorRegistryV1Admin.sol#L381):
```solidity
    utilization: activeDeposits.divWad(validators.cap)
```

For **Operator A (operatorId=1)**, if they decide to inform about exited validators through ```reportOutOfOrderValidatorExits``` with the following parameters ```(operatorId=1, fromIndex=1, validatorCount=2)``` their utilization will change to the following:
* activeDeposits = 10 - 2 = 8
* Utilization = 8 / 15 = ~53%

For **Operator B (operatorId=2)**, if they decide to inform about exited validators through ```reportOutOfOrderValidatorExits``` with the following parameters ```(operatorId=2, fromIndex=1, validatorCount=3)``` their utilization will change to the following:
* activeDeposits = 10 - 3 = 7
* Utilization = 7 / 15 = ~46%

In this case, when the ```deallocateETHDeposits``` is called, deallocation will affect the Operator A (as the one with highest utilization).

However, as the ```reportOutOfOrderValidatorExits``` can be called be anyone with any parameters. Operator A can front-run Operator B reporting transaction with smaller ```validatorCount``` e.g. 1 instead of 3.

That means, the transaction would execute with the following values:
```(operatorId=2, fromIndex=1, validatorCount=1)```

The following check passes (https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L314).
 
```solidity
if (fromIndex < validators.exited || fromIndex + validatorCount > validators.deposited) revert INVALID_INDEX();
```

As first condition is satisfied:
```(fromIndex = 1) > (validators.exited = 0)```
and second condition is satisfied as well:
```(fromIndex = 1) + (validatorCount = 1) < validators.deposited = 10```



The number of ```validators.exited = 0``` increments by the number of ```validatorCount = 1```, so now its ```validators.exited = 1```.

The front-runned **Operator B** transaction with the values ```(operatorId=2, fromIndex=1, validatorCount=3)``` now will revert, because the first condition will not be satisfied.

Check fails, as first condition is not satisfied:
```(fromIndex = 1) > (validators.exited = 1)```

In this scenario utilization will change to the following:

For **Operator A (operatorId=1)**, the same as previously.
* activeDeposits = 10 - 2 = 8
* Utilization = 8 / 15 = ~53%

For **Operator B (operatorId=2)**, because they were front-runned with the values provided by Operator A ```(operatorId=2, fromIndex=1, validatorCount=1)``` their utilization will increase to the following:
* activeDeposits = 10 - 1 = 9
* Utilization = 9 / 15 = ~60%

Now, when the ```deallocateETHDeposits``` is called, deallocation will not affect the **Operator A**, but **Operator B** (as now they are the one with highest utilization).

**The successful attack allowed Operator A to avoid deallocation and continue to earn rewards and deallocate from Operator B.**

*Conditions that need to be satisfied:*
1. *An operator exited some validators without the protocol instructing them to do so.*
2. *The attack should be performed before the ```deallocateETHDeposits```. Otherwise, the victim has the option of reporting the actual number of exited validators via additional transaction with updated parameters. (However, the attacker can front-run transaction again. The higher ```validatorCount``` the more times the attacker can front-run it).*

## Impact
**MEDIUM**, as the attack can only be performed when specific conditions are met.
The successful attack allowed Operator A to avoid deallocation and continue to earn rewards and deallocate from Operator B.

## Code Snippet
https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTOperatorRegistry.sol#L310

## Tool used

Manual Review

## Recommendation

The operator should be able to report only their own exited validators.