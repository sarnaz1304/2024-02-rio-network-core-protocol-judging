Straight Neon Blackbird

high

# RioLTROperatorDelegator.receive always reverts so it is unable to receive partial withdrawals and any amount in excess of 32 ETH for full withdrawals

## Summary

The `RioLRTOperatorDelegator.receive` function always fails due to out of gas. `RioLRTOperatorDelegator.receive` is a function to receive partial withdrawals and any amount in excess of 32 ETH for full withdrawals from EigenLayer. As the transaction is reverted, the RioLTROperatorDelegator cannot receive these.

## Vulnerability Detail

When the RioLRTOperatorDelegator contract receives ETH, it forwards it to the RioLRTRewardDistributor using the `Asset.transferETH` function.

```solidity
receive() external payable {
@>  address(rewardDistributor()).transferETH(msg.value);
}
```

The `Asset.transferETH` function sends ETH with 10,000 gas.

```solidity
function transferETH(address recipient, uint256 amount) internal {
@>  (bool success,) = recipient.call{value: amount, gas: 10_000}('');
    if (!success) {
        revert ETH_TRANSFER_FAILED();
    }
}
```

`RioLRTRewardDistributor.receive` distributes ETH to the treasury, operatorRewardPool, and depositPool using `Asset.transferETH` again, also with 10,000 gas. However, since `RioLRTRewardDistributor.receive` is called with 10,000 gas, it does not have enough gas to make `Asset.transferETH` calls. Therefore, the transaction always fails due to out of gas.

```solidity
receive() external payable {
    uint256 value = msg.value;

    uint256 treasuryShare = value * treasuryETHValidatorRewardShareBPS / MAX_BPS;
    uint256 operatorShare = value * operatorETHValidatorRewardShareBPS / MAX_BPS;
    uint256 poolShare = value - treasuryShare - operatorShare;

@>  if (treasuryShare > 0) treasury.transferETH(treasuryShare);
@>  if (operatorShare > 0) operatorRewardPool.transferETH(operatorShare);
@>  if (poolShare > 0) address(depositPool()).transferETH(poolShare);

    emit ETHValidatorRewardsDistributed(treasuryShare, operatorShare, poolShare);
}
```

This is PoC. Add it to the RioLRTOperatorDelegator.t.sol file and run it.

```solidity

function testPoCOutOfGas() public {
    uint8 operatorId = addOperatorDelegator(reETH.operatorRegistry, address(reETH.rewardDistributor));
    RioLRTOperatorDelegator delegatorContract =
        RioLRTOperatorDelegator(payable(reETH.operatorRegistry.getOperatorDetails(operatorId).delegator));

    (bool success,) = address(delegatorContract).call{value: 1.123 ether}('');
    assertFalse(success);
}

```

## Impact

Unable to receive partial withdrawals and any amount in excess of 32 ETH for full withdrawals. These are locked in EigenLayer and cannot be retrieved.

## Code Snippet

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/Asset.sol#L42](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/utils/Asset.sol#L42)

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTOperatorDelegator.sol#L245](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTOperatorDelegator.sol#L245)

[https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTRewardDistributor.sol#L82-L94](https://github.com/sherlock-audit/2024-02-rio-network-core-protocol/blob/4f01e065c1ed346875cf5b05d2b43e0bcdb4c849/rio-sherlock-audit/contracts/restaking/RioLRTRewardDistributor.sol#L82-L94)

## Tool used

Manual Review

## Recommendation

Instead of using `Asset.transferETH` in `RioLTROperatorDelegator.receive`, use `call` to forward sufficient gas.

```diff
receive() external payable {
-   address(rewardDistributor()).transferETH(msg.value);
+   (bool success,) = address(rewardDistributor()).call{value: msg.value}('');
+   if (!success) revert ETH_TRANSFER_FAILED();
}
```